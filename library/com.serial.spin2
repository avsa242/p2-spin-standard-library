{
----------------------------------------------------------------------------------------------------
    Filename:       com.serial.spin2
    Description:    Low-level UART serial driver
    Author:         Jesse Burt
    Started:        Jun 28, 2019
    Updated:        Oct 8, 2024
    Copyright (c) 2024 - See end of file for terms of use.
----------------------------------------------------------------------------------------------------

    NOTE: This is based on SmartSerial.spin,
        originally by Eric Smith.
}

CON

    { I/O configuration - these can be overridden by the parent object }
    RX_PIN      = 63
    TX_PIN      = 62
    SER_BAUD    = 2_000_000


    { TX/RX buffers size (_must_ be a power of 2) }
#ifndef TXBUFF_SZ
    TXBUFF_SZ   = 64
#endif
#ifndef RXBUFF_SZ
    RXBUFF_SZ   = 64
#endif


VAR

    long _rx_pin, _tx_pin
    long _buff_cog
    long _bit_time

OBJ

#ifdef BUFFERED_IO
    { ring buffers }
    txbuff: "memory.ring-buffer" | RBUFF_SZ=TXBUFF_SZ
    rxbuff: "memory.ring-buffer" | RBUFF_SZ=RXBUFF_SZ
#endif


PUB start = init_def
PUB init_def(bps=SER_BAUD): status
' Start using P2 default serial pins
    return init(RX_PIN, TX_PIN, 0, bps)


con WORDLEN         = 8-1                       ' serial word length, in bits-1
PUB startrxtx = init
PUB init(rxpin, txpin, mode, bps=-1): status | bitperiod, bit_mode
' Start using custom I/O pins and configuration
'   rxpin: receive pin
'   txpin: transmit pin
'   mode: unused; currently for compatibility only
'   bps: bits per second/baud (-1 to attempt auto-baud)
    if ( bps == -1 )
        bitperiod := autobaud(rxpin)            ' try to detect link speed if unspecified
    else
        bitperiod := (clkfreq / bps)

    _rx_pin := rxpin
    _tx_pin := txpin
    _bit_time := bitperiod / (clkfreq / 1_000_000)
    bit_mode := ((bitperiod << 16) + WORDLEN)

    { start a buffer I/O manager in a new cog if buffered I/O is requested at build time,
        otherwise just return the parent cog ID+1 }
#ifdef BUFFERED_IO
    _buff_cog :=    ( cogspin(  NEWCOG, ...
                                cog_buffered_io(txpin, rxpin, bit_mode), ...
                                @_buffer_stack) + 1 )
    waitms(1)
    return _buff_cog
#else


    if ( lookdown(txpin: 0..63) )
        pinstart(txpin, (P_ASYNC_TX | P_OE), bit_mode, 0)

    if ( lookdown(rxpin: 0..63) )
        pinstart(rxpin, P_ASYNC_RX, bit_mode, 0)

    return (cogid() + 1)
#endif

pub stop()
#ifdef BUFFERED_IO
    if ( _buff_cog )
        cogstop(_buff_cog)
        txbuff.flush()
        rxbuff.flush()


pub fifo_rx_bytes(): b
' Get the number of bytes waiting in the receive buffer
'   Returns: count of bytes
    return rxbuff.available()


PUB flush_rx()
' Flush receive buffer
    rxbuff.flush()


PUB flush_tx()
' Flush transmit buffer
    txbuff.flush()


PUB rx = getchar
PUB charin = getchar
PUB getchar(): ch
' Receive a single byte (blocking)
'   Returns: $00..$FF
    repeat
        ch := rxbuff.getchar()
    while ( ch == -1)


PUB rxcheck = getchar_noblock
PUB rx_check = getchar_noblock
PUB getchar_noblock(): ch
' Check if byte received (non-blocking)
'   Returns:
'       -1 if no byte, otherwise byte
    return rxbuff.getchar()


PUB getchar_timeout(tmout=10): ch | t
' Receive a single byte (blocking, with timeout)
'   tmout:  maximum time to wait for a valid character, in milliseconds (default: 10)
'   Returns:
'       0..255 on success
'       -1 on timeout
    t := getct()                                ' snapshot of current system counter
    repeat
        ch := getchar_noblock()                 ' check for a valid character
        if ( ch >= 0 )                          ' got one
            return ch
    until ( (getct()-t) > ( (clkfreq/1000) * tmout) )


PUB tx = putchar
PUB char = putchar
PUB putchar(ch): s
' Send single-byte character (blocking)
'   ch: character (ASCII byte value) to send
    repeat
        s := txbuff.putchar(ch)
    while ( s == -1 )                           ' keep trying until there's space in the TX buffer
    waitus( _bit_time * (8+1+1) )               ' wait for the char to be sent to avoid corruption


PUB putchar_noblock(ch): s
' Send single-byte character (non-blocking)
'   ch: character (ASCII byte value) to send
    return txbuff.putchar(ch)


#else


pub available(): s
' Flag indicating new data is available to read
    return ( pinr(_rx_pin) )


PUB tx = putchar
PUB char = putchar
PUB putchar(ch)
' Send single-byte character (blocking)
'   ch: character (ASCII byte value) to send
    wypin(_tx_pin, ch)
    flush_tx()


PUB rx = getchar
PUB charin = getchar
PUB getchar(): ch
' Receive a single byte (blocking)
'   Returns: $00..$FF
    repeat
        ch := rx_check()
    while (ch == -1)


PUB rxcheck = getchar_noblock
PUB rx_check = getchar_noblock
PUB getchar_noblock(): ch
' Check if byte received (non-blocking)
'   Returns:
'       -1 if no byte, otherwise byte
    ch := -1
    if ( pinr(_rx_pin) )
        return (rdpin(_rx_pin) >> 24)


PUB getchar_timeout(tmout=10): ch | t
' Receive a single byte (blocking, with timeout)
'   tmout:  maximum time to wait for a valid character, in milliseconds (default: 10)
'   Returns:
'       0..255 on success
'       -1 on timeout
    t := getct()                                ' snapshot of current system counter
    repeat
        ch := getchar_noblock()                 ' check for a valid character
        if ( ch >= 0 )                          ' got one
            return ch
    until ( (getct()-t) > ( (clkfreq/1000) * tmout) )


PUB flush_tx()
' Flush transmit buffer
    repeat until pinr(_tx_pin)


#endif


#ifdef BUFFERED_IO
VAR long _buffer_stack[50]
PUB cog_buffered_io(txpin, rxpin, bit_mode)

    if ( lookdown(txpin: 0..63) )
        pinstart(txpin, (P_ASYNC_TX | P_OE), bit_mode, 0)

    if ( lookdown(rxpin: 0..63) )
        pinstart(rxpin, P_ASYNC_RX, bit_mode, 0)

    repeat
        if ( pinr(rxpin) )                      ' if there's a char waiting, buffer it
            rxbuff.putchar( rdpin(rxpin) >> 24 )
        if ( txbuff.available() )               ' if there's a buffered char waiting, send it
            wypin(txpin, txbuff.getchar())
            repeat until not (rdpin(txpin) & 1) ' wait for char to finish sending (xxx really needed?)

#endif


PRI autobaud(pin): bitperiod | a, b, c, port, mask

    pinf(pin)                                   ' set pin as input
    waitx(1000)                                 ' wait to settle
    if pin >= 32
        port := 1
        mask := 1 << (pin-32)
    else
        port := 0
        mask := 1 << pin

' Code for detecting HI->LO->HI->LO transition
' We time the length of the first 1 bit sequence in the character,
'   then the next 0 bit sequence
' We assume one of these is the correct length
' This works if the character sent is space ($20), which has 1 bit high
'   or CR ($0d) which has 1 bit low after the high bit
'
    org
        test    port, #1 wc                     ' Set C to distinguish INA/OUTA
        test    port, #2 wz                     ' set Z (match on =)

        setpat  mask, #0                        ' Wait for pin lo (start bit)
        waitpat
        setpat  mask, mask                      ' Wait for pin hi (first 1)
        waitpat
        getct   a
        setpat  mask, #0                        ' Wait for pin lo again (following 0)
        waitpat
        getct   b
        setpat  mask,mask                       ' Wait for pin hi again (end of 0 sequence)
        waitpat
        getct   c
    end
    bitperiod := (b - a)                        ' Length of first 1 bit sequence
    c := (c - b)                                ' Length of following 0
    if (c < bitperiod)
        bitperiod := c

' Now want to wait for idle
    waitx(16 * bitperiod)
    return bitperiod

DAT
{
Copyright 2024 Jesse Burt

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
associated documentation files (the "Software"), to deal in the Software without restriction,
including without limitation the rights to use, copy, modify, merge, publish, distribute,
sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or
substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT
NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT
OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}

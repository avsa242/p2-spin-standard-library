{
    --------------------------------------------
    Filename: NRF24L01-RX-LED.spin2
    Author: Jesse Burt
    Description: Wireless control of a Smart LED (receiver)
        Uses:
        * nRF24L01+
        * 1x Smart LED (NeoPixel)
    Copyright (c) 2022
    Started Aug 29, 2022
    Updated Nov 13, 2022
    See end of file for terms of use.
    --------------------------------------------
}

CON

    _clkfreq    = cfg#_clkfreq_def
    _xtlfreq    = cfg#_xtlfreq

' -- User-modifiable constants
    LED1        = cfg.LED1
    SER_BAUD    = 2_000_000

    { SPI configuration - nRF24L01+ }
    CE_PIN      = 20
    CS_PIN      = 21
    SCK_PIN     = 18
    MOSI_PIN    = 16                            ' MOSI and MISO must be within
    MISO_PIN    = 17                            '   3 pins +/- of SCK
    SCK_FREQ    = 10_000_000                    ' 10_000_000 max

    { Smart LED configuration }
    LED_PIN     = 8
    LED_MODEL   = led.WS2812
    COLOR       = RED                           ' choose from below
' --

    RED         = 24
    GREEN       = 16
    BLUE        = 8
    WHITE       = 0                             ' not all models have this

OBJ

    ser  : "com.serial.terminal.ansi"
    cfg  : "boardcfg.p2eval"
    nrf24: "wireless.transceiver.nrf24l01"
    led  : "display.led.smart"

VAR

    long _led

PUB main() | bright, last, payload

    setup()

    bright := 0
    last := 0

' -- User-modifiable settings (NOTE: These settings _must_ match the transmit side) }
    nrf24.channel(2)                            ' 0..125 (2.400 .. 2.525GHz)

    { set syncword (note: order in string() is LSB, ..., MSB) }
    nrf24.set_syncwd(string($e7, $e7, $e7, $e7, $e7))
' --

    ser.clear()
    ser.pos_xy(0, 0)
    ser.printf(@"Receive mode (channel %d)\n\r", nrf24.channel())

    repeat
        { clear local buffer and wait until a payload is received }
        payload := 0
        repeat until nrf24.payld_rdy()
        nrf24.rx_payld(4, @payload)

        { Only allow changes of up to +/- 5 from the last encoder reading received, so
            possible transmission glitches causing reception of bad values like 256
            don't cause the LED to suddenly go full-brightness.
            Also, clamp the brightness to the range 0..255 }
        if (abs(last-payload) <= 5)
            last := payload
            bright := 0 #> (bright + payload) <# 255
            led.plot(0, 0, bright << COLOR)
            led.show()

        { clear interrupt and receive buffer for next loop }
        nrf24.int_clear(nrf24.PAYLD_RDY)
        nrf24.flush_rx()

PUB setup()

    ser.start(SER_BAUD)
    ser.clear()
    if nrf24.startx(CE_PIN, CS_PIN, SCK_PIN, MOSI_PIN, MISO_PIN, SCK_FREQ)
        ser.strln(@"nRF24L01+ driver started")
        nrf24.preset_rx2m_noaa()                ' 2Mbps, No Auto-Ack
        nrf24.crc_check_ena(true)
        nrf24.crc_len(2)
        nrf24.payld_len(4)
    else
        ser.strln(@"nRF24L01+ driver failed to start - halting")
        repeat

    led.startx(LED_PIN, 1, 1, LED_MODEL, @_led)
    led.clear()
    led.show()

DAT
{
Copyright 2022 Jesse Burt

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
associated documentation files (the "Software"), to deal in the Software without restriction,
including without limitation the rights to use, copy, modify, merge, publish, distribute,
sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or
substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT
NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT
OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}


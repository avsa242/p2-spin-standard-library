{
    --------------------------------------------
    Filename: Mic-to-Headphones-Demo.spin2
    Description: Demo that captures audio from a mic and
        outputs it directly to attached headphones
    Author: Jesse Burt
    Started: May 16, 2020
    Updated: Jul 9, 2020
    See end of file for terms of use.
    --------------------------------------------
}
CON

     _xtlfreq       = cfg._xtlfreq
     _clkfreq       = cfg._clkfreq_def

' User-modifiable constants
    LED             = cfg.LED1
    SER_RX          = cfg.SER_RX
    SER_TX          = cfg.SER_TX
    SER_BAUD        = 2_000_000

    ADC_PIN         = 48
    AUDIO_LPIN      = 6
    AUDIO_RPIN      = 7
    SAMPLE_RATE     = 8000
' ---

    ADC_PERIOD      = _clkfreq / SAMPLE_RATE
    AUDIO_PERIOD    = _clkfreq / SAMPLE_RATE

OBJ

    ser         : "com.serial.terminal.ansi"
    cfg         : "core.con.boardcfg.p2eval"
    io          : "io"
    audio       : "signal.dac.audio"
    mic         : "signal.adc.audio"

VAR

    long _audio_stack[50]
    long _audio_cog, _adc_sample

PUB Main() | adc_min, adc_max

    setup()
    ser.hidecursor()

    adc_max := 0
    repeat 500                                              ' Gather some samples to get a
        adc_min := _adc_sample                              '   "real" minimum

    repeat
        adc_min := _adc_sample <# adc_min
        adc_max := adc_max #> _adc_sample
        ser.position(0, 4)
        ser.printf("ADC current: %d\nmin: %d\nmax: %d", _adc_sample, adc_min, adc_max)

PUB cog_Audio()

    mic.start(ADC_PIN, ADC_PERIOD, 1)
    audio.start(AUDIO_LPIN, AUDIO_RPIN, AUDIO_PERIOD)
    _audio_cog := cogid()

    repeat
        _adc_sample := mic.adcsample16()
        audio.output16(_adc_sample)

PUB Setup()

    repeat until ser.startrxtx (SER_RX, SER_TX, 0, SER_BAUD)
    ser.clear()
    ser.printf("Serial terminal started (P2 @%dMHz)\n", clkfreq / 1_000_000)

    cogspin(16, cog_audio(), @_audio_stack)
    repeat until _audio_cog
    ser.printf("Audio cog started (%dHz sample rate)\n", SAMPLE_RATE)

#include "lib.utility.spin2"


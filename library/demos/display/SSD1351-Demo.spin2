{
    --------------------------------------------
    Filename: SSD1351-Demo.spin
    Description: Demo of the SSD1351 driver
    Author: Jesse Burt
    Copyright (c) 2020
    Started: Mar 10, 2019
    Updated: Jul 13, 2020
    See end of file for terms of use.
    --------------------------------------------
}

CON

    _xtlfreq    = cfg._xtlfreq
    _clkfreq    = cfg._clkfreq_def

' -- User-modifiable constants
    LED         = cfg.LED1
    SER_RX      = cfg.SER_RX
    SER_TX      = cfg.SER_TX
    SER_BAUD    = 2_000_000

    RES_PIN     = 36
    DC_PIN      = 35
    CS_PIN      = 34
    CLK_PIN     = 33
    DIN_PIN     = 32
    CLK_HZ      = 20_000_000

    WIDTH       = 128
    HEIGHT      = 128
' --

    BPP         = 2
    BPL         = WIDTH * BPP
    BUFFSZ      = (WIDTH * HEIGHT) * 2  'in BYTEs - 12288
    XMAX        = WIDTH - 1
    YMAX        = HEIGHT - 1

OBJ

    cfg         : "core.con.boardcfg.p2eval"
    ser         : "com.serial.terminal.ansi"
    time        : "time"
    io          : "io"
    oled        : "display.oled.ssd1351.spi"
    int         : "string.integer"
    fnt         : "font.5x8"

VAR

    long _stack_timer[50]
    long _timer_set
    long _rndSeed
    byte _framebuff[BUFFSZ]
    byte _timer_cog, _ser_cog, _oled_cog

PUB Main() | time_ms, r

    setup()
    oled.clearall()

    oled.mirrorh(TRUE)
    oled.mirrorv(FALSE)

    demo_greet()
    waitms(5000)
    oled.clearall()

    time_ms := 10_000

    ser.position (0, 3)

    demo_sinewave (time_ms)
    oled.clearall()

    demo_triwave (time_ms)
    oled.clearall()

    demo_memscroller(time_ms, $0000, $FFFF-BUFFSZ)
    oled.clearall()

    demo_bitmap (time_ms, $FFFF-BUFFSZ)
    oled.clearall()

    demo_linesweepx(time_ms)
    oled.clearall()

    demo_linesweepy(time_ms)
    oled.clearall()

    demo_line (time_ms)
    oled.clearall()

    demo_plot (time_ms)
    oled.clearall()

    demo_plotaccel (time_ms)
    oled.clearall()

    demo_bouncingball (time_ms, 5)
    oled.clearall()

    demo_circle(time_ms)
    oled.clearall()

    demo_wander (time_ms)
    oled.clearall()

    demo_seqtext (time_ms)
    oled.clearall()

    demo_rndtext (time_ms)

    demo_contrast(2, 1)
    oled.clearall()

    stop()
    flashled(LED, 100)

PUB Demo_BouncingBall(testtime, radius) | iteration, bx, by, dx, dy
' Draws a simple ball bouncing off screen edges
    bx := (rnd(XMAX) // (WIDTH - radius * 4)) + radius * 2  'Pick a random screen location to
    by := (rnd(YMAX) // (HEIGHT - radius * 4)) + radius * 2 ' start from
    dx := rnd(4) // 2 * 2 - 1                               'Pick a random direction to
    dy := rnd(4) // 2 * 2 - 1                               ' start moving

    ser.printf("Demo_BouncingBall - ")
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        bx += dx
        by += dy
        if (by <= radius OR by >= YMAX-radius)          'If we reach the top or bottom of the screen,
            dy *= -1                                        ' change direction
        if (bx <= radius OR bx >= XMAX-radius)           'Ditto with the left or right sides
            dx *= -1

        oled.circle (bx, by, radius, oled.MAX_COLOR)
        oled.update()
        iteration++
        oled.clear()

    report(testtime, iteration)

PUB Demo_Bitmap(testtime, bitmap_addr) | iteration
' Continuously redraws bitmap at address bitmap_addr
    ser.printf("Demo_Bitmap - ")
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        oled.bitmap (bitmap_addr, BUFFSZ, 0)
        oled.update()
        iteration++

    report(testtime, iteration)

PUB Demo_Circle(testtime) | iteration, x, y, r
' Draws circles at random locations
    ser.printf("Demo_Circle - ")
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        x := rnd(XMAX)
        y := rnd(YMAX)
        r := rnd(YMAX/2)
        oled.circle (x, y, r, RND565())
        oled.update()
        iteration++

    report(testtime, iteration)

PUB Demo_Contrast(reps, delay_ms) | contrast_level
' Fades out and in display contrast
    ser.printf("Demo_Contrast - N/A")

    repeat reps
        repeat contrast_level from 255 to 1
            oled.contrast (contrast_level)
            waitms (delay_ms)
        repeat contrast_level from 0 to 254
            oled.contrast (contrast_level)
            waitms (delay_ms)

    ser.newline()

PUB Demo_Greet()
' Display the banner/greeting on the OLED
    oled.fgcolor(oled.MAX_COLOR)
    oled.bgcolor(0)
    oled.position (0, 0)
    oled.printf("SSD1351 on the\n")
    oled.printf("Parallax P2X8C4M64P @\n")
    oled.printf("%dMHz\n", clkfreq/1_000_000)
    oled.printf("%dx%d", WIDTH, HEIGHT)
    oled.update()

PUB Demo_Line (testtime) | iteration
' Draws random lines
    ser.printf("Demo_Line - ")
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        oled.line (rnd(XMAX), rnd(YMAX), rnd(XMAX), rnd(YMAX), RND565())
        oled.update()
        iteration++

    report(testtime, iteration)

PUB Demo_LineSweepX (testtime) | iteration, x
' Draws lines top left to lower-right, sweeping across the screen, then
'  from the top-down
    x := 0

    ser.printf("Demo_LineSweepX - ")
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        x++
        if x > XMAX
            x := 0
        oled.line (x, 0, XMAX-x, YMAX, x)
        oled.update()
        iteration++

    report(testtime, iteration)

PUB Demo_LineSweepY (testtime) | iteration, y
' Draws lines top left to lower-right, sweeping across the screen, then
'  from the top-down
    y := 0

    ser.printf("Demo_LineSweepY - ")
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        y++
        if y > YMAX
            y := 0
        oled.line (XMAX, y, 0, YMAX-y, y)
        oled.update()
        iteration++

    report(testtime, iteration)

PUB Demo_MEMScroller(testtime, start_addr, end_addr) | iteration, pos, st, en
' Dumps Propeller Hub RAM to the display buffer
    pos := start_addr

    ser.printf("Demo_MEMScroller - ")
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        pos += BPL
        if pos >end_addr
            pos := start_addr
        oled.bitmap (pos, BUFFSZ, 0)
        oled.update()
        iteration++

    report(testtime, iteration)

PUB Demo_Plot(testtime) | iteration, x, y
' Draws random pixels
    ser.printf("Demo_Plot - ")
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        oled.plot (rnd(XMAX), rnd(YMAX), RND565())
        oled.update()
        iteration++

    report(testtime, iteration)

PUB Demo_PlotAccel(testtime) | iteration, x, y
' Draws random pixels (accelerated/native)
    ser.printf("Demo_PlotAccel - ")
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        oled.plotaccel (rnd(XMAX), rnd(YMAX), RND565())
        iteration++

    oled.displaybounds(0, 0, XMAX, YMAX)
    report(testtime, iteration)

PUB Demo_Sinewave(testtime) | iteration, x, y, modifier, offset, div
' Draws a sine wave the length of the screen, influenced by the system counter
    ser.printf("Demo_Sinewave - ")

    div := 3072
    offset := YMAX/2                                    ' Offset for Y axis

    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        repeat x from 0 to XMAX
            modifier := (abs(getct()) / 1_000_000)           ' Use system counter as modifier
            y := offset + sin(x * modifier) / div
            oled.plot(x, y, oled.MAX_COLOR)

        oled.update()
        iteration++
        oled.clear()

    report(testtime, iteration)

PUB Demo_SeqText(testtime) | iteration, col, row, ch, fg, bg
' Sequentially draws the whole font table to the screen
    ch := $20
    ser.printf("Demo_SeqText - ")
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        ch++
        if ch > $7F
            ch := $20
        fg++
        if fg > oled.MAX_COLOR
            fg := 0
        bg--
        if bg < 0
            bg := oled.MAX_COLOR
        oled.fgcolor(fg)
        oled.bgcolor(bg)
        oled.char (ch)
        iteration++

    report(testtime, iteration)

PUB Demo_RndText(testtime) | iteration, chars
' Draw random text characters
    chars := oled.textcols() * oled.textrows()
    ser.printf("Demo_RndText - ")
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        repeat chars
            oled.fgcolor(RND565())
            oled.bgcolor(RND565())
            oled.char (32 #> rnd(127))
        oled.update()
        iteration++

    report(testtime, iteration)

PUB Demo_TriWave(testtime) | iteration, x, y, ydir
' Draws a simple triangular wave
    ydir := 1
    y := 0

    ser.printf("Demo_TriWave - ")
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        repeat x from 0 to XMAX
            if y == YMAX
                ydir := -1
            if y == 0
                ydir := 1
            y := y + ydir
            oled.plot (x, y, oled.MAX_COLOR)
        oled.update()
        iteration++
        oled.clear()

    report(testtime, iteration)

PUB Demo_Wander(testtime) | iteration, x, y, d
' Draws randomly wandering pixels
    x := XMAX/2
    y := YMAX/2

    ser.printf("Demo_Wander - ")
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        case d := rnd(4)
            1:
                x += 2
                if x > XMAX
                    x := 0
            2:
                x -= 2
                if x < 0
                    x := XMAX
            3:
                y += 2
                if y > YMAX
                    y := 0
            4:
                y -= 2
                if y < 0
                    y := YMAX
        oled.plot (x, y, RND565())
        oled.update()
        iteration++

    report(testtime, iteration)

PUB RND565(): rgb565
' Return random 16bit/5-6-5 color
    rgb565 := (rnd(31) << 11) | (rnd(63) << 5) | rnd(31)

PUB RND(maxval): r
' Return a random number up to maximum 'maxval'
    r := getrnd() +// maxval

PUB Sin(angle): sine
' Return the sine of angle
    org
        shl     angle, #20
        qrotate ##100000, angle
        getqy   sine
    end

PRI Report(testtime, iterations)

    ser.printf("Total iterations: ")
    ser.dec(iterations)

    ser.printf(", Iterations/sec: ")
    ser.dec(iterations / (testtime/1000))

    ser.printf(", ms/Iteration: ")
    decimal( (testtime * 1_000) / iterations, 1_000)
    ser.newline()

PRI Decimal(scaled, divisor) | whole[4], part[4], places, tmp
' Display a fixed-point scaled up number in decimal-dot notation - scale it back down by divisor
'   e.g., Decimal (314159, 100000) would display 3.14159 on the termainl
'   scaled: Fixed-point scaled up number
'   divisor: Divide scaled-up number by this amount
    whole := scaled / divisor
    tmp := divisor
    places := 0

    repeat
        tmp /= 10
        places++
    until tmp == 1
    part := int.deczeroed(abs(scaled // divisor), places)

    ser.Dec (whole)
    ser.Char (".")
    ser.str (part)

PRI cog_Timer() | time_left

    repeat
        repeat until _timer_set
        time_left := _timer_set

        repeat
            time_left--
            waitms(1)
        while time_left > 0
        _timer_set := 0

PUB Setup()

    repeat until _ser_cog := ser.StartRXTX (SER_RX, SER_TX, 0, SER_BAUD)
    ser.clear()
    ser.printf("Serial terminal started\n")
    if _oled_cog := oled.Start (CS_PIN, DC_PIN, DIN_PIN, CLK_PIN, RES_PIN, CLK_HZ, WIDTH, HEIGHT, @_framebuff)
        ser.printf("SSD1351 driver started\n", _oled_cog)
        oled.fontaddress(fnt.baseaddr())
        oled.fontsize(6, 8)
        oled.defaultscommon()
        oled.subpixelorder(oled.BGR)
        oled.clearall()
    else
        ser.printf("SSD1351 driver failed to start - halting\n")
        stop()

    _timer_cog := cogspin(16, cog_timer(), @_stack_timer)

PUB Stop()

    oled.powered (FALSE)
    oled.stop()
    cogstop(_timer_cog)

#include "lib.utility.spin2"

{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

{
    --------------------------------------------
    Filename: IL3897-Demo.spin2
    Author: Jesse Burt
    Description: Demo of the IL3897 driver (P2 version)
    Copyright (c) 2022
    Started Apr 4, 2021
    Updated Oct 16, 2022
    See end of file for terms of use.
    --------------------------------------------
}
#include "core.con.click.spin2"

CON

    _clkfreq    = cfg._clkfreq_def
    _xtlfreq    = cfg._xtlfreq

' -- User-modifiable constants
    LED         = cfg.LED1
    SER_BAUD    = 2_000_000

    CLICK_BASE  = 0
    RST_PIN     = CLICK_BASE+7
    DC_PIN      = CLICK_BASE+5
    BUSY_PIN    = CLICK_BASE+4
    SCK_FREQ    = 15_000_000                    ' max is 20_000_000
    ' NOTE: To use SCK_FREQ > 15MHz, _clkfreq needs to be
    '   increased to 250MHz or higher.

    WIDTH       = 122
    HEIGHT      = 250
' --

    XMAX        = WIDTH-1
    YMAX        = HEIGHT-1
    CENTERX     = XMAX/2
    CENTERY     = YMAX/2
    MIDLEFT     = CENTERX/2
    MIDTOP      = CENTERY/2
    MIDRIGHT    = CENTERX+MIDLEFT
    MIDBOTTOM   = CENTERY+MIDTOP
    BUFF_SZ     = ((WIDTH + 6) * HEIGHT) / 8

OBJ

    cfg     : "boardcfg.p2eval"
    ser     : "com.serial.terminal.ansi"
    epaper  : "display.epaper.il3897"
    fnt     : "font.5x8"

VAR

    byte _framebuff[BUFF_SZ]

PUB Main() | i

    setup()

    epaper.preset_2_13_bw()                     ' set presets for 2.13" BW

    repeat until epaper.disp_rdy()              ' wait for display to be ready

    epaper.bgcolor(epaper.WHITE)                ' set BG color for text and
    epaper.clear()                              '   Clear()
    epaper.fgcolor(epaper.BLACK)                ' set FG color for text
    epaper.box(0, 0, XMAX, YMAX, 0, FALSE)      ' draw box full-screen size

    epaper.position(5, 2)
    epaper.str(string("HELLO WORLD"))

    repeat i from XMAX/2 to 0 step 4            ' concentric circles
        epaper.circle(CENTERX, CENTERY, i, epaper.INVERT, false)
    epaper.line(0, 0, XMAX, YMAX, epaper.INVERT)' draw diagonal lines
    epaper.line(XMAX, 0, 0, YMAX, epaper.INVERT)
    epaper.box(MIDLEFT, MIDTOP, MIDRIGHT, MIDBOTTOM, -1, false)

    hrule()                                     ' draw rulers at screen edges
    vrule()

    epaper.show()                               ' update the display

    repeat

PUB HRule() | x, grad_len
' Draw a simple rule along the x-axis
    grad_len := 5

    repeat x from 0 to XMAX step 5
        if x // 10 == 0
            epaper.line(x, 0, x, grad_len, epaper.INVERT)
        else
            epaper.line(x, 0, x, grad_len*2, epaper.INVERT)

PUB VRule() | y, grad_len
' Draw a simple rule along the y-axis
    grad_len := 5

    repeat y from 0 to YMAX step 5
        if y // 10 == 0
            epaper.line(0, y, grad_len, y, epaper.INVERT)
        else
            epaper.line(0, y, grad_len*2, y, epaper.INVERT)

PUB Setup()

    ser.start(SER_BAUD)
    waitms(30)
    ser.clear()
    ser.strln(string("Serial terminal started"))
    if epaper.startx(CS_PIN, SCK_PIN, MOSI_PIN, RST_PIN, DC_PIN, BUSY_PIN, SCK_FREQ, WIDTH, HEIGHT, @_framebuff)
        ser.strln(string("IL3897 driver started"))
        epaper.fontscale(1)
        epaper.fontaddress(fnt.ptr())
        epaper.fontsize(6, 8)
    else
        ser.strln(string("IL3897 driver failed to start - halting"))
        repeat

DAT
{
Copyright 2022 Jesse Burt

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
associated documentation files (the "Software"), to deal in the Software without restriction,
including without limitation the rights to use, copy, modify, merge, publish, distribute,
sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or
substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT
NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT
OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}


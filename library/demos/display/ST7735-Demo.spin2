{
    --------------------------------------------
    Filename: ST7735-Demo.spin2
    Description: Demo of the ST7735 driver (P2 version)
    Author: Jesse Burt
    Copyright (c) 2020
    Started: Mar 10, 2020
    Updated: May 14, 2020
    See end of file for terms of use.
    --------------------------------------------
}

CON

    XTAL        = cfg#XTAL
    XDIV        = cfg#XDIV
    XMUL        = cfg#XMUL
    XDIVP       = cfg#XDIVP
    XOSC        = cfg#XOSC
    XSEL        = cfg#XSEL
    XPPPP       = cfg#XPPPP
    CLOCKFREQ   = cfg#CLOCKFREQ
    SETFREQ     = cfg#SETFREQ
    ENAFREQ     = cfg#ENAFREQ

    LED         = cfg#LED1
    SER_RX      = cfg#SER_RX
    SER_TX      = cfg#SER_TX
    SER_BAUD    = 2_000_000

    CS_PIN      = 10
    DC_PIN      = 11
    RESET_PIN   = 12
    SDA_PIN     = 9
    SCK_PIN     = 8
    SCK_HZ      = 20_000_000

    WIDTH       = 128
    HEIGHT      = 128
    BPP         = 2
    BPL         = WIDTH * BPP
    BUFFSZ      = (WIDTH * HEIGHT) * 2  'in BYTEs - 12288
    XMAX        = WIDTH - 1
    YMAX        = HEIGHT - 1

OBJ

    cfg         : "core.con.boardcfg.p2eval"
    ser         : "com.serial.terminal.ansi"
    time        : "time"
    io          : "io"
    lcd         : "display.lcd.st7735.spi.spin2"
    int         : "string.integer"
    fnt         : "font.5x8"

VAR

    long _stack_timer[50]
    long _timer_set
    long _rndSeed
    byte _framebuff[BUFFSZ]
    byte _timer_cog, _ser_cog, _lcd_cog

PUB Main | time_ms, r

    Setup
    lcd.ClearAll

    lcd.MirrorH(TRUE)                                       ' Change these to suit the orientation
    lcd.MirrorV(TRUE)                                       '   of your display

    Demo_Greet
    time.Sleep (5)
    lcd.ClearAll

    time_ms := 10_000

    ser.position (0, 3)

    Demo_SineWave (time_ms)
    lcd.ClearAll

    Demo_TriWave (time_ms)
    lcd.ClearAll

    Demo_MEMScroller(time_ms, $0000, $FFFF-BUFFSZ)
    lcd.ClearAll

    Demo_Bitmap (time_ms, $8000)
    lcd.ClearAll

    Demo_LineSweepX(time_ms)
    lcd.ClearAll

    Demo_LineSweepY(time_ms)
    lcd.ClearAll

    Demo_Line (time_ms)
    lcd.ClearAll

    Demo_Plot (time_ms)
    lcd.ClearAll

    Demo_BouncingBall (time_ms, 5)
    lcd.ClearAll

    Demo_Circle(time_ms)
    lcd.ClearAll

    Demo_Wander (time_ms)
    lcd.ClearAll

    Demo_SeqText (time_ms)
    lcd.ClearAll

    Demo_RndText (time_ms)

    Demo_Contrast(2, 1)
    lcd.ClearAll

    Stop
    FlashLED(LED, 100)

PUB Demo_BouncingBall(testtime, radius) | iteration, bx, by, dx, dy
' Draws a simple ball bouncing off screen edges
    bx := (rnd(XMAX) // (WIDTH - radius * 4)) + radius * 2  'Pick a random screen location to
    by := (rnd(YMAX) // (HEIGHT - radius * 4)) + radius * 2 ' start from
    dx := rnd(4) // 2 * 2 - 1                               'Pick a random direction to
    dy := rnd(4) // 2 * 2 - 1                               ' start moving

    ser.str(string("Demo_BouncingBall - "))
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        bx += dx
        by += dy
        if (by =< radius OR by => HEIGHT - radius)          'If we reach the top or bottom of the screen,
            dy *= -1                                        ' change direction
        if (bx =< radius OR bx => WIDTH - radius)           'Ditto with the left or right sides
            dx *= -1

        lcd.Circle (bx, by, radius, $FFFF)
        lcd.Update
        iteration++
        lcd.Clear

    Report(testtime, iteration)
    return iteration

PUB Demo_Bitmap(testtime, bitmap_addr) | iteration
' Continuously redraws bitmap at address bitmap_addr
    ser.str(string("Demo_Bitmap - "))
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        lcd.Bitmap (bitmap_addr, BUFFSZ, 0)
        lcd.Update
        iteration++

    Report(testtime, iteration)
    return iteration

PUB Demo_Circle(testtime) | iteration, x, y, r
' Draws circles at random locations
    ser.str(string("Demo_Circle - "))
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        x := rnd(XMAX)
        y := rnd(YMAX)
        r := rnd(YMAX/2)
        lcd.Circle (x, y, r, RND565)
        lcd.Update
        iteration++

    Report(testtime, iteration)
    return iteration

PUB Demo_Contrast(reps, delay_ms) | contrast_level
' Fades out and in display contrast
    ser.str(string("Demo_Contrast - N/A"))

    repeat reps
        repeat contrast_level from 255 to 1
            lcd.Contrast (contrast_level)
            time.MSleep (delay_ms)
        repeat contrast_level from 0 to 254
            lcd.Contrast (contrast_level)
            time.MSleep (delay_ms)

    ser.newline

PUB Demo_Greet
' Display the banner/greeting on the OLED
    lcd.FGColor($FFFF)
    lcd.BGColor(0)
    lcd.Position (0, 0)
    lcd.printf("ST7735 on the\n")
    lcd.printf("Parallax P2X8C4M64P @\n")
    lcd.printf("%dMHz\n", clkfreq/1_000_000)
    lcd.printf("%dx%d", WIDTH, HEIGHT)
    lcd.Update

PUB Demo_Line (testtime) | iteration
' Draws random lines with color -1 (invert)
    ser.str(string("Demo_Line - "))
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        lcd.Line (rnd(XMAX), rnd(YMAX), rnd(XMAX), rnd(YMAX), RND565)
        lcd.Update
        iteration++

    Report(testtime, iteration)
    return iteration

PUB Demo_LineSweepX (testtime) | iteration, x
' Draws lines top left to lower-right, sweeping across the screen, then
'  from the top-down
    x := 0

    ser.str(string("Demo_LineSweepX - "))
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        x++
        if x > XMAX
            x := 0
        lcd.Line (x, 0, XMAX-x, YMAX, x)
        lcd.Update
        iteration++

    Report(testtime, iteration)
    return iteration

PUB Demo_LineSweepY (testtime) | iteration, y
' Draws lines top left to lower-right, sweeping across the screen, then
'  from the top-down
    y := 0

    ser.str(string("Demo_LineSweepY - "))
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        y++
        if y > YMAX
            y := 0
        lcd.Line (XMAX, y, 0, YMAX-y, y)
        lcd.Update
        iteration++

    Report(testtime, iteration)
    return iteration

PUB Demo_MEMScroller(testtime, start_addr, end_addr) | iteration, pos, st, en
' Dumps Propeller Hub RAM (and/or ROM) to the display buffer
    pos := start_addr

    ser.str(string("Demo_MEMScroller - "))
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        pos += BPL
        if pos >end_addr
            pos := start_addr
        lcd.Bitmap (pos, BUFFSZ, 0)
        lcd.Update
        iteration++

    Report(testtime, iteration)
    return iteration

PUB Demo_Plot(testtime) | iteration, x, y
' Draws random pixels to the screen, with color -1 (invert)
    ser.str(string("Demo_Plot - "))
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        lcd.Plot (rnd(XMAX), rnd(YMAX), RND565)
        lcd.Update
        iteration++

    Report(testtime, iteration)
    return iteration

PUB Demo_Sinewave(testtime) | iteration, x, y, modifier, offset, div
' Draws a sine wave the length of the screen, influenced by the system counter
    ser.str(string("Demo_Sinewave - "))

    div := 3072
    offset := YMAX/2                                    ' Offset for Y axis

    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        repeat x from 0 to XMAX
            modifier := (abs(cnt) / 1_000_000)           ' Use system counter as modifier
            y := offset + sin(x * modifier) / div
            lcd.Plot(x, y, $FFFF)

        lcd.Update
        iteration++
        lcd.Clear

    Report(testtime, iteration)
    return iteration

PUB Demo_SeqText(testtime) | iteration, ch, chars
' Sequentially draws the whole font table to the screen
    chars := lcd.TextCols * lcd.TextRows
    ch := 32

    ser.str(string("Demo_SeqText - "))
    _timer_set := testtime
    iteration := 0

    lcd.Position(0, 0)
    repeat while _timer_set
        repeat chars
            lcd.FGColor(RND565)
            lcd.BGColor(RND565)
            lcd.Char (ch++)
            if ch > 127
                ch := 32

        lcd.Update
        iteration++

    Report(testtime, iteration)
    return iteration

PUB Demo_RndText(testtime) | iteration, chars
' Draw random text characters
    chars := lcd.TextCols * lcd.TextRows
    ser.str(string("Demo_RndText - "))
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        repeat chars
            lcd.FGColor(RND565)
            lcd.BGColor(RND565)
            lcd.Char (32 #> rnd(127))
        lcd.Update
        iteration++

    Report(testtime, iteration)
    return iteration

PUB Demo_TriWave(testtime) | iteration, x, y, ydir
' Draws a simple triangular wave
    ydir := 1
    y := 0

    ser.str(string("Demo_TriWave - "))
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        repeat x from 0 to XMAX
            if y == YMAX
                ydir := -1
            if y == 0
                ydir := 1
            y := y + ydir
            lcd.Plot (x, y, $FFFF)
        lcd.Update
        iteration++
        lcd.Clear

    Report(testtime, iteration)
    return iteration

PUB Demo_Wander(testtime) | iteration, x, y, d
' Draws randomly wandering pixels
    _rndSeed := cnt
    x := XMAX/2
    y := YMAX/2

    ser.str(string("Demo_Wander - "))
    _timer_set := testtime
    iteration := 0

    repeat while _timer_set
        case d := rnd(4)
            1:
                x += 2
                if x > XMAX
                    x := 0
            2:
                x -= 2
                if x < 0
                    x := XMAX
            3:
                y += 2
                if y > YMAX
                    y := 0
            4:
                y -= 2
                if y < 0
                    y := YMAX
        lcd.Plot (x, y, RND565)
        lcd.Update
        iteration++

    Report(testtime, iteration)
    return iteration

PUB RND565
' Return random 16bit/5-6-5 color
    return (rnd(31) << 11) | (rnd(63) << 5) | rnd(31)

PUB RND(maxval) | i

    asm
        getrnd i
        shr i, #16
        add maxval, #1
        mul i, maxval
        shr i, #16
    endasm
    return i

PUB Sin(angle) | tmp
' Return the sine of angle
    asm
        shl     angle, #20
        qrotate ##100000, angle
        getqy   tmp               'Can't getqy directly into result - too complex for inline asm
    endasm
    return tmp

PRI Report(testtime, iterations)

    ser.str(string("Total iterations: "))
    ser.dec(iterations)

    ser.str(string(", Iterations/sec: "))
    ser.dec(iterations / (testtime/1000))

    ser.str(string(", Iterations/ms: "))
    Decimal( (iterations * 1_000) / testtime, 1_000)
    ser.newline

PRI Decimal(scaled, divisor) | whole[4], part[4], places, tmp
' Display a fixed-point scaled up number in decimal-dot notation - scale it back down by divisor
'   e.g., Decimal (314159, 100000) would display 3.14159 on the termainl
'   scaled: Fixed-point scaled up number
'   divisor: Divide scaled-up number by this amount
    whole := scaled / divisor
    tmp := divisor
    places := 0

    repeat
        tmp /= 10
        places++
    until tmp == 1
    part := int.DecZeroed(abs(scaled // divisor), places)

    ser.Dec (whole)
    ser.Char (".")
    ser.Str (part)

PRI cog_Timer | time_left

    repeat
        repeat until _timer_set
        time_left := _timer_set

        repeat
            time_left--
            time.MSleep(1)
        while time_left > 0
        _timer_set := 0

PUB Setup

    clkset(ENAFREQ, CLOCKFREQ)
    repeat until _ser_cog := ser.StartRXTX (SER_RX, SER_TX, 0, SER_BAUD)
    time.MSleep(30)
    ser.Clear
    ser.str(string("Serial terminal started", ser#CR, ser#LF))
    if _lcd_cog := lcd.Start (CS_PIN, SCK_PIN, SDA_PIN, DC_PIN, RESET_PIN, SCK_HZ, WIDTH, HEIGHT, @_framebuff)
        ser.str(string("ST7735 driver started", ser#CR, ser#LF))
        lcd.FontAddress(fnt.BaseAddr)
        lcd.FontSize(6, 8)
        lcd.DefaultsCommon
        lcd.ClearAll
    else
        ser.str(string("ST7735 driver failed to start - halting", ser#CR, ser#LF))
        Stop

    _timer_cog := cognew(cog_Timer, @_stack_timer)

PUB Stop

    lcd.DisplayVisibility(lcd#ALL_OFF)
    lcd.Powered (FALSE)
    lcd.Stop
    cogstop(_timer_cog)

#include "lib.utility.spin2"


{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

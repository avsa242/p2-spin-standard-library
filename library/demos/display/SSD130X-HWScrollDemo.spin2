{
    --------------------------------------------
    Filename: SSD130X-HWScrollDemo.spin2
    Description: SSD130X Hardware-accelerated scrolling demo (P2 version)
    Author: Jesse Burt
    Copyright (c) 2023
    Started: Mar 12, 2023
    Updated: Jul 23, 2023
    See end of file for terms of use.
    --------------------------------------------
}
CON

    _clkfreq    = cfg._clkfreq_def
    _xtlfreq    = cfg._xtlfreq

' -- User-modifiable constants
    SER_BAUD    = 2_000_000
' --

OBJ

    cfg: "boardcfg.p2eval"
    disp:"display.oled.ssd130x" |   WIDTH=128, HEIGHT=64, ...
                                    {I2C} SCL=28, SDA=29, I2C_ADDR=0, I2C_FREQ=1_000_000, ...
                                    {SPI} CS=0, SCK=1, MOSI=2, DC=3, RST=4, SPI_FREQ=10_000_000
                                    ' (set RST=-1 to disable (reccommend tying to propeller RESET)
    ser: "com.serial.terminal.ansi"
    fnt: "font.5x8"

PUB main() | y

    setup()

    disp.strln(@"SSD130x on the")
    disp.strln(@"Parallax P2X8C4M64P")
    disp.strln(@"HW-accelerated")
    disp.str(@"scrolling demo")
    disp.show()

    waitms(2_000)

    { NOTE: the actual granularity of y coordinates is 8 pixels - they will be rounded
        to the nearest multiple of 8 by the driver (hardware limitation) }

    { horizontal scrolling }
    disp.scroll_left_cont(0, 0, 127, 63, 2)     ' sx, sy, ex, ey, inter-scroll step delay (frames)
    waitms(2_000)

    disp.scroll_right_cont(0, 0, 127, 63, 2)
    waitms(2_000)

    { vertical/horizontal scrolling }
    { NOTE: vertical scroll by itself isn't possible in hardware - there are two modes that
        combine vertical with horizontal scrolling }
    disp.scroll_right_up_cont(0, 63, 1, 2)      ' sy, ey, vertical scroll step (pixels), delay
    waitms(2_000)

    disp.scroll_left_up_cont(0, 63, 1, 2)
    waitms(2_000)

    { scroll invidual horizontal pages (groups of 8 rows) }
    repeat y from 0 to 24 step 8
        disp.scroll_left_cont(0, y, 127, y+7, 2)
        waitms(2_000)
        disp.scroll_right_cont(0, y, 127, y+7, 2)
        waitms(2_000)

    disp.scroll_stop()
    repeat

PUB setup()

    ser.start(SER_BAUD)
    waitms(30)
    ser.clear()
    ser.strln(string("Serial terminal started"))

    if ( disp.start() )
        ser.strln(string("SSD130X driver started"))
        disp.font_spacing(1, 0)
        disp.font_scl(1)
        disp.font_sz(fnt.WIDTH, fnt.HEIGHT)
        disp.font_addr(fnt.ptr())
        disp.preset_128x()
    else
        ser.strln(string("SSD130X driver failed to start - halting"))
        repeat

    disp.mirror_h(FALSE)
    disp.mirror_v(FALSE)
    disp.clear()
    disp.fgcolor(1)

DAT
{
Copyright 2023 Jesse Burt

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
associated documentation files (the "Software"), to deal in the Software without restriction,
including without limitation the rights to use, copy, modify, merge, publish, distribute,
sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or
substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT
NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT
OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}


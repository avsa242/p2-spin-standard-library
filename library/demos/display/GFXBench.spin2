{
    --------------------------------------------
    Filename: GFXBench.spin2
    Description: Graphics benchmark
    Author: Jesse Burt
    Copyright (c) 2021
    Started: Apr 10, 2021
    Updated: Apr 11, 2021
    See end of file for terms of use.
    --------------------------------------------
}
' Uncomment one display type below
'#define SSD1306_I2C
'#define SSD1306_SPI
'#define SSD1309
'#define SSD1331
'#define SSD1351
'#define ST7735
#define VGABITMAP8BPP

' Comment out this line to define custom I/O pins below
' or, uncomment to use MikroE Click I/O pin offset definitions
'#include "core.con.click.spin2"

OBJ

    cfg     : "core.con.boardcfg.p2eval"
    ser     : "com.serial.terminal.ansi"
    time    : "time"
    int     : "string.integer"
    fnt     : "font.5x8"
#ifdef SSD1306_I2C
    disp    : "display.oled.ssd1306.i2c"
#elseifdef SSD1306_SPI
    disp    : "display.oled.ssd1306.spi"
#elseifdef SSD1309
    disp    : "display.oled.ssd1309.spi"
#elseifdef SSD1331
    disp    : "display.oled.ssd1331.spi"
#elseifdef SSD1351
    disp    : "display.oled.ssd1351.spi"
#elseifdef ST7735
    disp    : "display.lcd.st7735.spi"
#elseifdef VGABITMAP8BPP
    disp    : "display.vga.bitmap-8bpp"
#endif

CON

#ifdef VGABITMAP8BPP
    _clkfreq    = disp._clkfreq                 ' for matching the VGA driver's
#else                                           '   clock setting
    _clkfreq    = cfg._clkfreq_def              ' can modify this for
#endif                                          '   other drivers
    _xtlfreq    = cfg._xtlfreq

' -- User-modifiable constants:
    LED         = cfg.LED1
    SER_BAUD    = 2_000_000

    WIDTH       = 320                           ' change these for your
    HEIGHT      = 240                           '   display

#ifdef MIKROE_CLICK
    CLICK_BASE  = 0                             ' MikroE Click adaptor basepin
    SCL_FREQ    = 400_000                       ' I2C
    SCK_FREQ    = 10_000_000                    ' SPI
#else
' I2C-connected displays                        ' free-form I/O connections
    SCL_PIN     = 16
    SDA_PIN     = 17
    ADDR_BITS   = 0
    SCL_FREQ    = 400_000

' SPI-connected displays
    CS_PIN      = 0
    SCK_PIN     = 1
    MOSI_PIN    = 2
    DC_PIN      = 3
    RES_PIN     = 4
    SCK_FREQ    = 10_000_000
#endif

' VGA
    VGA_BASEPIN = 32
' --

    BPP         = disp.BYTESPERPX
    BYTESPERLN  = WIDTH * BPP
#ifdef SSD1306_I2C
    BUFFSZ      = (WIDTH * HEIGHT) / 8
#elseifdef SSD1306_SPI
    BUFFSZ      = (WIDTH * HEIGHT) / 8
#elseifdef SSD1309
    BUFFSZ      = (WIDTH * HEIGHT) / 8
#elseifdef VGABITMAP8BPP
    BUFFSZ      = (WIDTH * HEIGHT)
#else
    BUFFSZ      = ((WIDTH * HEIGHT) * BPP) / 2
#endif
    XMAX        = WIDTH - 1
    YMAX        = HEIGHT - 1
    CENTERX     = WIDTH/2
    CENTERY     = HEIGHT/2

    BITMAP      = 0
    BOX         = 1
    BOX_F       = 2
    CIRCLE      = 3
    CIRCLE_F    = 4
    DLINE       = 5
    HLINE       = 6
    PLOT        = 7
    TEXT        = 8
    VLINE       = 9

VAR

    long _stack_timer[50]
    long _timer_set
#ifdef SSD1306_I2C
    byte _framebuff[BUFFSZ]                     ' 1bpp
#elseifdef SSD1306_SPI
    byte _framebuff[BUFFSZ]                     ' 1bpp
#elseifdef VGABITMAP8BPP
    long _palette[256]
    byte _framebuff[BUFFSZ]                     ' 8bpp
#else
    word _framebuff[BUFFSZ]                     ' 16bpp
#endif

PUB Main() | time_ms, sz, maxsz, iteration, bench, ch, color

    setup()

    disp.mirrorh(false)                         ' change these to reorient for
    disp.mirrorv(false)                         '   your display

    disp.clear()
    disp.fgcolor(disp#MAX_COLOR)
    disp.bgcolor(0)
    disp.position(0, 0)
    disp.printf(string("%s\n"), @_drv_name)
    disp.printf(string("P2X8C4M64P\n @%dMHz\n"), clkfreq/1_000_000)
    disp.printf(string("%dx%d"), WIDTH, HEIGHT)
    disp.update()
    time.sleep(5)
    disp.clearall()

    maxsz := WIDTH <# HEIGHT                    ' find smallest disp. dimension

    color := disp.MAX_COLOR
    time_ms := 2_000                            ' time to run each test
    bench := BITMAP                             ' starting benchmark

    ch := $20
    sz := 1
    ser.position(0, 3)
    repeat
        ser.printf(string("Bench %s (%dpx): "), @_bench_name[bench*14], sz)

        _timer_set := time_ms
        iteration := 0
        case bench
            BITMAP:
                repeat while _timer_set
                    disp.bitmap($00000, sz-1, 0)
                    disp.update()
                    iteration++
            BOX:
                repeat while _timer_set
                    disp.box(0, 0, sz-1, sz-1, color, false)
                    disp.update()
                    iteration++
            BOX_F:
                repeat while _timer_set
                    disp.box(0, 0, sz-1, sz-1, color, true)
                    disp.update()
                    iteration++
            CIRCLE:
                repeat while _timer_set
                    disp.circle(CENTERX, CENTERY, sz-1, color, false)
                    disp.update()
                    iteration++
            CIRCLE_F:
                repeat while _timer_set
                    disp.circle(CENTERX, CENTERY, sz-1, color, true)
                    disp.update()
                    iteration++
            DLINE:
                repeat while _timer_set
                    disp.line(0, 0, sz-1, sz-1, color)
                    disp.update()
                    iteration++
            HLINE:
                repeat while _timer_set
                    disp.line(0, 0, sz-1, 0, color)
                    disp.update()
                    iteration++
            PLOT:
                repeat while _timer_set
                    disp.plot(0, 0, color)
                    disp.update()
                    iteration++
            TEXT:
                disp.fontscale(lookdown(sz: 2, 4, 8, 16, 32, 64, 128))
                repeat while _timer_set
                    disp.char(ch)
                    ch++
                    if ch > $7F
                        ch := $20
                    disp.update()
                    iteration++
            VLINE:
                repeat while _timer_set
                    disp.line(0, 0, 0, sz-1, color)
                    disp.update()
                    iteration++
            other:
                quit
        report(time_ms, iteration)              ' show the results
        disp.clear()

        sz *= 2

        if sz > maxsz                           ' if max size reached,
            sz := 1                             ' reset it and move on to
            bench++                             ' the next test

        if bench > VLINE
            quit

    ser.strln(string("COMPLETE"))
    repeat

PRI Report(testtime, iterations)
' Display benchmark test results
    ser.printf("%d per sec, ", iterations / (testtime/1000))
    decimal((testtime * 1_000) / iterations, 1_000)
    ser.str(" ms/iteration, ")
    ser.printf("(total iterations: %d in %dms)", iterations, testtime)
    ser.newline()

PRI Decimal(scaled, divisor) | whole[4], part[4], places, tmp
' Display a fixed-point scaled up number in decimal-dot notation - scale it back down by divisor
'   e.g., Decimal (314159, 100000) would display 3.14159 on the termainl
'   scaled: Fixed-point scaled up number
'   divisor: Divide scaled-up number by this amount
    whole := scaled / divisor
    tmp := divisor
    places := 0

    repeat
        tmp /= 10
        places++
    until tmp == 1
    part := int.deczeroed(abs(scaled // divisor), places)

    ser.dec(whole)
    ser.char(".")
    ser.str(part)

PRI cog_Timer() | time_left
' Benchmark timer loop
    repeat
        repeat until _timer_set                 ' wait here until a timer has
        time_left := _timer_set                 '   been set

        repeat                                  ' loop for time_left ms
            time_left--
            waitms(1)
        while time_left > 0
        _timer_set := 0                         ' signal the timer's been reset

PUB Setup()

    ser.start(SER_BAUD)
    waitms(30)
    ser.clear()
    ser.strln(string("Serial terminal started"))

#ifdef SSD1306_I2C
    if disp.startx(SCL_PIN, SDA_PIN, RES_PIN, SCL_FREQ, ADDR_BITS, WIDTH, HEIGHT, @_framebuff)
        disp.preset_128x()
#elseifdef VGABITMAP8BPP
    setuppalette()
    disp.settimings(10, 33, 16, 96, 48, 640)
    if disp.start(VGA_BASEPIN, @_framebuff, @_palette, WIDTH, HEIGHT)
#elseifdef SSD1306_SPI
    if disp.startx(CS_PIN, SCK_PIN, MOSI_PIN, DC_PIN, RES_PIN, SCK_FREQ, WIDTH, HEIGHT, @_framebuff)
        disp.preset_128x()
#elseifdef SSD1309
    if disp.startx(CS_PIN, SCK_PIN, MOSI_PIN, DC_PIN, RES_PIN, SCK_FREQ, WIDTH, HEIGHT, @_framebuff)
        disp.preset_128x()
#elseifdef SSD1331
    if disp.startx(CS_PIN, SCK_PIN, MOSI_PIN, DC_PIN, RES_PIN, SCK_FREQ, WIDTH, HEIGHT, @_framebuff)
        disp.preset_96x64()
#elseifdef SSD1351
    if disp.startx(CS_PIN, SCK_PIN, MOSI_PIN, DC_PIN, RES_PIN, SCK_FREQ, WIDTH, HEIGHT, @_framebuff)
        disp.preset_oled_c_click_96x96()
#elseifdef ST7735
    if disp.startx(CS_PIN, SCK_PIN, MOSI_PIN, DC_PIN, RES_PIN, SCK_FREQ, WIDTH, HEIGHT, @_framebuff)
        disp.preset_greentab128x128()
#endif
        disp.fontscale(1)
        disp.fontsize(6, 8)
        disp.fontaddress(fnt.baseaddr())
        ser.printf(string("%s driver started\n"), @_drv_name)
    else
        ser.printf(string("%s driver failed to start"), @_drv_name)
        repeat

    cogspin(NEWCOG, cog_timer(), @_stack_timer)

#ifdef VGABITMAP8BPP
PRI SetupPalette() | i, r, g, b, c
' Set up palette (VGA)
    r := 0
    g := 0
    b := 0

    repeat i from 0 to 255
        c := 0 | (r << 16) | (g << 8) | b
        _palette[i] := c

        case i
            0..63:
                r += 4
                g := 0
                b := 0
            64..127:
                r := 0
                g += 4
                b := 0
            128..191:
                r := 0
                g := 0
                b += 4
            192..253:
                r += 4
                g += 4
                b := 0
            254..255:
                r := 255
                g := 255
                b := 255
#endif

DAT

    _bench_name byte "Bitmap       ", 0
                byte "Box          ", 0
                byte "BoxFilled    ", 0
                byte "Circle       ", 0
                byte "CircleFilled ", 0
                byte "DLine        ", 0
                byte "HLine        ", 0
                byte "Plot         ", 0
                byte "Text         ", 0
                byte "VLine        ", 0

#ifdef SSD1306_I2C
    _drv_name   byte "SSD1306 (I2C)", 0
#elseifdef SSD1306_SPI
    _drv_name   byte "SSD1306 (SPI)", 0
#elseifdef SSD1309
    _drv_name   byte "SSD1309", 0
#elseifdef SSD1331
    _drv_name   byte "SSD1331", 0
#elseifdef SSD1351
    _drv_name   byte "SSD1351", 0
#elseifdef ST7735
    _drv_name   byte "ST7735", 0
#elseifdef VGABITMAP8BPP
    _drv_name   byte "VGA8BPP", 0
#endif

{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

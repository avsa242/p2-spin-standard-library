{
    --------------------------------------------
    Filename: PrintF-Demo.spin2
    Author: Jesse Burt
    Description: Demonstrate the functionality of
        the printf() method variants
    Copyright (c) 2020
    Started Nov 9, 2020
    Updated Nov 9, 2020
    See end of file for terms of use.
    --------------------------------------------
}
CON

    _clkfreq    = cfg#_clkfreq_def
    _xtlfreq    = cfg#_xtlfreq

' -- User-modifiable constants
    LED         = cfg#LED1
    SER_BAUD    = 2_000_000

    BUFFSZ      = 200                           ' maximum buffer size, in bytes
' --

    CR          = 13
    LF          = 10
    TB          = 9

OBJ

    cfg     : "boardcfg.p2eval"
    ser     : "com.serial.terminal.ansi"
    sf      : "string.format"

VAR

    byte _buff[BUFFSZ]

PUB Main() | sz, format, str1, str2

    setup()

'   printf() prints directly to the terminal (serial, vga, oled, lcd, etc)
'       whereas sprintf() and snprintf() output to a user-allocated buffer
'       (e.g., _buff in this demo)

'   Allowed format specifiers:
'       %%: literal, i.e., just print a % sign
'       %d or %u: decimal (signed; unsigned is not supported at this time)
'       %x: hex (always 8 digits, with leading zeroes)
'       %s: string

'   Allowed escape sequences:
'       \\: literal, i.e., just print a \ symbol
'       \r: carriage return
'       \n: carriage return, line feed (newline)
'       \t: tab (ASCII $09)

'   a simple example:
    format := string("A decimal: %d\n")

'   a more complex example
'    format := string("Test literal: %%  char: %c  dec: %d  hex: %x  str: %s  str: %s\nnext line\n\n\n")

    str1 := string("a string")
    str2 := string("another")

    ser.position(0, 5)

    ' You can specify the format inline:
    ser.printf(string("Test literal: %%  char: %c  dec: %d  hex: %x  str: %s  str: %s\nnext line\n\n\n"), "A", -1000, $DEADBEEF, str1, str2)

    '   or use a pre-defined format:
    ser.printf(format, "A", -1000, $DEADBEEF, str1, str2)

    ' Print to a buffer (for use in e.g., a file written to SD, or
    '   other external memory, etc)
    sf.sprintf(@_buff, format, "A", -1000, $DEADBEEF, str1, str2)
    ser.str(@_buff)

    bytefill(@_buff, 0, BUFFSZ)                 ' clear the buffer

    ' As above, but the second parameter specifies the maximum number
    '   of bytes to write
    sf.snprintf(@_buff, BUFFSZ, format, "A", -1000, $DEADBEEF, str1, str2)
    ser.str(@_buff)

    bytefill(@_buff, 0, BUFFSZ)

    ' An example showing comma-separated values, which could, for example,
    '   be written to a file on an SD-card
    format := string("%d,%d,%d,%d,%d,%d\n")
    sf.sprintf(@_buff, format, 7, 10, 3, 84, 16, 51)
    ser.str(@_buff)

    repeat

PUB Setup()

    ser.start(SER_BAUD)
    waitms(30)
    ser.reset()
    ser.clear()
    ser.strln(string("Serial terminal started"))

DAT
{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

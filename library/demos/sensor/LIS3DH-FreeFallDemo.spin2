{
    --------------------------------------------
    Filename: LIS3DH-FreeFallDemo.spin2
    Author: Jesse Burt
    Description: Demo of the LIS3DH driver
        Free-fall detection functionality (P2 version)
    Copyright (c) 2022
    Started Dec 22, 2021
    Updated Nov 5, 2022
    See end of file for terms of use.
    --------------------------------------------

    Build-time symbols supported by driver:
        -DLIS3DH_SPI
        -DLIS3DH_I2C (default if none specified)
}
#include "core.con.click.spin2"

CON

    _clkfreq    = cfg._clkfreq_def
    _xtlfreq    = cfg._xtlfreq

' -- User-modifiable constants
    LED1        = cfg.LED1
    SER_BAUD    = 2_000_000

    CLICK_BASE  = 0

    { I2C configuration }
    { custom I/O pin definitions - uncomment the below, and comment out the #include
        line above to use }
'    SCL_PIN     = 0
'    SDA_PIN     = 1
    I2C_FREQ    = 400_000                       ' max is 400_000
    ADDR_BITS   = 0                             ' 0, 1

    { SPI configuration }
    { custom I/O pin definitions - uncomment the below, and comment out the #include
        line above to use }
'    CS_PIN      = 0
'    SCK_PIN     = 1                             ' SCL
'    MOSI_PIN    = 2                             ' SDA
'    MISO_PIN    = 3                             ' SDO
    SPI_FREQ    = 10_000_000                    ' max is 10_000_000

    INT1_PIN    = 15

'   NOTE: If LIS3DH_SPI is #defined, and MOSI_PIN and MISO_PIN are the same,
'   the driver will attempt to start in 3-wire SPI mode.
' --

    DAT_X_COL   = 20
    DAT_Y_COL   = DAT_X_COL + 15
    DAT_Z_COL   = DAT_Y_COL + 15

OBJ

    cfg     : "boardcfg.p2eval"
    ser     : "com.serial.terminal.ansi"
    accel   : "sensor.accel.3dof.lis3dh"

VAR

    long _isr_stack[50]                         ' stack for ISR core
    long _intflag                               ' interrupt flag

PUB main() | intsource

    setup()
    accel.preset_freefall()                     ' default settings, but enable
                                                ' sensors, set scale factors,
                                                ' and free-fall parameters

    ser.pos_xy(0, 3)
    ser.puts(@"Waiting for free-fall condition...")

    ' When the sensor detects free-fall, a message is displayed and
    '   is cleared after the user presses a key
    ' The preset for free-fall detection sets a free-fall threshold of
    '   0.320g's for a minimum time of 100ms. This can be tuned using
    '   accel.freefall_set_thresh() and accel.freefall_set_time():
    accel.freefall_set_thresh(0_320000)         ' 0.320g's
    accel.freefall_set_time(100_000)            ' 100_000us/100ms

    repeat
        if (_intflag)                           ' interrupt triggered?
            intsource := accel.accel_int()      ' read & clear interrupt flags
            if (intsource & %01_01_01)          ' free-fall event?
                ser.pos_xy(0, 4)
                ser.puts(@"Sensor in free-fall!")
                ser.clear_line()
                ser.newline()
                ser.puts(@"Press any key to reset")
                ser.getchar()
                ser.pos_x(0)
                ser.clear_line()
                ser.pos_xy(0, 4)
                ser.puts(@"Sensor stable")
                ser.clear_line()
        if (ser.rxcheck() == "c")               ' press the 'c' key in the demo
            calibrate()                         ' to calibrate sensor offsets

PUB calibrate()
' Calibrate sensor/set bias offsets
    ser.pos_xy(0, 7)
    ser.str(@"Calibrating...")
    accel.calibrate_accel()
    ser.pos_x(0)
    ser.clear_line()

PRI cog_isr()
' Interrupt service routine
    pinf(INT1_PIN)                              ' INT1_PIN as input
    pinl(LED1)                                  ' LED as output

    repeat
        repeat until pinr(INT1_PIN) == 1        ' wait for INT1_PIN (active high)
        pinh(LED1)                              ' light LED
        _intflag := 1                           '   set flag

        repeat until pinr(INT1_PIN) == 0        ' now wait for it to clear
        pinl(LED1)                              ' turn off LED
        _intflag := 0                           '   clear flag

PUB setup()

    ser.start(SER_BAUD)
    waitms(30)
    ser.clear()
    ser.strln(@"Serial terminal started")
#ifdef LIS3DH_SPI
    if accel.startx(CS_PIN, SCK_PIN, MOSI_PIN, MISO_PIN)
        ser.strln(@"LIS3DH driver started (SPI)")
#else
    if accel.startx(SCL_PIN, SDA_PIN, I2C_FREQ, ADDR_BITS)
        ser.strln(@"LIS3DH driver started (I2C)")
#endif
    else
        ser.strln(@"LIS3DH driver failed to start - halting")
        repeat

    cogspin(NEWCOG, cog_isr(), @_isr_stack)         ' start ISR in another core

DAT
{
Copyright 2022 Jesse Burt

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
associated documentation files (the "Software"), to deal in the Software without restriction,
including without limitation the rights to use, copy, modify, merge, publish, distribute,
sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or
substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT
NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT
OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}


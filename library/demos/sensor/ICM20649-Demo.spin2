{
    --------------------------------------------
    Filename: ICM20649-Demo.spin2
    Author: Jesse Burt
    Description: Demo of the ICM20649 driver (P2 version)
    Copyright (c) 2020
    Started Sep 4, 2020
    Updated Oct 31, 2020
    See end of file for terms of use.
    --------------------------------------------
}
' Uncomment _one_ of the below to choose the interface
'#define ICM20649_I2C
#define ICM20649_SPI
CON

    _clkfreq    = cfg._clkfreq_def
    _xtlfreq    = cfg._xtlfreq

' -- User-modifiable constants
    LED         = cfg.LED1
    SER_BAUD    = 2_000_000

' I2C:
    SCL_PIN     = 26
    SDA_PIN     = 27
    I2C_HZ      = 400_000
    ADDR_BITS   = 0                             ' optional address bit: 0 or 1

' SPI:
    CS          = 32
    SCK         = 35
    MOSI        = 34
    MISO        = 36
    SCK_FREQ    = 7_000_000

    TEMP_SCALE  = C
' --

    DAT_X_COL   = 20
    DAT_Y_COL   = DAT_X_COL + 15
    DAT_Z_COL   = DAT_Y_COL + 15

    C           = 0
    F           = 1

OBJ

    cfg     : "core.con.boardcfg.p2eval"
    ser     : "com.serial.terminal.ansi"
    int     : "string.integer"
#ifdef ICM20649_SPI
    imu     : "sensor.imu.6dof.icm20649.spi"
#elseifdef ICM20649_I2C
    imu     : "sensor.imu.6dof.icm20649.i2c"
#endif

PUB Main() | dispmode

    setup()

    imu.accellowpassfilter(50)                              ' 6, 12, 24, 50, 111, 246, 473
    imu.acceldatarate(100)                                  ' 1..1121
    imu.accelscale(4)                                       ' 4, 8, 16, 30 (g's)
    imu.accelaxisenabled(%111)                              ' 0: disable, %001..%111: enable (all axes)

    imu.gyrolowpassfilter(51)                               ' 6, 12, 24, 51, 120, 152, 197, 361, 12106 (disable LPF)
    imu.gyrodatarate(100)                                   ' 1..1100
    imu.gyroscale(500)                                      ' 500, 1000, 2000, 4000
    imu.gyroaxisenabled(%111)                               ' 0: disable, %001..%111: enable (all axes)
    imu.gyrobias(0, 0, 0, 1)                                ' x, y, z: -32768..32767, rw = 1 (write)

    imu.tempscale(TEMP_SCALE)                               ' C (0), F (1)

    ser.hidecursor()
    dispmode := 0
    displaysettings()
    repeat
        case ser.rxcheck()
            "q", "Q":                                       ' Quit the demo
                ser.position(0, 17)
                ser.printf("Halting")
                imu.stop()
                waitms(5)
                quit
            "c", "C":                                       ' Perform calibration
                calibrate()
                displaysettings()
            "r", "R":                                       ' Change display mode: raw/calculated
                ser.position(0, 14)
                repeat 2
                    ser.clearline()
                    ser.newline()
                dispmode ^= 1
        case dispmode
            0:
                ser.position(0, 14)
                accelraw()
                gyroraw()
                temp()
            1:
                ser.position(0, 14)
                accelcalc()
                gyrocalc()
                temp()

    ser.showcursor()
    repeat

PUB AccelCalc() | ax, ay, az

    repeat until imu.acceldataready()
    imu.accelg(@ax, @ay, @az)
    ser.printf("Accel micro-g: ")
    ser.position(DAT_X_COL, 14)
    decimal(ax, 1_000_000)
    ser.position(DAT_Y_COL, 14)
    decimal(ay, 1_000_000)
    ser.position(DAT_Z_COL, 14)
    decimal(az, 1_000_000)
    ser.clearline()
    ser.newline()

PUB AccelRaw() | ax, ay, az

    repeat until imu.acceldataready()
    imu.acceldata(@ax, @ay, @az)
    ser.printf("Accel raw: ")
    ser.position(DAT_X_COL, 14)
    ser.str(int.decpadded (ax, 7))
    ser.position(DAT_Y_COL, 14)
    ser.str(int.decpadded (ay, 7))
    ser.position(DAT_Z_COL, 14)
    ser.str(int.decpadded (az, 7))
    ser.clearline()
    ser.newline()

PUB GyroCalc() | gx, gy, gz

    repeat until imu.gyrodataready()
    imu.gyrodps(@gx, @gy, @gz)
    ser.printf("Gyro micro DPS:  ")
    ser.position(DAT_X_COL, 15)
    decimal(gx, 1_000_000)
    ser.position(DAT_Y_COL, 15)
    decimal(gy, 1_000_000)
    ser.position(DAT_Z_COL, 15)
    decimal(gz, 1_000_000)
    ser.clearline()
    ser.newline()

PUB GyroRaw() | gx, gy, gz

    repeat until imu.gyrodataready()
    imu.gyrodata(@gx, @gy, @gz)
    ser.printf("Gyro raw:  ")
    ser.position(DAT_X_COL, 15)
    ser.str(int.decpadded (gx, 7))
    ser.position(DAT_Y_COL, 15)
    ser.str(int.decpadded (gy, 7))
    ser.position(DAT_Z_COL, 15)
    ser.str(int.decpadded (gz, 7))
    ser.clearline()
    ser.newline()

PUB Temp()

    ser.printf("Temp:")
    ser.position(DAT_X_COL, 16)
    decimal(imu.temperature(), 1_00)
    ser.char(lookupz(TEMP_SCALE: "C", "F"))

PUB Calibrate()

    ser.position(0, 21)
    ser.printf("Calibrating...")
    imu.calibrateaccel()
    imu.calibrategyro()
    ser.position (0, 21)
    ser.printf("              ")

PUB DisplaySettings() | axo, ayo, azo, gxo, gyo, gzo

    ser.position(0, 3)
    ser.printf("AccelScale: ")
    ser.dec(imu.accelscale())
    ser.newline()

    ser.printf("AccelLowPassFilter: ")
    ser.dec(imu.accellowpassfilter())
    ser.newline()

    ser.printf("AccelDataRate: ")
    ser.dec(imu.acceldatarate())
    ser.newline()

    ser.printf("GyroScale: ")
    ser.dec(imu.gyroscale())
    ser.newline()

    ser.printf("GyroLowPassFilter: ")
    ser.dec(imu.gyrolowpassfilter())
    ser.newline()

    ser.printf("GyroDataRate: ")
    ser.dec(imu.gyrodatarate())
    ser.newline()

    imu.gyrobias(@gxo, @gyo, @gzo, 0)
    ser.printf("GyroBias: ")
    ser.dec(gxo)
    ser.printf("(x), ")
    ser.dec(gyo)
    ser.printf("(y), ")
    ser.dec(gzo)
    ser.printf("(z)")
    ser.newline()

    imu.accelbias(@axo, @ayo, @azo, 0)
    ser.printf("AccelBias: ")
    ser.dec(axo)
    ser.printf("(x), ")
    ser.dec(ayo)
    ser.printf("(y), ")
    ser.dec(azo)
    ser.printf("(z)")
    ser.newline()

PUB Decimal(scaled, divisor) | whole[4], part[4], places, tmp, sign
' Display a scaled up number as a decimal
'   Scale it back down by divisor (e.g., 10, 100, 1000, etc)
    whole := scaled / divisor
    tmp := divisor
    places := 0
    part := 0
    sign := 0
    if scaled < 0
        sign := "-"
    else
        sign := " "

    repeat
        tmp /= 10
        places++
    until tmp == 1
    scaled //= divisor
    part := int.deczeroed(abs(scaled), places)

    ser.char(sign)
    ser.dec(abs(whole))
    ser.char(".")
    ser.str(part)

PUB Setup()

    ser.start(SER_BAUD)
    waitms(30)
    ser.clear()
    ser.strln("Serial terminal started")
#ifdef ICM20649_SPI
    if imu.start(CS, SCK, MOSI, MISO, SCK_FREQ)
        imu.defaults()
        imu.presetimuactive()
        ser.strln("ICM20649 driver started (SPI)")
#elseifdef ICM20649_I2C
    if imu.start(SCL_PIN, SDA_PIN, I2C_HZ, ADDR_BITS)
        imu.defaults()
        imu.presetimuactive()
        ser.strln("ICM20649 driver started (I2C)")
#endif
    else
        ser.strln("ICM20649 driver failed to start - halting")
        imu.stop()
        waitms(5)
        repeat

DAT
{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

{
    --------------------------------------------
    Filename: AHT20-Demo.spin2
    Author: Jesse Burt
    Description: Demo of the AHT20 driver (P2 version)
    Copyright (c) 2022
    Started Mar 27, 2022
    Updated Mar 27, 2022
    See end of file for terms of use.
    --------------------------------------------
}

CON

    _clkfreq    = cfg._clkfreq_def
    _xtlfreq    = cfg._xtlfreq

' -- User-defined constants
    SER_BAUD    = 2_000_000
    LED         = cfg.LED1

    I2C_SCL     = 0
    I2C_SDA     = 1
    I2C_HZ      = 400_000                       ' max is 400_000
' --

    C           = 0
    F           = 1
    K           = 2

OBJ

    cfg : "core.con.boardcfg.p2eval"
    ser : "com.serial.terminal.ansi"
    trh : "sensor.temp_rh.aht20"
    int : "string.integer"

PUB Main()

    setup()

    trh.reset()
    trh.tempscale(C)                            ' set temperature scale

    repeat
        trh.measure()
        repeat until trh.temprhdataready()
        ser.position(0, 3)

        ser.str(string("Temperature: "))
        decimal(trh.lasttemp(), 100)
        ser.newline()

        ser.str(string("Relative humidity: "))
        decimal(trh.lastrh(), 100)
        ser.newline()

        waitms(1000)

PRI Decimal(scaled, divisor) | whole[4], part[4], places, tmp, sign
' Display a scaled up number as a decimal
'   Scale it back down by divisor (e.g., 10, 100, 1000, etc)
    whole := scaled / divisor
    tmp := divisor
    places := 0
    part := 0
    sign := 0
    if scaled < 0
        sign := "-"
    else
        sign := " "

    repeat
        tmp /= 10
        places++
    until tmp == 1
    scaled //= divisor
    part := int.deczeroed(abs(scaled), places)

    ser.char(sign)
    ser.dec(abs(whole))
    ser.char(".")
    ser.str(part)


PUB Setup()

    ser.start(SER_BAUD)
    waitms(30)
    ser.clear()
    ser.strln(string("Serial terminal started"))

    if (trh.startx(I2C_SCL, I2C_SDA, I2C_HZ))
        ser.strln(string("AHT20 driver started"))
    else
        ser.strln(string("AHT20 driver failted to start - halting"))
        repeat

DAT
{
TERMS OF USE: MIT License

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
}


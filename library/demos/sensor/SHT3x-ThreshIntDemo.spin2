{
    --------------------------------------------
    Filename: SHT3x-ThreshIntDemo.spin2
    Author: Jesse Burt
    Description: Demo of the SHT3x driver
        Threshold interrupt functionality (P2 version)
    Copyright (c) 2022
    Started Jan 8, 2022
    Updated Mar 27, 2022
    See end of file for terms of use.
    --------------------------------------------
}

CON

    _clkfreq        = cfg._clkfreq_def
    _xtlfreq        = cfg._xtlfreq

' -- User-modifiable constants
    LED             = cfg.LED1
    SER_BAUD        = 2_000_000

    SCL_PIN         = 0
    SDA_PIN         = 1
    RESET_PIN       = 2                         ' optional (-1 to disable)
    INT1            = 3                         ' ALERT pin (active high)

    ADDR_BIT        = 0                         ' 0, 1: opt. I2C address bit
    I2C_HZ          = 1_000_000                 ' max is 1_000_000
' --

' Temperature scale
    C               = 0
    F               = 1

OBJ

    cfg     : "core.con.boardcfg.p2eval"
    ser     : "com.serial.terminal.ansi"
    sht3x   : "sensor.temp_rh.sht3x"
    int     : "string.integer"

VAR

    long _isr_stack[50]                         ' stack for ISR core
    long _intflag                               ' interrupt flag

PUB Main() | dr

    setup()

    dr := 2                                     ' data rate: 0 (0.5), 1, 2, 4, 10Hz
    sht3x.repeatability(0)
    sht3x.datarate(dr)
    sht3x.opmode(sht3x.CONT)

    sht3x.tempscale(C)
    sht3x.intrhhithresh(25)                     ' RH hi/lo thresholds
    sht3x.intrhlothresh(5)
    sht3x.intrhhiclear(24)                      ' hi/lo thresh hysteresis
    sht3x.intrhloclear(6)

    sht3x.inttemphithresh(30)                   ' temp hi/lo thresholds
    sht3x.inttemplothresh(10)
    sht3x.inttemphiclear(29)                    ' hi/lo thresh hysteresis
    sht3x.inttemploclear(7)

    ser.strln(string("Set thresholds:"))
    ser.printf(string("RH Set low: %d  hi: %d\n"), sht3x.intrhlothresh(-2), {
}   sht3x.intrhhithresh(-2))
    ser.printf(string("RH Clear low: %d  hi: %d\n"), sht3x.intrhloclear(-2), {
}   sht3x.intrhhiclear(-2))
    ser.printf(string("Temp Set low: %d  hi: %d\n"), sht3x.inttemplothresh(-256), {
}   sht3x.inttemphithresh(-256))
    ser.printf(string("Temp Clear low: %d  hi: %d\n"), sht3x.inttemploclear(-256), {
}   sht3x.inttemphiclear(-256))

    repeat
        if (dr > 0)
            waitms(1000/dr)
        else
            waitms(2000)
        ser.position(0, 10)

        ser.str(string("Temperature: "))
        decimal(sht3x.temperature(), 100)
        ser.newline()

        ser.str(string("Relative humidity: "))
        decimal(sht3x.lastrh(), 100)
        ser.newline()
        if _intflag
            ser.position(0, 12)
            ser.str(string("Interrupt"))
        else
            ser.position(0, 12)
            ser.clearline()

PRI Decimal(scaled, divisor) | whole[4], part[4], places, tmp, sign
' Display a scaled up number as a decimal
'   Scale it back down by divisor (e.g., 10, 100, 1000, etc)
    whole := scaled / divisor
    tmp := divisor
    places := 0
    part := 0
    sign := 0
    if scaled < 0
        sign := "-"
    else
        sign := " "

    repeat
        tmp /= 10
        places++
    until tmp == 1
    scaled //= divisor
    part := int.deczeroed(abs(scaled), places)

    ser.char(sign)
    ser.dec(abs(whole))
    ser.char(".")
    ser.str(part)

PRI ISR()
' Interrupt service routine
    pinf(INT1)                                  ' INT1 as input
    repeat
        repeat until pinr(INT1) == 1            ' wait for INT1 (active high)
        _intflag := 1                           '   set flag
        repeat until pinr(INT1) == 0            ' now wait for it to clear
        _intflag := 0                           '   clear flag

PUB Setup()

    ser.start(SER_BAUD)
    waitms(30)
    ser.clear()
    ser.strln(string("Serial terminal started"))

    if sht3x.startx(SCL_PIN, SDA_PIN, I2C_HZ, ADDR_BIT, RESET_PIN)
        ser.strln(string("SHT3x driver started"))
    else
        ser.strln(string("SHT3x driver failed to start - halting"))
        repeat

    cogspin(NEWCOG, isr, @_isr_stack)                    ' start ISR in another core

DAT
{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

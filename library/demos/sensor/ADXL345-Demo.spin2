{
    --------------------------------------------
    Filename: ADXL345-Demo.spin2
    Author: Jesse Burt
    Description: Demo of the ADXL345 driver (P2 version)
    Copyright (c) 2020
    Started Mar 14, 2020
    Updated Jul 19, 2020
    See end of file for terms of use.
    --------------------------------------------
}

CON

    _xtlfreq        = cfg._xtlfreq
    _clkfreq        = cfg._clkfreq_def

' -- User-modifiable constants
    LED             = cfg.LED1
    SER_RX          = cfg.SER_RX
    SER_TX          = cfg.SER_TX
    SER_BAUD        = 2_000_000

    CS_PIN          = 8
    SCL_PIN         = 11
    SDA_PIN         = 10
    SDO_PIN         = 9
    SCL_HZ          = 5_000_000
' --

OBJ

    cfg     : "core.con.boardcfg.p2eval"
    ser     : "com.serial.terminal.ansi"
    time    : "time"
    int     : "string.integer"
    accel   : "sensor.accel.3dof.adxl345.spi.spin2"

VAR

    long _overruns
    byte _ser_cog, _accel_cog

PUB Main() | dispmode

    Setup()

    accel.OpMode(accel.STANDBY)
    accel.AccelADCRes(accel.FULL)                           ' 10, accel.FULL (dynamic, based on AccelScale)
    accel.AccelScale(2)                                     ' 2, 4, 8, 16 (g's)
    accel.AccelDataRate(100)                                ' 0_10, 0_20, 0_39, 0_78, 1_56, 3_13, 6_25, 12_5,
'                                                               25, 50, 100, 200, 400, 800, 1600, 3200
    accel.FIFOMode(accel.BYPASS)                            ' accel.BYPASS, accel.FIFO, accel.STREAM, accel.TRIGGER
    accel.IntMask(%0000_0000)                               ' 0, 1 each bit
    accel.AccelSelfTest(FALSE)                              ' FALSE, TRUE
    accel.OpMode(accel.MEASURE)                             ' accel.STANDBY, accel.MEASURE
    ser.HideCursor()

    dispmode := 0

    ser.position(0, 3)
    ser.printf("AccelScale: %d\n", accel.AccelScale())
    ser.printf("AccelADCRes: %d\n", accel.AccelADCRes())
    ser.printf("AccelDataRate: %d\n", accel.AccelDataRate())
    ser.printf("FIFOMode: %d\n", accel.FIFOMode())
    ser.printf("IntMask: %d\n", accel.IntMask())
    ser.printf("AccelSelfTest: %d\n", accel.AccelSelfTest())

    repeat
        case ser.RxCheck()
            "q", "Q":
                ser.Position(0, 12)
                ser.printf("Halting")
                accel.Stop()
                waitms(5)
                quit
            "c", "C":
                Calibrate()
            "r", "R":
                ser.Position(0, 10)
                repeat 2
                    ser.ClearLine(ser.CLR_CUR_TO_END)
                    ser.Newline()
                dispmode ^= 1

        ser.Position (0, 10)
        case dispmode
            0: AccelRaw()
            1: AccelCalc()

    ser.ShowCursor()
    FlashLED(LED, 100)

PUB AccelCalc() | ax, ay, az

    repeat until accel.AccelDataReady()
    accel.AccelG (@ax, @ay, @az)
    if accel.AccelDataOverrun()
        _overruns++
    ser.printf("Accel micro-g: ")
    ser.Str (int.DecPadded (ax, 10))
    ser.Str (int.DecPadded (ay, 10))
    ser.Str (int.DecPadded (az, 10))
    ser.Newline()
    ser.printf("Overruns: %d", _overruns)

PUB AccelRaw() | ax, ay, az

    repeat until accel.AccelDataReady()
    accel.AccelData (@ax, @ay, @az)
    if accel.AccelDataOverrun()
        _overruns++
    ser.printf("Raw Accel: ")
    ser.Str (int.DecPadded (ax, 7))
    ser.Str (int.DecPadded (ay, 7))
    ser.Str (int.DecPadded (az, 7))

    ser.Newline()
    ser.printf("Overruns: ")
    ser.Dec (_overruns)

PUB Calibrate()

    ser.Position (0, 12)
    ser.printf("Calibrating...")
    accel.Calibrate()
    time.sleep(1)
    ser.Position (0, 12)
    ser.printf("              ")

PUB Setup()

    repeat until _ser_cog := ser.StartRXTX (SER_RX, SER_TX, 0, SER_BAUD)
    ser.Clear()
    ser.printf("Serial terminal started\n")
    if _accel_cog := accel.Start(CS_PIN, SCL_PIN, SDA_PIN, SDO_PIN, SCL_HZ)
        ser.printf("ADXL345 driver started\n")
        accel.Defaults()
    else
        ser.printf("ADXL345 driver failed to start - halting\n")
        accel.Stop()
        waitms (5)
        FlashLED(LED, 500)

#include "lib.utility.spin2"

DAT
{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

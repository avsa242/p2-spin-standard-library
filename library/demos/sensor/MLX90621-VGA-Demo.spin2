 {
    --------------------------------------------
    Filename: MLX90621-VGA-Demo.spin2
    Author: Jesse Burt
    Description: Demo of the MLX90621 driver using a
        VGA display (P2 version)
    Copyright (c) 2022
    Started: Jun 26, 2020
    Updated: Nov 12, 2022
    See end of file for terms of use.
    --------------------------------------------
}

CON

    _xtlfreq        = cfg._xtlfreq
    _clkfreq        = 250_000_000

' -- User-modifiable constants
    LED             = cfg.LED1
    SER_BAUD        = 2_000_000

    SCL_PIN         = 0
    SDA_PIN         = 1
    I2C_FREQ        = 1_000_000

    VGA_BASEPIN     = 32                        ' 0, 8, 16, 24, 32, 40, 48
' --

    WIDTH           = 320
    HEIGHT          = 240
    XMAX            = WIDTH-1
    YMAX            = HEIGHT-1
    CENTERX         = XMAX/2
    CENTERY         = YMAX/2
    BUFFSZ          = WIDTH * HEIGHT
    BPP             = 1
    BPL             = WIDTH * BPP

OBJ

    cfg     : "boardcfg.p2eval"
    ser     : "com.serial.terminal.ansi"
    mlx     : "sensor.thermal-array.mlx90621"
    vga     : "display.vga.bitmap-8bpp"
    fnt     : "font.5x8"

VAR

    long _keyinput_stack[50]
    long _ir_frame[66]
    long _offset
    long _settings_changed

    word _mlx_refrate

    word _fx, _fy, _fw, _fh

    byte _disp_buff[BUFFSZ]
    byte _mlx_adc_res, _mlx_adcref
    byte _invert_x, _col_scl
    byte _hotspot_mark

PUB main()

    vga.set_timings(10, 33, 16, 89, 85, 640)
    setup()
    draw_vscale(XMAX-5, 0, YMAX)

    repeat
        if (_settings_changed)
            update_settings()
        mlx.get_frame(@_ir_frame)
        draw_frame(_fx, _fy, _fw, _fh)

PUB draw_frame(fx, fy, pixw, pixh) | x, y, color_c, ir_offset, pixsx, pixsy, pixex, pixey, maxx, {
}                                   maxy, maxp
' Draw the thermal image
    vga.wait_vsync()                             ' wait for vertical sync
    repeat y from 0 to mlx.YMAX
        repeat x from 0 to mlx.XMAX
            if (_invert_x)                      ' Invert X display if set
                ir_offset := ((mlx.XMAX-x) * 4) + y
            else
                ir_offset := (x * 4) + y
            ' compute color
            color_c := (_ir_frame[ir_offset] * _col_scl) / 256 + _offset
            pixsx := fx + (x * pixw)            ' start and end image pixel
            pixsy := fy + (y * (pixh + 1))      '   coords
            pixex := pixsx + pixw
            pixey := pixsy + pixh

            if (_ir_frame[ir_offset] > maxp)    ' Check if this is the hottest
                maxp := _ir_frame[ir_offset]    '   spot in the image
                maxx := pixsx
                maxy := pixsy
            vga.box(pixsx, pixsy, pixex, pixey, color_c, TRUE)

    if (_hotspot_mark)                          ' Mark hotspot
        ' white box
'        vga.box(maxx, maxy, maxx+pixw, maxy+pixh, vga.MAX_COLOR, false)

        ' white cross-hair
        vga.line(maxx, maxy+(pixh/2), maxx+pixw, maxy+(pixh/2), vga.MAX_COLOR)
        vga.line(maxx+(pixw/2), maxy, maxx+(pixw/2), maxy+pixh, vga.MAX_COLOR)

PUB draw_vscale(x, y, ht) | idx, color, scl_width, bottom, top, range
' Draw the color scale setup at program start
    range := bottom := y+ht
    top := y
    scl_width := 5

    repeat idx from bottom to top
        color := (range-idx)
        vga.line(x, idx, x+scl_width, idx, color)

PUB update_settings() | col, row, reftmp
' Settings have been changed by the user - update the sensor and the
'   displayed settings
    mlx.temp_adc_res(_mlx_adc_res)              ' Update sensor with current
    mlx.refresh_rate(_mlx_refrate)              '   settings
    mlx.adc_ref(_mlx_adcref)

    reftmp := mlx.adc_ref()                     ' read from sensor for display
    col := 0
    row := (vga.textrows()-1) - 5               ' Position at screen bottom
    vga.fgcolor(vga.MAX_COLOR)
    vga.pos_xy(col, row)

    vga.printf(@"X-axis invert: %s\n", lookupz(_invert_x: @"No ", @"Yes"))
    vga.printf(@"FPS: %dHz   \n", mlx.refresh_rate())
    vga.printf(@"ADC: %dbits\n", mlx.temp_adc_res())
    vga.printf(@"ADC reference: %s\n", lookupz(reftmp: @"High", @"Low  "))

    _fx := CENTERX - ((_fw * 16) / 2)           ' Approx center of screen
    _fy := 10
    vga.box(0, 0, XMAX-10, CENTERY, 0, TRUE)    ' Clear out last thermal image
                                                ' (in case resizing smaller)
    _settings_changed := FALSE

PUB cog_key_input() | cmd

    repeat
        repeat until cmd := ser.getchar()
        case cmd
            "A":                                ' ADC resolution (bits)
                _mlx_adc_res := (_mlx_adc_res + 1) <# 18
            "a":
                _mlx_adc_res := (_mlx_adc_res - 1) #> 15
            "C":                                ' Color scaling/contrast
                _col_scl := (_col_scl + 1) <# 16' ++
            "c":
                _col_scl := (_col_scl - 1) #> 1 ' --
            "F":                                ' sensor refresh rate (Hz)
                _mlx_refrate := (_mlx_refrate * 2) <# 512
            "f":
                _mlx_refrate := (_mlx_refrate / 2) #> 1
            "h":                                ' mark hotspot on/off
                _hotspot_mark ^= 1
            "r":                                ' sensor ADC reference (hi/low)
                _mlx_adcref ^= 1
            "S":                                ' thermal image pixel size
                _fw := (_fw + 1) <# 18          ' ++
                _fh := (_fh + 1) <# 18
            "s":
                _fw := (_fw - 1) #> 1           ' --
                _fh := (_fh - 1) #> 1
            "-":                                ' thermal image reference level
                _offset := 0 #> (_offset - 1)   '   or color offset
            "=":
                _offset := (_offset + 1) <# vga.MAX_COLOR
            "x":                                ' invert thermal image X-axis
                _invert_x ^= 1
            other:
                next
        _settings_changed := TRUE               ' trigger for main loop to call update_settings()

PUB setup()

    ser.start(SER_BAUD)
    waitms(30)
    ser.clear()
    ser.strln(@"Serial terminal started")

    setup_palette()
    vga.start(VGA_BASEPIN, WIDTH, HEIGHT, @_disp_buff)
        ser.strln(@"VGA 8bpp driver started")
        vga.font_addr(fnt.ptr())
        vga.font_scl(1)
        vga.font_sz(6, 8)
        vga.clear()

    if mlx.startx(SCL_PIN, SDA_PIN, I2C_FREQ)
        ser.strln(@"MLX90621 driver started")
        mlx.defaults()
        mlx.opmode(mlx.CONT)
        _mlx_adc_res := 18                       ' Initial sensor settings
        _mlx_refrate := 32
        _mlx_adcref := 1
    else
        ser.strln(@"MLX90621 driver failed to start - halting")
        repeat

    _col_scl := 8
    _fw := 10
    _fh := 10
    _invert_x := 0
    cogspin(16, cog_key_input(), @_keyinput_stack)
    _settings_changed := TRUE

PUB setup_palette() | i, r, g, b, c, ptr_pal
' Set up palette
    r := g := b := c := 0
    ptr_pal := vga.palette_ptr()
    repeat i from 0 to vga.MAX_COLOR
        case i
            0..31:
                r += 4
                g := 0
                b += 4
            32..63:
                r -= 4
                g := 0
                b := b
            64..95:
                r := 0
                g += 4
                b := b
            96..127:
                r := 0
                g := g
                b -= 4
            128..159:
                r += 4
                g := g
                b := b
            160..191:
                r := r
                g -= 4
                b := 0
            192..254:
                r := r
                g += 4
                b += 4
            255:
                r := g := b := 255

        c := 0 | (r << 16) | (g << 8) | b
        long[ptr_pal][i] := c
    long[ptr_pal][0] := $00

DAT
{
Copyright 2022 Jesse Burt

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
associated documentation files (the "Software"), to deal in the Software without restriction,
including without limitation the rights to use, copy, modify, merge, publish, distribute,
sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or
substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT
NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT
OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}


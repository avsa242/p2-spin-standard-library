{
    --------------------------------------------
    Filename: MMA7455-ThreshIntDemo.spin2
    Author: Jesse Burt
    Description: Demo of the MMA7455 driver
        Threshold interrupt functionality (P2 version)
    Copyright (c) 2022
    Started Jul 17, 2022
    Updated Jul 17, 2022
    See end of file for terms of use.
    --------------------------------------------
}

CON

    _clkfreq    = cfg._clkfreq_def
    _xtlfreq    = cfg._xtlfreq

' -- User-modifiable constants
    LED         = cfg.LED1
    SER_BAUD    = 2_000_000

    SCL_PIN     = 0
    SDA_PIN     = 1
    I2C_FREQ    = 400_000
    ADDR_BITS   = 0

    INT1        = 2
' --

    DAT_X_COL   = 20
    DAT_Y_COL   = DAT_X_COL + 15
    DAT_Z_COL   = DAT_Y_COL + 15

OBJ

    cfg     : "boardcfg.p2eval"
    ser     : "com.serial.terminal.ansi"
    int     : "string.integer"
    accel   : "sensor.accel.3dof.mma7455"

VAR

    long _isr_stack[50]                         ' stack for ISR core
    long _intflag                               ' interrupt flag

PUB Main()

    setup()

    accel.preset_threshdetect()                 ' set up for accel threshold
                                                '   detection

    accel.accelintclear(accel.INT1 | accel.INT2)' clear INT1 and INT2

    ' Set threshold to 1.0g, and enable detection on X axis only
    ' NOTE: Though there are threshold setting methods for all three
    '   axes, they are locked together (chip limitation). This is done
    '   for API-compatibility with other chips that have the ability to
    '   set independent thresholds.
    ' NOTE: The full-scale range of the threshold setting is 8g's,
    '   regardless of what AccelScale() is set to.
    accel.accelintthreshz(1_000000)
    accel.accelintmask(accel.XTHR)

    repeat
        ser.position(0, 3)
        accelcalc()
        if _intflag
            ser.position(0, 5)
            ser.strln(@"Interrupt")
            repeat until ser.charin()
            accel.accelintclear(%11)            ' must clear interrupts
            ser.position(0, 5)
            ser.clearline()
        if ser.rxcheck() == "c"                 ' press the 'c' key in the demo
            calibrate()                         ' to calibrate sensor offsets

PUB AccelCalc() | ax, ay, az

    repeat until accel.acceldataready()         ' wait for new sensor data set
    accel.accelg(@ax, @ay, @az)                 ' read calculated sensor data
    ser.str(@"Accel (g):")
    ser.positionx(DAT_X_COL)
    decimal(ax, 1000000)                        ' data is in micro-g's; display
    ser.positionx(DAT_Y_COL)                    ' it as if it were a float
    decimal(ay, 1000000)
    ser.positionx(DAT_Z_COL)
    decimal(az, 1000000)
    ser.clearline()
    ser.newline()

PUB Calibrate()

    ser.position(0, 7)
    ser.str(@"Calibrating...")
    accel.calibrateaccel()
    ser.positionx(0)
    ser.clearline()

PRI Decimal(scaled, divisor) | whole[4], part[4], places, tmp, sign
' Display a scaled up number as a decimal
'   Scale it back down by divisor (e.g., 10, 100, 1000, etc)
    whole := scaled / divisor
    tmp := divisor
    places := 0
    part := 0
    sign := 0
    if scaled < 0
        sign := "-"
    else
        sign := " "

    repeat
        tmp /= 10
        places++
    until tmp == 1
    scaled //= divisor
    part := int.deczeroed(abs(scaled), places)

    ser.char(sign)
    ser.dec(abs(whole))
    ser.char(".")
    ser.str(part)
    ser.chars(" ", 5)

PRI ISR()
' Interrupt service routine
    dira[INT1] := 0                             ' INT1 as input
    repeat
        repeat until pinr(INT1)                 ' wait for INT1 (active low)
        _intflag := 1                           '   set flag
        repeat until not pinr(INT1)             ' now wait for it to clear
        _intflag := 0                           '   clear flag

PUB Setup()

    ser.start(SER_BAUD)
    waitms(30)
    ser.clear()
    ser.strln(@"Serial terminal started")

    if accel.startx(SCL_PIN, SDA_PIN, I2C_FREQ, ADDR_BITS)
        ser.strln(@"MMA7455 driver started (I2C)")
    else
        ser.strln(@"MMA7455 driver failed to start - halting")
        repeat

    cogspin(NEWCOG, isr, @_isr_stack)                    ' start ISR in another core

DAT
{
TERMS OF USE: MIT License

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
}


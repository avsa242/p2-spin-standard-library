{
    --------------------------------------------
    Filename: PCF8563-Demo.spin
    Author: Jesse Burt
    Description: Demo of the PCF8563 driver
    Copyright (c) 2020
    Started Sep 6, 2020
    Updated Sep 7, 2020
    See end of file for terms of use.
    --------------------------------------------
}

CON

    _clkfreq    = cfg._clkfreq_def
    _xtlfreq    = cfg._xtlfreq

' -- User-modifiable constants
    SER_BAUD    = 2_000_000
    LED         = cfg.LED1

    I2C_SCL     = 26
    I2C_SDA     = 27
    I2C_HZ      = 400_000
' --

' Named constants that can be used in place of numerical month, or weekday
    #1, JAN, FEB, MAR, APR, MAY, JUN, JUL, AUG, SEP, OCT, NOV, DEC
    #1, SUN, MON, TUE, WED, THU, FRI, SAT

OBJ

    cfg     : "core.con.boardcfg.p2eval"
    ser     : "com.serial.terminal.ansi"
    time    : "time"
    int     : "string.integer"
    rtc     : "time.rtc.pcf8563.i2c"

PUB Main() | wkday, month, day, yr

    setup()

' Uncomment below to set date/time
'                hh, mm, ss, MMM, DD, WKDAY, YY
'    setdatetime(08, 51, 00, NOV, 18, WED, 20)

    repeat
        rtc.pollrtc()
        wkday := @wkday_name[(rtc.weekday() - 1) * 4] ' Pull strings from
        month := @month_name[(rtc.month() - 1) * 4]   ' DAT table below
        day := int.deczeroed(rtc.day(), 2)
        yr := rtc.year()

        ser.position(0, 3)
        ser.str(wkday)
        ser.printf(string(" %s %s 20%d "), day, month, yr, 0, 0, 0)

        ser.str(int.deczeroed(rtc.hours(), 2))    ' Discrete statements
        ser.char(":")                               ' due to a bug in
        ser.str(int.deczeroed(rtc.minutes(), 2))  ' string.integer
        ser.char(":")
        ser.str(int.deczeroed(rtc.seconds(), 2))

PUB SetDateTime(h, m, s, mmm, dd, wkday, yy)

    rtc.hours(h)                                ' 00..23
    rtc.minutes(m)                              ' 00..59
    rtc.seconds(s)                              ' 00..59

    rtc.month(mmm)                              ' 01..12
    rtc.day(dd)                                 ' 01..31
    rtc.weekday(wkday)                          ' 01..07
    rtc.year(yy)                                ' 00..99

PUB Setup()

    ser.start(SER_BAUD)
    waitms(30)
    ser.clear()
    ser.printf("Serial terminal started\n")
    if rtc.start(I2C_SCL, I2C_SDA, I2C_HZ)
        ser.printf("PCF8563 driver started\n")
    else
        ser.printf("PCF8563 driver failed to start - halting\n")
        rtc.stop()
        waitms(50)

DAT
' Tables for mapping numbers to weekday and month names
    wkday_name
            byte    "Sun", 0                    ' 1
            byte    "Mon", 0                    ' 2
            byte    "Tue", 0                    ' 3
            byte    "Wed", 0                    ' 4
            byte    "Thu", 0                    ' 5
            byte    "Fri", 0                    ' 6
            byte    "Sat", 0                    ' 7

    month_name
            byte    "Jan", 0                    ' 1
            byte    "Feb", 0
            byte    "Mar", 0
            byte    "Apr", 0
            byte    "May", 0
            byte    "Jun", 0
            byte    "Jul", 0
            byte    "Aug", 0
            byte    "Sep", 0
            byte    "Oct", 0
            byte    "Nov", 0
            byte    "Dec", 0                    ' 12

DAT
{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

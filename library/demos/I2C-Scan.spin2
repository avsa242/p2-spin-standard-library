{
    --------------------------------------------
    Filename: I2C-Scanner.spin2
    Author: Jesse Burt
    Description: Simple utility to scan for active slaves on an
        I2C bus
    Copyright (c) 2020
    Started Jun 17, 2019
    Updated Mar 23, 2020
    See end of file for terms of use.
    --------------------------------------------
}

CON

    XTAL        = cfg#XTAL
    XDIV        = cfg#XDIV
    XMUL        = cfg#XMUL
    XDIVP       = cfg#XDIVP
    XOSC        = cfg#XOSC
    XSEL        = cfg#XSEL
    XPPPP       = cfg#XPPPP
    CLOCKFREQ   = cfg#CLOCKFREQ
    SETFREQ     = cfg#SETFREQ
    ENAFREQ     = cfg#ENAFREQ

    I2C_SCL     = 39
    I2C_SDA     = 38
    I2C_HZ      = 100_000

    LED         = cfg#LED1
    SER_RX      = cfg#SER_RX
    SER_TX      = cfg#SER_TX
    SER_BAUD    = 2_000_000

    ESC         = 27

OBJ

    cfg     : "core.con.boardcfg.p2eval"
    ser     : "com.serial.terminal.ansi"
    i2c     : "com.i2c"
    io      : "io"
    time    : "time"
    int     : "string.integer"

VAR

    long _key_cog, _keyinput_stack[50]
    byte _fmt

PUB Main | slave_addr, flag, x, y, offsetx, offsety, start, end

    Setup
    ser.Clear
    ser.Bold
    ser.Underline
    ser.PrintF ("I2C Scanner")
    ser.Underline(FALSE)
    offsetx := 3
    offsety := 3
    start := $02
    end := $FF
    _fmt := 7                       '7 or 8 bit format Slave addresses (affects display only)
    ser.Italic
    ser.PrintF (" (%d-bit format)", _fmt)
    ser.Reset
    ser.HideCursor
    repeat
        ser.Position(13, 0)
        ser.Dec(_fmt)
        repeat slave_addr from start to end step 2
            flag := i2c.present (slave_addr)
            x := ((slave_addr & $F) + (slave_addr & $F)) + offsetx
            y := (slave_addr >> 4) + offsety
            PrintSA (x, y, slave_addr, flag)
        time.MSleep(100)
    until _key_cog == 0

    Stop
    FlashLED(LED, 100)

PUB PrintSA(x, y, slave_addr, flag)            

    case _fmt
        7:
            case flag
                TRUE:
                    ser.Position (x, y)
                    ser.Color (ser#FG_BLACK, ser#BG_GREEN)
'                    ser.Char ("/")
                    ser.Hex (slave_addr >> 1, 2)
'                    ser.Char ("/")
                    ser.Reset

                FALSE:
                    ser.Position (x, y)
'                    ser.Char (" ")
                    ser.Hex (slave_addr >> 1, 2)
'                    ser.Char (" ")

        8:
            case flag
                TRUE:
                    ser.Position (x, y)
                    ser.Color (ser#FG_BLACK, ser#BG_GREEN)
 '                   ser.Char ("/")
                    ser.Hex (slave_addr, 2)
'                    ser.Char ("/")
                    ser.Reset

                FALSE:
                    ser.Position (x, y)
 '                   ser.Char (" ")
                    ser.Hex (slave_addr, 2)
'                    ser.Char (" ")
        OTHER:

PUB KeyInput | tmp
' Wait for keypress from user
    repeat
        repeat
            tmp := ser.RXCheck
        until tmp <> -1

        case tmp
            "7": _fmt := 7
            "8": _fmt := 8
            "q", "Q":
                _key_cog := 0
                cogstop(cogid)
            OTHER:

PUB Setup

    clkset(ENAFREQ, CLOCKFREQ)
    ser.StartRxTx (SER_RX, SER_TX, 0, SER_BAUD)
    time.MSleep(5)
    ser.Reset
    ser.Clear
    ser.PrintF("Serial terminal started\n")
    i2c.Setup (I2C_SCL, I2C_SDA, I2C_HZ)
    ser.PrintF("I2C driver started\n")
    _key_cog := cognew (KeyInput, @_keyinput_stack)

PUB Stop

    ser.Reset
    ser.ShowCursor
    ser.Clear
    ser.PrintF("Stopped\n")

#include "lib.utility.spin2"

DAT
{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

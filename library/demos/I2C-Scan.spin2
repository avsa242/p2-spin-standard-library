{
    --------------------------------------------
    Filename:
    Author:
    Description:
    Copyright (c) 20__
    Started Month Day, Year
    Updated Month Day, Year
    See end of file for terms of use.
    --------------------------------------------
}

CON

    XTAL        = cfg#XTAL
    XDIV        = cfg#XDIV
    XMUL        = cfg#XMUL
    XDIVP       = cfg#XDIVP
    XOSC        = cfg#XOSC
    XSEL        = cfg#XSEL
    XPPPP       = cfg#XPPPP
    CLOCKFREQ   = cfg#CLOCKFREQ
    SETFREQ     = cfg#SETFREQ
    ENAFREQ     = cfg#ENAFREQ

    I2C_SCL     = 27
    I2C_SDA     = 28
    I2C_HZ      = 100_000

    LED         = cfg#LED1
    SER_BAUD    = 2_000_000

    ESC         = 27

OBJ

    cfg     : "core.con.boardcfg.p2eval"
    ser     : "com.smartserial"
    i2c     : "com.i2c.spin2"
    time    : "time"
    int     : "string.integer"

VAR

    byte _fmt

PUB Main | slave_addr, flag, x, y, offsetx, offsety, start, end

    Setup
    Clear
    Position(0, 0)
    ser.Str (@white)
    ser.Str (string("I2C Scanner"))
    offsetx := 3
    offsety := 3
    start := $02
    end := $FF
    _fmt := 7                       '7 or 8 bit format Slave addreses (affects display only)
    ser.Str (string(" ("))
    ser.Dec (_fmt)
    ser.Str (string("-bit format)"))
    ser.Str(@grey)
    repeat
        repeat slave_addr from start to end step 2
            flag := i2c.present (slave_addr)
            x := ((slave_addr & $F) + (slave_addr & $F)) + offsetx
            y := (slave_addr >> 4) + offsety
            PrintSA (x, y, slave_addr, flag)
        time.MSleep (100)

PUB PrintSA(x, y, slave_addr, flag)            

    case _fmt
        7:
            case flag
                TRUE:
                    Position (x, y)
                    ser.Str(@green)
                    ser.tx ("/")
                    ser.Hex (slave_addr >> 1, 2)
                    ser.tx ("/")
                    ser.Str(@grey)

                FALSE:
                    Position (x, y)
                    ser.tx (" ")
                    ser.Hex (slave_addr >> 1, 2)
                    ser.tx (" ")

        8:
            case flag
                TRUE:
                    Position (x, y)
                    ser.Str(@green)
                    ser.tx ("/" )
                    ser.Hex (slave_addr, 2)
                    ser.tx ("/")
                    ser.Str(@grey)

                FALSE:
                    Position (x, y)
                    ser.tx (" ")
                    ser.Hex (slave_addr, 2)
                    ser.tx (" ")
        OTHER:

PUB CSI

    ser.tx (27)
    ser.tx ("[")

PUB Clear

    CSI
    ser.tx ("2")
    ser.tx ("J")

PUB Home

    CSI
    ser.tx ("f")

PUB Position(x, y)
' Position cursor at column x, row y (from top-left).
    CSI
    ser.Dec (y)
    ser.tx (";")
    ser.Dec (x)
    ser.tx ("f")

PUB NL

    ser.tx (10)
    ser.tx (13)

PUB Setup

    clkset(ENAFREQ, CLOCKFREQ, XSEL)
    ser.Start (63, 62, 0, SER_BAUD)
    time.MSleep(5)
    Clear
    ser.Str(string("Serial terminal started"))
    NL
    i2c.Setup (I2C_SCL, I2C_SDA, I2C_HZ)
    ser.Str (string("I2C driver started"))
    NL

PUB Flash(led_pin, delay_ms)

    case led_pin
        0..31:
            dira[led_pin] := 1
            repeat
                !outa[led_pin]
                time.MSleep (delay_ms)

        32..63:
            led_pin &= 31
            dirb[led_pin] := 1
            repeat
                !outb[led_pin]
                time.MSleep (delay_ms)
DAT

    white       byte ESC, "[", "1", ";", "3", "7", "m", 0
    grey        byte ESC, "[", "0", ";", "3", "7", "m", 0
    green       byte ESC, "[", "0", ";", "3", "2", "m", 0

DAT
{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

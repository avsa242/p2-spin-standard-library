{
---------------------------------------------------------------------------------------------------
    Filename:       I2C-Scan.spin2
    Description:    Utility to scan for active devices on an I2C bus
    Author:         Jesse Burt
    Started:        Jun 17, 2019
    Updated:        Jan 21, 2024
    Copyright (c) 2024 - See end of file for terms of use.
---------------------------------------------------------------------------------------------------
}

CON

    _clkfreq    = 180_000_000
    _xtlfreq    = 20_000_000


' -- User-modifiable constants
    I2C_SCL     = 0
    I2C_SDA     = 1
    I2C_HZ      = 100_000
' --


OBJ

    ser:    "com.serial.terminal.ansi" | SER_BAUD=2_000_000
    i2c:    "com.i2c"


VAR

    long _key_cog, _keyinput_stack[50]
    byte _fmt


PUB main() | slave_addr, flag, x, y, offsetx, offsety

    setup()
    ser.clear()
    ser.bold()
    ser.underline()
    ser.str(@"I2C Scanner")
    ser.underline_off()
    ser.bold_off()

    offsetx := 3
    offsety := 3

    ser.italic()
    ser.printf(@" (%d-bit format)", _fmt)
    ser.reset()
    ser.hide_cursor()

    { probe all _legal_ addresses (write devices only) for a device;
        'reserved' addresses (0..6, 120..127) are not touched since it's possible doing so
        could cause issues for some devices }
    repeat
        ser.pos_xy(13, 0)
        ser.dec(_fmt)
        repeat slave_addr from $08 to $77 step 2
            flag := i2c.present(slave_addr << 1)' probe this address for a device
            x := ((slave_addr & $F) + (slave_addr & $F)) + offsetx
            y := (slave_addr >> 4) + offsety
            show_addr(x, y, slave_addr, flag)
    until (_key_cog == 0)                       ' loop until key input cog is stopped

    ser.show_cursor()                           ' restore the terminal settings
    ser.clear()
    ser.reset()
    repeat


PUB show_addr(x, y, slave_addr, flag)            
' Show I2C device address
    if (_fmt == 7)                              ' 7-bit display format
        if (flag)
            ser.pos_xy(x, y)
            ser.color(ser.BLACK, ser.GREEN)
            ser.puthexs(slave_addr, 2)
            ser.reset()
        else
            ser.pos_xy(x, y)
            ser.puthexs(slave_addr, 2)
    else                                        ' 8-bit display format
        if (flag)
            ser.pos_xy(x, y)
            ser.color(ser.BLACK, ser.GREEN)
            ser.puthexs(slave_addr << 1, 2)
            ser.reset()
        else
            ser.pos_xy(x, y)
            ser.puthexs(slave_addr << 1, 2)


PRI cog_key_input()
' Wait for keypress from user
    repeat
        case ser.getchar()
            "7":
                _fmt := 7
            "8":
                _fmt := 8
            "q", "Q":
                _key_cog := 0                   ' stopping this cog informs the main cog,
                cogstop(cogid())                '   which then resets the terminal settings


PUB setup()

    ser.start()
    ser.reset()
    ser.clear()
    ser.strln(@"Serial terminal started")

    i2c.init(I2C_SCL, I2C_SDA, I2C_HZ)
    ser.strln(@"I2C engine started")
    _key_cog := cogspin(NEWCOG, cog_key_input(), @_keyinput_stack)
    _fmt := 7


DAT
{
Copyright 2024 Jesse Burt

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
associated documentation files (the "Software"), to deal in the Software without restriction,
including without limitation the rights to use, copy, modify, merge, publish, distribute,
sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or
substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT
NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT
OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}

{
   --------------------------------------------
    Filename: com.i2c.spin2
    Description: Bitbanged I2C driver for the P2X8C4M64P
    Author: Jesse Burt
    Copyright (c) 2019
    Created: Jun 15, 2019
    Updated: Jun 15, 2019
    See end of file for terms of use.
    --------------------------------------------

    NOTE: This is a derivative of jm_i2c.spin, the SPIN I2C driver
        originally written by Jon McPhalen. The originl header is
        perserved below.
}
'' =================================================================================================
''
''   File....... jm_i2c.spin2
''   Purpose.... Low-level I2C routines (requires pull-ups on SCL and SDA)
''   Author..... Jon "JonnyMac" McPhalen
''               Copyright (c) 2009-2014 Jon McPhalen
''               -- elements inspired by code from Mike Green
''   E-mail.....
''   Started.... 28 JUL 2009
''   Updated.... 06 APR 2014
''
'' =================================================================================================

'  IMPORTANT Note: This code requires pull-ups on the SDA _and_ SCL lines -- it does not drive
'  the SCL line high.

CON

    #0, ACK, NAK

VAR

    long _scl                                                   ' Bus pins
    long _sda
    long _delay, _hdelay, _qdelay

PUB Setup(I2C_SCL, I2C_SDA, I2C_HZ=100_000)
' Define I2C SCL (clock) and SDA (data) pins
    _delay := (_clkfreq/I2C_HZ)                                 ' Calculate delays based on clock freq
    _hdelay := _delay >> 1
    _qdelay := _delay >> 2

    _scl := I2C_SCL
    _sda := I2C_SDA
    fltl_ (_scl)                                                ' Float pins
    fltl_ (_sda)
    Reset

PUB Present(slave_addr)
' Pings device, returns true if ACK
    Start
    result := (Write(slave_addr) == ACK)
    Stop

PUB Read(ackbit) | i2cbyte
' Read byte from I2C bus
'      Send ackbit to slave: ACK (0) or NAK (1)
    dira[_sda] := 0                                             ' Make SDA input

    repeat 8
        qd
        fltl_(_scl)                                             ' Float SCL
        hd
        i2cbyte := (i2cbyte << 1) | ina[_sda]                   ' Read the bit
        dirh_(_scl)                                             ' SCL low
        qd

    dira[_sda] := !ackbit                                       ' Output ack bit
    qd
    fltl_(_scl)                                                 ' Float SCL
    hd
    dirh_(_scl)                                                 ' SCL low
    qd

    return (i2cbyte & $FF)

PUB Reset
' Reset device
    repeat 9
        dirh_(_scl)
        hd
        fltl_(_scl)
        hd
        if (ina[_sda])
            quit

PUB Start
' I2C Start condition (S, Sr)
' -- will wait if I2C bus SDA pin is held low
    fltl_(_sda)                                                 ' Float pins
    fltl_(_scl)
    repeat
    while (ina[_scl] == 0)                                      ' Allow "clock stretching"
    hd
    dirh_(_sda)                                                 ' SDA low
    hd
    dirh_(_scl)                                                 ' SCL low
    hd

PUB Stop
' I2C Stop condition (P)
    dirh_(_sda)                                                 ' SDA low
    hd
    fltl_(_scl)                                                 ' Float SCL
    hd
    repeat
    until (ina[_scl] == 1)                                      ' Hold for clock stretch
    fltl_(_sda)                                                 ' Float SDA
    hd

PUB Wait(slave_addr) | ackbit
' Waits for I2C device to be ready for new command

    repeat
        Start
        ackbit := write(slave_addr)
    until (ackbit == ACK)

PUB Write(i2cbyte) | ackbit
' Write byte to I2C bus
' -- leaves SCL low
    i2cbyte := (i2cbyte ^ $FF) << 24                             ' Move msb (bit7) to bit31
    repeat 8                                                     ' Output eight bits
        dira[_sda] := i2cbyte <-= 1                              ' Send msb first
        hd'
        fltl_(_scl)                                              ' Float SCL
        hd
        dirh_(_scl)                                              ' SCL low
    fltl_(_sda)                                                  ' Release SDA to read ack bit
    hd
    fltl_(_scl)                                                  ' Float SCL
    hd
    ackbit := ina[_sda]                                          ' Read ack bit
    dirh_(_scl)                                                  ' SCL low
    return ackbit

PRI hd

    waitx_ (_hdelay)

PRI qd

    waitx_ (_qdelay)

DAT
{{

  Terms of Use: MIT License

  Permission is hereby granted, free of charge, to any person obtaining a copy of this
  software and associated documentation files (the "Software"), to deal in the Software
  without restriction, including without limitation the rights to use, copy, modify,
  merge, publish, distribute, sublicense, and/or sell copies of the Software, and to
  permit persons to whom the Software is furnished to do so, subject to the following
  conditions:

  The above copyright notice and this permission notice shall be included in all copies
  or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
  INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
  PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
  CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE
  OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

}}

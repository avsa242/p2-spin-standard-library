{
    --------------------------------------------
    Filename: com.spi.bitbang-fast.spin2
    Description: SPI engine (bit-banged, faster) for the Propeller 2
    Author: Jesse Burt
    Created: Nov 9, 2019
    Updated: Jan 25, 2021
    See end of file for terms of use.
    --------------------------------------------

    NOTE: This is based on SPI_Spin.spin, by Beau Schwabe.
}

VAR

    long _SCK, _MOSI, _MISO, _clk_delay
    long _spi_mode, _cpol, _cog

PUB Null{}
' This is not a top-level object

PUB Init(SCK, MOSI, MISO, SPI_MODE=0, SCK_HZ=1_000_000): status
' Initialize SPI engine using custom pins
'   SCK, MOSI, MISO: 0..63 (each unique)
'   SCK_HZ: 1..*TBD*
'   SPI_MODE: 0..3
'       0: CPOL 0, CPHA 0
'           SCK idles low
'           MISO shifted in on rising clock pulse
'           MOSI shifted out on falling clock pulse
'       1: CPOL 0, CPHA 1
'           SCK idles low
'           MISO shifted in on falling clock pulse
'           MOSI shifted out on rising clock pulse
'       2: CPOL 1, CPHA 0
'           SCK idles high
'           MISO shifted in on falling clock pulse
'           MOSI shifted out on rising clock pulse
'       3: CPOL 1, CPHA 1
'           SCK idles high
'           MISO shifted in on rising clock pulse
'           MOSI shifted out on falling clock pulse
'   NOTE: CS must be handled by the parent object
    longmove(@_SCK, @SCK, 3)                    ' copy pins to hub vars
    mode(SPI_MODE)                              ' set SPI mode

    pinl(MOSI)                                  ' ensure MOSI is an output
    pinf(MISO)                                  ' and MISO is an input
    _clk_delay := ((clkfreq / SCK_HZ) / 2) #> 2 ' calculate SPI clock period
    status := _cog := cogid() + 1               ' return current cogid + 1

PUB DeInit{}
' Deinitialize
'   Float I/O pins, clear out hub vars
    pinf(_SCK)
    pinf(_MOSI)
    pinf(_MISO)
    longfill(@_SCK, 0, 7)

PUB Mode(mode_nr): curr_mode
' Set SPI mode
'   Valid values: 0..3
'   Any other value returns the current setting
    case mode_nr
        0, 1:
            _cpol := 0                          ' clock idles low
        2, 3:
            _cpol := 1                          ' clock idles high
        other:
            return _spi_mode                    ' read current setting

    _spi_mode := mode_nr
    (_cpol == 0) ? (pinl(_SCK)) : (pinh(_SCK))  ' set SCK initial state

PUB RdBlock_LSBF(ptr_buff, nr_bytes) | SCK, MOSI, MISO, clk_delay, b_num, tmp
' Read block of data from SPI bus, least-significant byte first
    longmove(@SCK, @_SCK, 4)                    ' copy hub vars
    pinf(MISO)                                  ' ensure MISO is an input
    case _spi_mode
        0, 2:
            repeat b_num from 0 to nr_bytes-1   ' byte loop
                repeat 8                        ' bit loop
                    ' sample MISO and update current working byte in buffer
                    byte[ptr_buff][b_num] := (byte[ptr_buff][b_num] << 1) | pinr(MISO)
                    pint(SCK)                   ' clock
                    waitx(clk_delay)
                    pint(SCK)
                    waitx(clk_delay)
        1, 3:
            repeat b_num from 0 to nr_bytes-1   ' byte loop
                repeat 8                        ' bit loop
                    waitx(clk_delay)
                    pint(SCK)                   ' clock
                    ' sample MISO and update current working byte in buffer
                    byte[ptr_buff][b_num] := (byte[ptr_buff][b_num] << 1) | pinr(MISO)
                    waitx(clk_delay)
                    pint(SCK)

PUB RdBlock_MSBF(ptr_buff, nr_bytes) | SCK, MOSI, MISO, clk_delay, b_num, tmp
' Read block of data from SPI bus, most-significant byte first
    longmove(@SCK, @_SCK, 4)                    ' copy hub vars
    pinf(MISO)                                  ' ensure MISO is an input
    case _spi_mode
        0, 2:
            repeat b_num from nr_bytes-1 to 0   ' byte loop
                repeat 8                        ' bit loop
                    ' sample MISO and update current working byte in buffer
                    byte[ptr_buff][b_num] := (byte[ptr_buff][b_num] << 1) | pinr(MISO)
                    pint(SCK)                   ' clock
                    waitx(clk_delay)
                    pint(SCK)
                    waitx(clk_delay)
        1, 3:
            repeat b_num from nr_bytes-1 to 0   ' byte loop
                repeat 8                        ' bit loop
                    waitx(clk_delay)
                    pint(SCK)                   ' clock
                    ' sample MISO and update current working byte in buffer
                    byte[ptr_buff][b_num] := (byte[ptr_buff][b_num] << 1) | pinr(MISO)
                    waitx(clk_delay)
                    pint(SCK)

PUB Rd_Byte{}: spi2byte
' Read byte from SPI bus
    rdblock_lsbf(@spi2byte, 1)

PUB RdLong_LSBF{}: spi2long
' Read long from SPI bus, least-significant byte first
    rdblock_lsbf(@spi2long, 4)

PUB RdLong_MSBF{}: spi2long
' Read long from SPI bus, least-significant byte first
    rdblock_msbf(@spi2long, 4)

PUB RdWord_LSBF{}: spi2word
' Read word from SPI bus, least-significant byte first
    rdblock_lsbf(@spi2word, 2)

PUB RdWord_MSBF{}: spi2word
' Read word from SPI bus, least-significant byte first
    rdblock_msbf(@spi2word, 2)

PUB WrBlock_LSBF(ptr_buff, nr_bytes) | SCK, MOSI, MISO, clk_delay, b_num, tmp
' Write block of data to SPI bus from ptr_buff, least-significant byte first
    longmove(@SCK, @_SCK, 4)                    ' copy pins from hub
    pinl(MOSI)                                  ' ensure MOSI is an output
    case _spi_mode
        0, 2:
            repeat b_num from 0 to nr_bytes-1       ' byte loop
                tmp := (byte[ptr_buff][b_num] << 24)' align byte with MSBit of long
                repeat 8                            ' bit loop
                    pinw(MOSI, (tmp <-= 1) & 1)     ' next bit into pos and isolate it
                    pint(SCK)                       ' clock
                    waitx(clk_delay)
                    pint(SCK)
                    waitx(clk_delay)
        1, 3:
            repeat b_num from 0 to nr_bytes-1       ' byte loop
                tmp := (byte[ptr_buff][b_num] << 24)' align byte with MSBit of long
                repeat 8                            ' bit loop
                    pinw(MOSI, (tmp <-= 1) & 1)     ' next bit into pos and isolate it
                    pint(SCK)                       ' clock
                    waitx(clk_delay)
                    pint(SCK)
                    waitx(clk_delay)

PUB WrBlock_MSBF(ptr_buff, nr_bytes) | SCK, MOSI, MISO, clk_delay, b_num, tmp
' Write block of data to SPI bus from ptr_buff, most-significant byte first
    longmove(@SCK, @_SCK, 4)                    ' copy pins from hub
    pinl(MOSI)                                  ' ensure MOSI is an output
    case _spi_mode
        0, 2:
            repeat b_num from nr_bytes-1 to 0       ' byte loop
                tmp := (byte[ptr_buff][b_num] << 24)' align byte with MSBit of long
                repeat 8                            ' bit loop
                    pinw(MOSI, (tmp <-= 1) & 1)     ' next bit into pos and isolate it
                    pint(SCK)                       ' clock
                    waitx(clk_delay)
                    pint(SCK)
                    waitx(clk_delay)
        1, 3:
            repeat b_num from nr_bytes-1 to 0       ' byte loop
                tmp := (byte[ptr_buff][b_num] << 24)' align byte with MSBit of long
                repeat 8                            ' bit loop
                    pinw(MOSI, (tmp <-= 1) & 1)     ' next bit into pos and isolate it
                    pint(SCK)                       ' clock
                    waitx(clk_delay)
                    pint(SCK)
                    waitx(clk_delay)

PUB Wr_Byte(byte2spi)
' Write byte to SPI bus
    wrblock_lsbf(@byte2spi, 1)

PUB WrLong_LSBF(long2spi)
' Write long to SPI bus, least-significant byte first
    wrblock_lsbf(@long2spi, 4)

PUB WrLong_MSBF(long2spi)
' Write long to SPI bus, most-significant byte first
    wrblock_msbf(@long2spi, 4)

PUB WrWord_LSBF(word2spi)
' Write word to SPI bus, least-significant byte first
    wrblock_lsbf(@word2spi, 2)

PUB WrWord_MSBF(word2spi)
' Write word to SPI bus, most-significant byte first
    wrblock_msbf(@word2spi, 2)

' -- Legacy methods below

PUB Start(MOSI, MISO, SCK, SCK_HZ, CPOL=0): okay

    _CPOL := CPOL
    _dirw(MOSI, 1)
    if MISO <> -1
        _dirw(MISO, 0)
    pinw(SCK, _CPOL)
    _dirw(SCK, 1)
    _clk_delay := ((clkfreq / SCK_HZ) / 2) #> 2
    okay := cogid() + 1

{{
************************************************
* Propeller SPI Engine  ... Spin Version  v1.0 *
* Author: Beau Schwabe                         *
* Copyright (c) 2009 Parallax                  *
* See end of file for terms of use.            *
************************************************

Revision History:
         V1.0   - original program

}}
CON

' Bit order: Read/ShiftIn
' MSBPRE   - Most Significant Bit first ; data is valid before the clock
' LSBPRE   - Least Significant Bit first ; data is valid before the clock
' MSBPOST  - Most Significant Bit first ; data is valid after the clock
' LSBPOST  - Least Significant Bit first ; data is valid after the clock
    #0, MSBPRE, LSBPRE, MSBPOST, LSBPOST

' Bit order: Write/ShiftOut
' LSBFIRST - Least Significant Bit first ; data is valid after the clock
' MSBFIRST - Most Significant Bit first ; data is valid after the clock
    #4, LSBFIRST, MSBFIRST

PUB Read(MISO, SCK, mode, nr_bits): result | clk_delay, tmp

    clk_delay := _clk_delay
    pinw(SCK, _CPOL)                                         ' Set initial clock state/clock polarity
    tmp := 32 - nr_bits

    result := 0

    case_fast mode
        MSBPRE:
            repeat nr_bits
                waitx(clk_delay)
                pint(SCK)
                waitx(clk_delay)
                pint(SCK)
                result := (result << 1) | pinr(MISO)

        LSBPRE:
            repeat nr_bits + 1
                result := (result >> 1) | (pinr(MISO) << 31)
                waitx(clk_delay)
                pint(SCK)
                waitx(clk_delay)
                pint(SCK)
                result >>= tmp

        MSBPOST:
            repeat nr_bits
                pint(SCK)
                waitx(clk_delay)
                pint(SCK)
                waitx(clk_delay)
                result := (result << 1) | pinr(MISO)

        LSBPOST:
            repeat nr_bits + 1
                pint(SCK)
                waitx(clk_delay)
                pint(SCK)
                waitx(clk_delay)
                result := (result >> 1) | (pinr(MISO) << 31)
                result >>= tmp

    return result

PUB Write(MOSI, SCK, mode, nr_bits, val) | clk_delay, tmp

    clk_delay := _clk_delay
    tmp := 32-(nr_bits-1)
    case_fast mode
        LSBFIRST:
            val <<= tmp
            val := val REV 31
        MSBFIRST:

    org
            shl     val,        tmp     wc

            rep     #loop,      nr_bits
            outc    MOSI                                    ' Put the MSB out on MOSI

            waitx   clk_delay
            drvnot  SCK                                     ' Clock out
            waitx   clk_delay
            drvnot  SCK

            shl     val,        #1      wc                  ' Shift the next bit into position for MOSI
        loop
    end

DAT
{
Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}

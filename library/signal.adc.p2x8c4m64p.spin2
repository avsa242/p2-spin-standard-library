{
---------------------------------------------------------------------------------------------------
    Filename:       signal.adc.p2x8c4m64p.spin2
    Description:    P2 smart-pin-based ADC
    Author:         Jesse Burt
    Started:        Jan 24, 2023
    Updated:        Jun 23, 2024
    Copyright (c) 2024 - See end of file for terms of use.
---------------------------------------------------------------------------------------------------
}

CON

    { default I/O configuration; these can be overridden by the parent object }
    BASE_PIN    = 0
    ADC_BITS    = 12
    CHANNELS    = 1


    { ADC sampling modes }
    SINC2_SMP   = %00 << 4
    SINC2_FILT  = %01 << 4
    SINC3_FILT  = %10 << 4
    BITS_CAPT   = %11 << 4


VAR

    long _adc_pin
    long _adc_bias[CHANNELS]
    long _adc_range[CHANNELS]
    long _adc_scaling
    byte _adc_res, _adc_samp_mode
    byte _channel


PUB start(): s
' Start the ADC using the default I/O configuration
    return startx(BASE_PIN, ADC_BITS, SINC2_SMP)


PUB startx(BASEPIN, adcres, samp_mode): status | ch
' Start the ADC
'   BASEPIN: first I/O pin to sample
'   adcres: ADC resolution, in bits (2..14)
'   samp_mode: sampling mode (see adc_set_sample_mode())
'   Returns: cogid+1 of parent cog
    _adc_pin := 0 #> BASEPIN <# 63
    adc_set_res(adcres)
    adc_set_sample_mode(samp_mode)
    repeat ch from 0 to CHANNELS-1
        set_adc_channel(ch)
        calibrate_adc(ch)                       ' must be done at least once
        adc_update_mode()

    return (cogid()+1)


PUB adc_res(): bits
' Get currently set ADC resolution
'   Returns: bits
    return _adc_res


PUB adc_set_sample_mode(mode)
' Set ADC sampling mode/filter
'   Valid values:
'       SINC2_SMP (0): SINC2 sampling mode
'       SINC2_FILT (16): SINC2 filtering mode
'       SINC3_FILT (32): SINC3 filtering mode
'       BITS_CAPT (48): bitstream capturing mode
    _adc_samp_mode := (SINC2_SMP #> mode <# BITS_CAPT)


PUB adc_set_res(bits)
' Set ADC resolution, in bits
'   Valid values: 2..14
    _adc_res := (2 #> bits <# 14)-1
    _adc_scaling := (1 << (14-bits)) * 1_000_000


PUB adc_update_mode()
' Update ADC mode, using current resolution and sample mode settings
    pinstart(_adc_pin+_channel, P_ADC | P_ADC_1X, _adc_samp_mode | _adc_res, 0)


PUB adc_word2volts(w): v
' Convert ADC word to millivolts
    return muldiv64(w, _adc_scaling, 3_300)


PUB adc_word(): w
' Get measurement word from ADC
    return rdpin(_adc_pin+_channel) - _adc_bias[_channel] ' remove bias


PUB calibrate_adc(ch=0) | gio, vio, tmp, avg, range, pin
' Calibrate the ADC smart-pin
'   ch: ADC channel to calibrate (default is 0, if unspecified)
    if ( (ch < 0) or (ch > CHANNELS-1) )
        return -1                               ' reject invalid channel numbers

    pin := _adc_pin + ch

    { get gio level }
    pinstart(pin, P_ADC | P_ADC_GIO, (_adc_samp_mode | _adc_res), 0)
    waitms(1)                                   ' wait for the pin to start up

    tmp := 0
    avg := 1000                                 ' number of samples to average
    repeat avg
        tmp += rdpin(pin)
    _adc_bias[ch] := gio := (tmp / avg)         ' set gio as the reference level

    { get vio level }
    pinstart(pin, P_ADC | P_ADC_VIO, (_adc_samp_mode | _adc_res), 0)
    waitms(1)

    tmp := 0
    avg := 1000
    repeat avg
        tmp += rdpin(pin)
    vio := (tmp/avg)

    _adc_range[ch] := (vio-gio)


PUB set_adc_channel(ch)
' Set ADC channel for subsequent reads
    _channel := ch


PUB set_ref_voltage(v)
' dummy method


PUB voltage(): v
' Get voltage measured at ADC
'   Returns: microvolts
    return adc_word2volts( adc_word() )


DAT
{
Copyright 2024 Jesse Burt

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
associated documentation files (the "Software"), to deal in the Software without restriction,
including without limitation the rights to use, copy, modify, merge, publish, distribute,
sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or
substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT
NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT
OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}


{
    --------------------------------------------
    Filename: display.oled.ssd1331.spi.spin2
    Author: Jesse Burt
    Description: Driver for Solomon Systech 96x64 RGB OLED
    Copyright (c) 2020
    Started: Nov 8, 2019
    Updated: Jun 23, 2020
    See end of file for terms of use.
    --------------------------------------------
}
#define SSD1331
#include "lib.gfx.bitmap.spin2"

CON

    _DISP_WIDTH = 96
    _DISP_HEIGHT= 64
    _DISP_XMAX  = _DISP_WIDTH-1
    _DISP_YMAX  = _DISP_HEIGHT-1
    _BUFF_SZ    = _DISP_WIDTH * _DISP_HEIGHT * 2
    BYTESPERPX  = 2
    BYTESPERLN  = _DISP_WIDTH * BYTESPERPX
    MAX_COLOR   = 65535

' Transaction type selection
    TRANS_CMD   = 0
    TRANS_DATA  = 1

' Display power modes
    OFF         = 0
    ON          = 1
    DIM         = 2

' Display visibility modes
    NORMAL      = 0
    ALL_ON      = 1
    ALL_OFF     = 2
    INVERTED    = 3

' Color depth formats
    COLOR_256   = %00
    COLOR_65K   = %01
    COLOR_65K2  = %10

' Address increment mode
    HORIZ       = 0
    VERT        = 1

' Subpixel order
    RGB         = 0
    BGR         = 1

OBJ

    core    : "core.con.ssd1331"
    spi     : "com.spi"

VAR

    long _DC, _RES, _MOSI, _SCK, _CS
    long _ptr_drawbuffer
    byte _sh_SETCOLUMN, _sh_SETROW, _sh_SETCONTRAST_A, _sh_SETCONTRAST_B, _sh_SETCONTRAST_C
    byte _sh_MASTERCCTRL, _sh_SECPRECHG[3], _sh_REMAPCOLOR, _sh_DISPSTARTLINE, _sh_DISPOFFSET
    byte _sh_DISPMODE, _sh_MULTIPLEX, _sh_DIM, _sh_MASTERCFG, _sh_DISPONOFF, _sh_POWERSAVE
    byte _sh_PHASE12PER, _sh_CLK, _sh_GRAYTABLE, _sh_PRECHGLEV, _sh_VCOMH, _sh_CMDLOCK
    byte _sh_HVSCROLL, _sh_FILL

PUB Start (CS_PIN, DC_PIN, DIN_PIN, CLK_PIN, RES_PIN, CLK_HZ, drawbuffer_address): okay

    if lookdown(CS_PIN: 0..63) and lookdown(DC_PIN: 0..63) and lookdown(DIN_PIN: 0..63) and lookdown(CLK_PIN: 0..63) and lookdown(RES_PIN: 0..63)
        if okay := spi.start (CLK_PIN, DIN_PIN, -1, CLK_HZ)
            _DC := DC_PIN
            _RES := RES_PIN
            _MOSI := DIN_PIN
            _SCK := CLK_PIN
            _CS := CS_PIN
            pinh(_DC)
            pinh(_RES)
            pinh(_CS)
            Reset()
            Address (drawbuffer_address)
            return okay
    return FALSE

PUB Stop()

    DisplayVisibility(ALL_OFF)
    Powered (FALSE)

PUB Defaults()
' Apply power-on-reset default settings
    ColorDepth (COLOR_65K)
    MirrorH (FALSE)
    Powered (FALSE)
    DisplayVisibility(ALL_OFF)
    DisplayStartLine (0)
    DisplayOffset (0)
    DisplayInverted (FALSE)
    DisplayLines (64)
    ExtSupply()
    PowerSaving (TRUE)
    Phase1Period (7)
    Phase2Period (4)
    ClockFreq (956)
    ClockDiv (1)
    PrechargeSpeed (127, 127, 127)
    PrechargeLevel (500)
    COMHighLogicLevel (830)
    CurrentLimit (16)
    ContrastA (127)
    ContrastB (127)
    ContrastC (127)
    Powered (TRUE)
    DisplayBounds (0, 0, _DISP_XMAX, _DISP_YMAX)
    ClearAccel()
    DisplayVisibility(NORMAL)

PUB DefaultsCommon()
' Apply some more common default settings
    Defaults()                                              ' Start with POR settings
    ClockDiv (1)
    ClockFreq (980)
    AddrMode (HORIZ)
    MirrorH (FALSE)
    SubpixelOrder (RGB)
    VertAltScan (FALSE)
    MirrorV (FALSE)
    Interlaced (FALSE)
    ColorDepth (COLOR_65K)
    FillAccelEnabled (TRUE)

PUB Address(addr)
' Set framebuffer/display buffer address
    _ptr_drawbuffer := addr

PUB AddrMode(mode): result | tmp
' Set display internal addressing mode
'   Valid values:
'       HORZ (0): Horizontal addressing mode
'       VERT (1): Vertical addressing mode
    tmp := _sh_REMAPCOLOR
    case mode
        HORIZ, VERT:
        OTHER:
            result := (tmp >> core.FLD_ADDRINC) & %1
            return

    _sh_REMAPCOLOR &= core.MASK_SEGREMAP
    _sh_REMAPCOLOR := (_sh_REMAPCOLOR | mode) & core.SSD1331_CMD_SETREMAP_MASK
    tmp.byte[0] := core.SSD1331_CMD_SETREMAP
    tmp.byte[1] := _sh_REMAPCOLOR
    writeReg (TRANS_CMD, 2, @tmp)

PUB BoxAccel(sx, sy, ex, ey, boxcolor, fillcolor) | tmp[3]
' Draw a box, using the display's native/accelerated box function
    sx := 0 #> sx <# _disp_width-1
    sy := 0 #> sy <# _disp_height-1
    ex := sx #> ex <# _disp_width-1
    ey := sy #> ey <# _disp_height-1

    tmp.byte[0] := core.SSD1331_CMD_DRAWRECT
    tmp.byte[1] := sx
    tmp.byte[2] := sy
    tmp.byte[3] := ex
    tmp.byte[4] := ey
    tmp.byte[5] := RGB565_R8 (boxcolor)
    tmp.byte[6] := RGB565_G8 (boxcolor)
    tmp.byte[7] := RGB565_B8 (boxcolor)
    tmp.byte[8] := RGB565_R8 (fillcolor)
    tmp.byte[9] := RGB565_G8 (fillcolor)
    tmp.byte[10] := RGB565_B8 (fillcolor)
    writeReg (TRANS_CMD, 11, @tmp)

PUB ClearAccel() | tmp[2]
' Clears the display directly, using the display's native/accelerated clear function
    tmp.byte[0] := core.SSD1331_CMD_NOP3
    tmp.byte[1] := core.SSD1331_CMD_CLEAR
    tmp.byte[2] := 0
    tmp.byte[3] := 0
    tmp.byte[4] := _DISP_XMAX
    tmp.byte[5] := _DISP_YMAX
    writeReg (TRANS_CMD, 6, @tmp)

PUB ClockDiv(divider): result | tmp
' Set clock frequency divider used by the display controller
'   Valid values: 1..16
'   Any other value returns the current setting
    tmp := _sh_CLK
    case divider
        1..16:
            divider -= 1
        OTHER:
            result := (tmp & core.BITS_CLKDIV) + 1
            return

    _sh_CLK &= core.MASK_CLKDIV
    _sh_CLK := _sh_CLK | divider
    tmp.byte[0] := core.SSD1331_CMD_CLOCKDIV
    tmp.byte[1] := divider
    writeReg (TRANS_CMD, 2, @tmp)

PUB ClockFreq(freq): result | tmp
' Set display internal oscillator frequency, in kHz
'   Valid values: 800..980, in steps of 12
'   Any other value returns the current setting
    tmp := _sh_CLK
    case freq
        800, 812, 824, 836, 848, 860, 872, 884, 896, 908, 920, 932, 944, 956, 968, 980:
            freq := lookdownz(freq: 800, 812, 824, 836, 848, 860, 872, 884, 896, 908, 920, 932, 944, 956, 968, 980) << core.FLD_FOSCFREQ
        OTHER:
            tmp := (tmp >> core.FLD_FOSCFREQ) & core.BITS_FOSCFREQ
            result := lookupz (tmp: 800, 812, 824, 836, 848, 860, 872, 884, 896, 908, 920, 932, 944, 956, 968, 980)
            return

    _sh_CLK &= core.MASK_FOSCFREQ
    _sh_CLK := _sh_CLK | freq
    tmp.byte[0] := core.SSD1331_CMD_CLOCKDIV
    tmp.byte[1] := freq
    writeReg (TRANS_CMD, 2, @tmp)

PUB ColorDepth(format): result | tmp
' Set expected color format of pixel data
'   Valid values:
'       COLOR_256 (0): 8-bit/256 color
'       COLOR_65K (1): 16-bit/65536 color format 1
'       COLOR_65K2 (2): 16-bit/65536 color format 2
'   Any other value returns the current setting
    tmp := _sh_REMAPCOLOR
    case format
        COLOR_256, COLOR_65K, COLOR_65K2:
            format <<= core.FLD_COLORFORMAT
        OTHER:
            result := tmp >> core.FLD_COLORFORMAT
            return

    _sh_REMAPCOLOR &= core.MASK_COLORFORMAT
    _sh_REMAPCOLOR := _sh_REMAPCOLOR | format

    tmp.byte[0] := core.SSD1331_CMD_SETREMAP
    tmp.byte[1] := _sh_REMAPCOLOR

    writeReg (TRANS_CMD, 2, @tmp)

PUB COMHighLogicLevel(mV): result | tmp
' Set logic high level threshold of COM pins rel. to Vcc, in millivolts
'   Valid values: 440, 520, 610, 710, 830
'   Any other value returns the current setting
    tmp := _sh_VCOMH
    case mV := lookdown(mv: 440, 520, 610, 710, 830)
        1..5:
            mV := lookup(mV: $00, $10, $20, $30, $3E)
        OTHER:
            result := lookdown(tmp: $00, $10, $20, $30, $3E)
            result := lookup(result: 440, 520, 610, 710, 830)
            return

    _sh_VCOMH := mV
    tmp.byte[0] := core.SSD1331_CMD_VCOMH
    tmp.byte[1] := mV
    writeReg (TRANS_CMD, 2, @tmp)

PUB Contrast(level)
' Set display contrast/brightness
'   Valid values: 0..255
'   Any other value returns the current setting
    ContrastA (level)
    ContrastB (level)
    ContrastC (level)

PUB ContrastA(level): tmp
' Set contrast/brightness level of subpixel a
'   Valid values: 0..255
'   Any other value returns the current setting
    tmp := _sh_SETCONTRAST_A
    case level
        0..255:
        OTHER:
            return tmp

    _sh_SETCONTRAST_A := level
    tmp.byte[0] := core.SSD1331_CMD_CONTRASTA
    tmp.byte[1] := level
    writeReg (TRANS_CMD, 2, @tmp)

PUB ContrastB(level): tmp
' Set contrast/brightness level of subpixel b
'   Valid values: 0..255
'   Any other value returns the current setting
    tmp := _sh_SETCONTRAST_B
    case level
        0..255:
        OTHER:
            return tmp

    _sh_SETCONTRAST_B := level
    tmp.byte[0] := core.SSD1331_CMD_CONTRASTB
    tmp.byte[1] := level
    writeReg (TRANS_CMD, 2, @tmp)

PUB ContrastC(level): tmp
' Set contrast/brightness level of subpixel c
'   Valid values: 0..255
'   Any other value returns the current setting
    tmp := _sh_SETCONTRAST_C
    case level
        0..255:
        OTHER:
            return tmp

    _sh_SETCONTRAST_C := level
    tmp.byte[0] := core.SSD1331_CMD_CONTRASTC
    tmp.byte[1] := level
    writeReg (TRANS_CMD, 2, @tmp)

PUB CopyAccel(sx, sy, ex, ey, dx, dy) | tmp[2]
' Use the display's accelerated Copy Region function
'   Valid values:
'       sx, ex, dx: 0..95
'       sy, ey, dy: 0..63
'   Any other value will be ignored
    case sx
        0..95:
        OTHER:
            return

    case sy
        0..63:
        OTHER:
            return

    case ex
        0..95:
        OTHER:
            return

    case ey
        0..63:
        OTHER:
            return

    case dx
        0..95:
        OTHER:
            return

    case dy
        0..63:
        OTHER:
            return

    tmp.byte[0] := core.SSD1331_CMD_COPY
    tmp.byte[1] := sx
    tmp.byte[2] := sy
    tmp.byte[3] := ex
    tmp.byte[4] := ey
    tmp.byte[5] := dx
    tmp.byte[6] := dy
    writeReg (TRANS_CMD, 7, @tmp)

PUB CopyAccelInverted(enabled): result | tmp
' Enable inverted colors, when using CopyAccel()
    tmp := _sh_FILL
    case abs(enabled)
        0, 1:
            enabled := (abs(enabled) << core.FLD_REVCOPY)
        OTHER:
            result := ((tmp >> core.FLD_REVCOPY) & %1) * TRUE
            return

    _sh_FILL &= core.MASK_REVCOPY
    _sh_FILL := (_sh_FILL | enabled) & core.SSD1331_CMD_FILL_MASK
    tmp.byte[0] := core.SSD1331_CMD_FILL
    tmp.byte[1] := _sh_FILL
    writeReg (TRANS_CMD, 2, @tmp)

PUB CurrentLimit(divisor): tmp
' Set master current limit divisor
    tmp := _sh_MASTERCCTRL
    case divisor
        1..16:
            divisor -= 1
        OTHER:
            return tmp + 1

    _sh_MASTERCCTRL := divisor
    tmp.byte[0] := core.SSD1331_CMD_MASTERCURRENT
    tmp.byte[1] := divisor
    writeReg (TRANS_CMD, 2, @tmp)

PUB DisplayBounds(sx, sy, ex, ey) | tmp[2]
' Set drawable display region for subsequent drawing operations
'   Valid values:
'       sx, ex: 0..95
'       sy, ey: 0..63
'   Any other value will be ignored
    ifnot lookup(sx: 0.._DISP_XMAX) or lookup(sy: 0.._DISP_YMAX) or lookup(ex: 0.._DISP_XMAX) or lookup(ey: 0.._DISP_YMAX)
        return

    tmp.byte[0] := core.SSD1331_CMD_SETCOLUMN
    tmp.byte[1] := sx
    tmp.byte[2] := ex

    writeReg (TRANS_CMD, 3, @tmp)

    tmp.byte[0] := core.SSD1331_CMD_SETROW
    tmp.byte[1] := sy
    tmp.byte[2] := ey

    writeReg (TRANS_CMD, 3, @tmp)

PUB DisplayInverted(enabled)
' Invert display colors
    case abs(enabled)
        0:
            DisplayVisibility(NORMAL)
        1:
            DisplayVisibility(INVERTED)
        OTHER:
            return FALSE

PUB DisplayLines(lines): tmp
' Set maximum number of display lines
'   Valid values: 16..64
'   Any other value returns the current setting
    tmp := _sh_MULTIPLEX
    case lines
        16..64:
            lines -= 1
        OTHER:
            return tmp + 1

    _sh_MULTIPLEX := lines
    tmp.byte[0] := core.SSD1331_CMD_SETMULTIPLEX
    tmp.byte[1] := lines
    writeReg (TRANS_CMD, 2, @tmp)

PUB DisplayOffset(lines): tmp
' Set display offset/vertical shift
    tmp := _sh_DISPOFFSET
    case lines
        0..63:
        OTHER:
            return tmp

    _sh_DISPOFFSET := lines
    tmp.byte[0] := core.SSD1331_CMD_DISPLAYOFFSET
    tmp.byte[1] := lines
    writeReg (TRANS_CMD, 2, @tmp)

PUB DisplayStartLine(disp_line): tmp
' Set display start line
    tmp := _sh_DISPSTARTLINE
    case disp_line
        0..63:
        OTHER:
            return tmp

    _sh_DISPSTARTLINE := disp_line
    tmp.byte[0] := core.SSD1331_CMD_STARTLINE
    tmp.byte[1] := disp_line
    writeReg (TRANS_CMD, 2, @tmp)

PUB DisplayVisibility(mode): result | tmp
' Set display visibility
    tmp := _sh_DISPMODE
    case mode
        NORMAL, ALL_ON, ALL_OFF, INVERTED:
            mode := mode + core.SSD1331_CMD_NORMALDISPLAY
        OTHER:
            return (_sh_DISPMODE - core.SSD1331_CMD_NORMALDISPLAY)

    _sh_DISPMODE := mode
    writeReg (TRANS_CMD, 1, @mode)

PUB ExtSupply() | tmp

    tmp.byte[0] := core.SSD1331_CMD_SETMASTER
    tmp.byte[1] := core.MASTERCFG_EXT_VCC
    writeReg (TRANS_CMD, 2, @tmp)

PUB FillAccelEnabled(enabled): result | tmp
' Enable the display's native/accelerated fill function, when using BoxAccel()
    tmp := _sh_FILL
    case abs(enabled)
        0, 1:
            enabled := abs(enabled) & %1
        OTHER:
            result := (tmp & %1) * TRUE
            return

    _sh_FILL &= core.MASK_FILL
    _sh_FILL := (_sh_FILL | enabled) & core.SSD1331_CMD_FILL_MASK
    tmp.byte[0] := core.SSD1331_CMD_FILL
    tmp.byte[1] := _sh_FILL
    writeReg (TRANS_CMD, 2, @tmp)

PUB Interlaced(enabled): result | tmp
' Alternate every other display line:
' Lines 0..31 will appear on even rows (starting on row 0)
' Lines 32..63 will appear on odd rows (starting on row 1)
'   Valid values: TRUE (-1 or 1), FALSE (0)
'   Any other value returns the current setting
    tmp := _sh_REMAPCOLOR
    case abs(enabled)
        0, 1:
            enabled := (abs(enabled) ^ 1) << core.FLD_COMSPLIT
        OTHER:
            result := not (((tmp >> core.FLD_COMSPLIT) & %1) * TRUE)
            return

    _sh_REMAPCOLOR &= core.MASK_COMSPLIT
    _sh_REMAPCOLOR := (_sh_REMAPCOLOR | enabled) & core.SSD1331_CMD_SETREMAP_MASK
    tmp.byte[0] := core.SSD1331_CMD_SETREMAP
    tmp.byte[1] := _sh_REMAPCOLOR
    writeReg (TRANS_CMD, 2, @tmp)

PUB LineAccel(sx, sy, ex, ey, color) | tmp[2]
' Draw a line, using the display's native/accelerated line function
    sx := 0 #> sx <# _disp_width-1
    sy := 0 #> sy <# _disp_height-1
    ex := 0 #> ex <# _disp_width-1
    ey := 0 #> ey <# _disp_height-1

    tmp.byte[0] := core.SSD1331_CMD_DRAWLINE
    tmp.byte[1] := sx
    tmp.byte[2] := sy
    tmp.byte[3] := ex
    tmp.byte[4] := ey
    tmp.byte[5] := RGB565_R8 (color)
    tmp.byte[6] := RGB565_G8 (color)
    tmp.byte[7] := RGB565_B8 (color)
    writeReg (TRANS_CMD, 8, @tmp)

PUB MirrorH(enabled): result | tmp
' Mirror the display, horizontally
'   Valid values: TRUE (-1 or 1), FALSE (0)
'   Any other value returns the current setting
    tmp := _sh_REMAPCOLOR
    case abs(enabled)
        0, 1:
            enabled := (abs(enabled)) << core.FLD_SEGREMAP
        OTHER:
            result := ((tmp >> core.FLD_SEGREMAP) & %1) * TRUE
            return

    _sh_REMAPCOLOR &= core.MASK_SEGREMAP
    _sh_REMAPCOLOR := (_sh_REMAPCOLOR | enabled) & core.SSD1331_CMD_SETREMAP_MASK
    tmp.byte[0] := core.SSD1331_CMD_SETREMAP
    tmp.byte[1] := _sh_REMAPCOLOR
    writeReg (TRANS_CMD, 2, @tmp)

PUB MirrorV(enabled): result | tmp
' Mirror the display, vertically
'   Valid values: TRUE (-1 or 1), FALSE (0)
'   Any other value returns the current setting
    tmp := _sh_REMAPCOLOR
    case abs(enabled)
        0, 1:
            enabled := (abs(enabled)) << core.FLD_COMREMAP
        OTHER:
            result := ((tmp >> core.FLD_COMREMAP) & %1) * TRUE
            return

    _sh_REMAPCOLOR &= core.MASK_COMREMAP
    _sh_REMAPCOLOR := (_sh_REMAPCOLOR | enabled) & core.SSD1331_CMD_SETREMAP_MASK
    tmp.byte[0] := core.SSD1331_CMD_SETREMAP
    tmp.byte[1] := _sh_REMAPCOLOR
    writeReg (TRANS_CMD, 2, @tmp)

PUB NoOp() | tmp
' No-operation
    tmp := core.SSD1331_CMD_NOP3
    writeReg (TRANS_CMD, 1, @tmp)

PUB Phase1Period(clks): result | tmp
' Set discharge/phase 1 period, in display clocks
    tmp := _sh_PHASE12PER
    case clks
        1..15:
        OTHER:
            result := tmp & core.BITS_PHASE1
            return

    _sh_PHASE12PER &= core.MASK_PHASE1
    _sh_PHASE12PER := (_sh_PHASE12PER | clks)
    tmp.byte[0] := core.SSD1331_CMD_PRECHARGE
    tmp.byte[1] := _sh_PHASE12PER
    writeReg (TRANS_CMD, 2, @tmp)

PUB Phase2Period(clks): result | tmp
' Set charge/phase 2 period, in display clocks
    tmp := _sh_PHASE12PER
    case clks
        1..15:
            clks <<= core.FLD_PHASE2
        OTHER:
            result := (tmp >> core.FLD_PHASE2) & core.BITS_PHASE2
            return

    _sh_PHASE12PER &= core.MASK_PHASE2
    _sh_PHASE12PER := (_sh_PHASE12PER | clks)
    tmp.byte[0] := core.SSD1331_CMD_PRECHARGE
    tmp.byte[1] := _sh_PHASE12PER
    writeReg (TRANS_CMD, 2, @tmp)

PUB PlotAccel(x, y, color) | tmp[2]
' Draw a pixel, using the display's native/accelerated plot/pixel function
    x := 0 #> x <# _disp_width-1
    y := 0 #> y <# _disp_height-1
    tmp.byte[0] := core.SSD1331_CMD_SETCOLUMN
    tmp.byte[1] := x
    tmp.byte[2] := 95
    tmp.byte[3] := core.SSD1331_CMD_SETROW
    tmp.byte[4] := y
    tmp.byte[5] := 63
    
    writeReg (TRANS_CMD, 6, @tmp)

    waitus (3)

    writeReg (TRANS_DATA, 2, @color)

PUB Powered(enabled): result | tmp
' Enable display power
    tmp := _sh_DISPONOFF
    case abs(enabled)
        OFF, ON, DIM:
            enabled := lookupz(abs(enabled): core.SSD1331_CMD_DISPLAYOFF, core.SSD1331_CMD_DISPLAYON, core.SSD1331_CMD_DISPLAYONDIM)
        OTHER:
            result := lookdownz(tmp: core.SSD1331_CMD_DISPLAYOFF, core.SSD1331_CMD_DISPLAYON, core.SSD1331_CMD_DISPLAYONDIM)
            return

    _sh_DISPONOFF := enabled
    writeReg (TRANS_CMD, 1, @_sh_DISPONOFF)

PUB PowerSaving(enabled): result | tmp
' Enable display power saving mode
'   Valid values: TRUE (-1 or 1), FALSE (0)
'   Any other value returns the current setting
    tmp := _sh_POWERSAVE
    case abs(enabled)
        0, 1:
            enabled := lookupz(abs(enabled): core.POWERMODE_POWERSAVE_DIS, core.POWERMODE_POWERSAVE_ENA)
        OTHER:
            result := (lookdownz(_sh_POWERSAVE: core.POWERMODE_POWERSAVE_DIS, core.POWERMODE_POWERSAVE_ENA) & %1) * TRUE
            return

    _sh_POWERSAVE := enabled
    tmp.byte[0] := core.SSD1331_CMD_POWERMODE
    tmp.byte[1] := enabled
    writeReg (TRANS_CMD, 2, @tmp)

PUB PrechargeLevel(mV): result | tmp
' Set first pre-charge voltage level (phase 2) of segment pins, in millivolts
    tmp := _sh_PRECHGLEV
    case mV := lookdown(mv: 100, 110, 130, 140, 150, 170, 180, 190, 200, 220, 230, 240, 260, 270, 280, 300, 310, 320, 330, 350, 360, 370, 390, 400, 410, 430, 440, 450, 460, 480, 490, 500)
        1..32:
            mV := (mV - 1) << 1
        OTHER:
            result := (tmp >> 1)
            result := lookupz(result: 100, 110, 130, 140, 150, 170, 180, 190, 200, 220, 230, 240, 260, 270, 280, 300, 310, 320, 330, 350, 360, 370, 390, 400, 410, 430, 440, 450, 460, 480, 490, 500)
            return

    _sh_PRECHGLEV := mV
    tmp.byte[0] := core.SSD1331_CMD_PRECHARGELEVEL
    tmp.byte[1] := mV
    writeReg (TRANS_CMD, 2, @tmp)

PUB PrechargeSpeed(seg_a, seg_b, seg_c) | tmp[2]

    case seg_a
        $00..$FF:
        OTHER:
            return _sh_SECPRECHG.byte[0]
    case seg_b
        $00..$FF:
        OTHER:
            return _sh_SECPRECHG.byte[1]
    case seg_c
        $00..$FF:
        OTHER:
            return _sh_SECPRECHG.byte[2]

    _sh_SECPRECHG[0] := seg_a
    _sh_SECPRECHG[1] := seg_b
    _sh_SECPRECHG[2] := seg_c

    tmp.byte[0] := core.SSD1331_CMD_PRECHARGEA
    tmp.byte[1] := seg_a
    tmp.byte[2] := core.SSD1331_CMD_PRECHARGEB
    tmp.byte[3] := seg_b
    tmp.byte[4] := core.SSD1331_CMD_PRECHARGEC
    tmp.byte[5] := seg_c
    writeReg (TRANS_CMD, 6, @tmp)

PUB Reset()
' Reset the display controller
    pinh(_RES)
    waitms (1)
    pinl(_RES)
    waitms (10)
    pinh(_RES)

PUB SubpixelOrder(order): result | tmp
' Set subpixel color order
'   Valid values:
'       RGB (0): Red-Green-Blue order
'       BGR (1): Blue-Green-Red order
'   Any other value returns the current setting
    tmp := _sh_REMAPCOLOR
    case order
        RGB, BGR:
            order <<= core.FLD_SUBPIX_ORDER
        OTHER:
            result := (tmp >> core.FLD_SUBPIX_ORDER) & %1
            return

    _sh_REMAPCOLOR &= core.MASK_SUBPIX_ORDER
    _sh_REMAPCOLOR := (_sh_REMAPCOLOR | order) & core.SSD1331_CMD_SETREMAP_MASK
    tmp.byte[0] := core.SSD1331_CMD_SETREMAP
    tmp.byte[1] := _sh_REMAPCOLOR
    writeReg (TRANS_CMD, 2, @tmp)

PUB Update()
' Write the current display buffer to the display
    writeReg(TRANS_DATA, _buff_sz, _ptr_drawbuffer)

PUB VertAltScan(enabled): result | tmp
' Alternate Left-Right, Right-Left scanning, every other display line
'   Valid values: TRUE (-1 or 1), FALSE (0)
'   Any other value returns the current setting
    tmp := _sh_REMAPCOLOR
    case abs(enabled)
        0, 1:
            enabled := (abs(enabled)) << core.FLD_COMLR_SWAP
        OTHER:
            result := ((tmp >> core.FLD_COMLR_SWAP) & %1) * TRUE
            return

    _sh_REMAPCOLOR &= core.MASK_COMLR_SWAP
    _sh_REMAPCOLOR := (_sh_REMAPCOLOR | enabled) & core.SSD1331_CMD_SETREMAP_MASK
    tmp.byte[0] := core.SSD1331_CMD_SETREMAP
    tmp.byte[1] := _sh_REMAPCOLOR
    writeReg (TRANS_CMD, 2, @tmp)

PUB WriteBuffer(buff_addr, buff_sz)
' Write alternate buffer to display
    writeReg(TRANS_DATA, buff_sz, buff_addr)

PRI writeReg(trans_type, nr_bytes, buff_addr) | tmp

    case trans_type
        TRANS_DATA:
            pinh(_DC)
        TRANS_CMD:
            pinl(_DC)

        OTHER:
            return

    pinl(_CS)
    spi.WriteBytes(nr_bytes, buff_addr)
    pinh(_CS)

{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

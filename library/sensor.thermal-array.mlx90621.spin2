{
    --------------------------------------------
    Filename: sensor.thermal-array.mlx90621.i2c.spin2
    Author: Jesse Burt
    Description: Driver for the Melexis MLX90621
        16x4 IR array (P2 version)
    Copyright (c) 2022
    started: Dec 2, 2019
    Updated: Jan 1, 2022
    See end of file for terms of use.
    --------------------------------------------
}

CON

    SLAVE_WR        = core.SLAVE_ADDR
    SLAVE_RD        = core.SLAVE_ADDR|1

    DEF_HZ          = 100_000
    I2C_MAX_FREQ    = core.I2C_MAX_FREQ

    EE_SIZE         = 256

    WIDTH           = 16
    HEIGHT          = 4
    XMAX            = WIDTH-1
    YMAX            = HEIGHT-1

' I2C Fast Mode
    I2CFMODE_ENA    = 0
    I2CFMODE_DIS    = 1

' Operation modes
    CONT            = 0
    SINGLE          = 1

' Sensor power states
    OFF             = 0
    ON              = 1

' ADC reference settings
    ADCREF_HI       = 0
    ADCREF_LO       = 1

' On-sensor EEPROM
    EE_ENA          = 0
    EE_DIS          = 1

' Offsets to sensor calibration data
    EE_OFFS_VTH     = $DA
    EE_OFFS_KT1     = $DC
    EE_OFFS_KT2     = $DE
    EE_OFFS_KT1SCL  = $D2
    EE_OFFS_KT2SCL  = $D2
    EE_OFFS_OSCTRIM = $F7
    EE_OFFS_CFGH    = $F6
    EE_OFFS_CFGL    = $F5

    RAM_OFFS_PTAT   = $40
    RAM_OFFS_CPIX   = $41

    CFG_CKBYTE      = $55
    OSC_CKBYTE      = $AA

    W               = 0
    R               = 1

OBJ

    core    : "core.con.mlx90621"               ' HW-specific constants
    i2c     : "com.i2c"                         ' I2C engine

VAR

    word _ptat
    byte _ee_data[EE_SIZE]

PUB Null()
' This is not a top-level object

PUB Startx(SCL_PIN, SDA_PIN, I2C_HZ): status
' Start using custom I/O settings
    if lookdown(SCL_PIN: 0..63) and lookdown(SDA_PIN: 0..63) and {
}   I2C_HZ <= core.I2C_MAX_FREQ
        if (status := i2c.init(SCL_PIN, SDA_PIN, core.EE_MAX_FREQ))
            waitus(core.T_POR)
            if i2c.present(core.EE_SLAVE_ADDR)  ' first start I2C engine
                if readeeprom()                 '   to read the EEPROM
                    waitms(5)
                    i2c.deinit()
                                                ' now re-setup for the sensor
                    i2c.init(SCL_PIN, SDA_PIN, I2C_HZ)
                    waitms(5)
                    if i2c.present(SLAVE_WR)    ' check device bus presence
                        return
    ' if this point is reached, something above failed
    ' Double check I/O pin assignments, connections, power
    ' Lastly - make sure you have at least one free core/cog
    return FALSE

PUB Stop()

    i2c.deinit()

PUB Defaults()
' Write osc trimming val extracted from EEPROM address $F7
    osctrim(_ee_data[EE_OFFS_OSCTRIM])
    refreshrate(1)
    adcres(18)
    opmode(CONT)
    powered(TRUE)
    i2cfm(TRUE)
    eeprom(TRUE)
    adcreference(ADCREF_LO)
    reset(TRUE)
    waitms(5)

PUB ADCReference(mode=-2): curr_mode
' Set ADC reference high, low
'   Valid values:
'      ADCREF_HI (0) - ADC High reference enabled
'     *ADCREF_LO (1) - ADC Low reference enabled
'   Any other value polls the chip and returns the current setting
' NOTE: Re-calibration must be done after this method is called
    readreg(core.CONFIG, 2, 0, @curr_mode)
    case mode
        ADCREF_HI, ADCREF_LO:
            mode := mode << core.ADCHIGHREF
        other:
            return (curr_mode >> core.ADCHIGHREF) & 1

    mode := ((curr_mode & core.ADCHIGHREF_MASK) | mode)
    writereg(core.CONFIG, mode)

'TODO: Call Re-cal method here

PUB ADCRes(bits=-2): curr_res
' Set ADC resolution, in bits
'   Valid values: 15..18 (default: 18)
'   Any other value polls the chip and returns the current setting
    readreg(core.CONFIG, 2, 0, @curr_res)
    case bits
        15..18:
            bits := lookdownz(bits: 15, 16, 17, 18) << core.ADCRES
        other:
            curr_res := (curr_res >> core.ADCRES) & core.ADCRES_BITS
            return lookupz(curr_res: 15, 16, 17, 18)

    bits := ((curr_res & core.ADCRES_MASK) | bits)
    writereg(core.CONFIG, bits)

'    _adc_res := 3-((_cfg_reg >> 4) & %11)   'Update the VAR used in calculations

PUB AmbientTemp(): ptat | Kt1, Kt2, Vth, Ta
' Read Proportional To Ambient Temperature sensor   'XXX needs calc
    readreg(core.PTAT, 1, 0, @ptat)
{    Kt1 := (_ee_data[$DD] << 8) | _ee_data[$DC]
    Kt2 := (_ee_data[$DF] << 8) | _ee_data[$DE]
    Vth := (_ee_data[$DB] << 8) | _ee_data[$DA]
    Ta := Kt1 * Kt1
    Ta := Ta - (4 * Kt2)
    Ta := Ta * (Vth - PTAT_data)
    Ta := ^^Ta
    Ta := Ta + (-Kt1)
    Ta := Ta / (Kt2 * 2)}
    return

PUB Dump_EE(ptr_buff)
' Copy downloaded EEPROM image to ptr_buff
' NOTE: This buffer must be at least 256 bytes
    bytemove(ptr_buff, @_ee_data, EE_SIZE)

PUB EEPROM(state=-2): curr_state
' Enable/disable the sensor's built-in EEPROM
'   Valid values:
'      TRUE (-1 or 1): Sensor's built-in EEPROM enabled (default)
'      FALSE: Sensor's built-in EEPROM disabled
'   Any other value polls the chip and returns the current setting
'   NOTE: Use with care! Driver will fail to restart if EEPROM is disabled.
'       Cycle power in this case.
    readreg(core.CONFIG, 2, 0, @curr_state)
    case abs(state)
        0, 1:
            state := (1-abs(state)) << core.EEPROMENA
        other:
            return (1-((curr_state >> core.EEPROMENA) & 1)) == 1

    state := ((curr_state & core.EEPROMENA_MASK) | state)
    writereg(core.CONFIG, state)

PUB GetColumn(ptr_buff, col) | tmpframe[2], tmp, offs, line
' Read a single column of pixels from the sensor into ptr_buff
'   NOTE This buffer must be at least 4 words
    if not lookdown(col: 0..15)
        return

    readreg(col * 4, 4, 1, @tmpframe)
    repeat line from 0 to YMAX
        offs := (col * 4) + line
        long[ptr_buff][tmp] := tmpframe.word[offs] signx 15

PUB GetFrame(ptr_buff) | tmpframe[32], offs
' Read entire frame from sensor and store it in buffer at ptr_buff
'   NOTE: This buffer must be at least 64 words
    readreg(0, 64, 1, @tmpframe)
    repeat offs from 0 to 63
        long[ptr_buff][offs] := tmpframe.word[offs] signx 15

PUB GetFrameExt(ptr_buff) | tmpframe[33], offs, line, col
' Read entire frame, as well as PTAT and compensation pixel data from sensor and stores it in buffer at ptr_buff
'   NOTE: This buffer must be at least 66 words
    readreg(0, 66, 1, @tmpframe)
    repeat offs from 0 to 65
        long[ptr_buff][offs] := tmpframe.word[offs] signx 15
    _PTAT := tmpframe[RAM_OFFS_PTAT]            ' Get PTAT data

PUB GetLine(ptr_buff, line) | tmpframe[8], offs, col
' Read a single line of pixels from the sensor into ptr_buff
'   NOTE: This buffer must be at least 16 words
    if not lookdown(line: 0..3)
        return
    readreg(line, 16, 4, @tmpframe)
    repeat col from 0 to XMAX
        offs := (col * 4) + line
        long[ptr_buff][offs] := tmpframe.word[offs] signx 15

PUB GetPixel(ptr_buff, col, line): pix_word | tmpframe, offs
' Read a single pixel from the sensor into ptr_buff
'   Returns: pixel value
'   NOTE: This buffer must be at least 1 word
    case col
        0..XMAX:
        other:
            return

    case line
        0..YMAX:
        other:
            return

    offs := (col * 4) + line                    ' offset within array image

    readreg(offs, 1, 0, @tmpframe)
    long[ptr_buff][offs] := pix_word := tmpframe.word[0] signx 15

PUB Measure()
' Perform measurement, when OpMode is set to SINGLE
'   NOTE: This method waits/blocks while a measurement is ongoing
    writereg(core.CMD_STEP_MEASURE, 0)
    repeat while measuring()

PUB Measuring(): flag
' Flag indicating a measurement is running
'   Returns:
'       FALSE (0): No IR measurement running
'       TRUE (-1): IR measurement running
'   NOTE: This method is intended for use when MeasureMode is set to SINGLE
    readreg(core.CONFIG, 2, 0, @flag)
    return ((flag >> core.MEASURING) & 1) == 1

PUB OpMode(mode=-2): curr_mode
' Set measurement mode
'   Valid values:
'     *CONT (0) - Continuous measurement mode
'      SINGLE (1) - Single-measurement mode only
'   Any other value polls the chip and returns the current setting
'   NOTE: In SINGLE mode, measurements must be triggered manually using the Measure method
    readreg(core.CONFIG, 2, 0, @curr_mode)
    case mode
        CONT, SINGLE:
            mode := (mode << core.MEASMODE)
        other:
            return (curr_mode >> core.MEASMODE) & 1

    mode := ((curr_mode & core.MEASMODE_MASK) | mode)
    writereg(core.CONFIG, mode)

PUB OSCTrim(val=-2): curr_val
' Set Oscillator Trim value
'   Valid values: 0..127 (default: 0)
'   Any other value polls the chip and returns the current setting
'   NOTE: It is recommended to use the factory set value contained in the device's EEPROM.
    case val
        0..127:
            val &= core.OSC_TRIM_MASK
            writereg(core.OSC_TRIM, curr_val)
        other:
            readreg(core.OSC_TRIM, 1, 0, @curr_val)
            return curr_val & core.OSC_TRIM_MASK

PUB Powered(state=-2): curr_state
' Power on sensor
'   Valid values:
'      *FALSE (0) - Sleep
'       TRUE (-1 or 1) - Powered on/normal operation
'   Any other value polls the chip and returns the current setting
    readreg(core.CONFIG, 2, 0, @curr_state)
    case abs(state)
        0, 1:
            ' chip logic is reversed, so flip the bit
            state := (abs(state) ^ 1) << core.OPMODE
        other:
            return (((curr_state >> core.OPMODE) & 1) ^ 1)

    state := ((curr_state & core.OPMODE_MASK) | state)
    writereg(core.CONFIG, state)

PUB ReadEEPROM(): status | ackbit, tries
' Read sensor EEPROM contents into RAM
'   Returns:
'       TRUE (-1): success
'       FALSE (0): failure
    bytefill(@_ee_data, 0, EE_SIZE)             ' clear RAM copy of EEPROM
    tries := 0

    i2c.start()                                 ' try to talk to the EEPROM
    repeat
        ackbit := i2c.write(core.EE_SLAVE_ADDR)
        if (++tries > 3)                        ' give up after 3 tries
            i2c.stop()
            return FALSE
    until (ackbit == i2c.ACK)
    i2c.write($00)

    i2c.start()                                 ' Read in the EEPROM
    repeat
        ackbit := i2c.write(core.EE_SLAVE_ADDR|1)
        if (++tries > 3)
            i2c.stop()
            return FALSE
    until (ackbit == i2c.ACK)
    i2c.rdblock_lsbf(@_ee_data, EE_SIZE, i2c.NAK)
    i2c.stop()
    return TRUE

PUB RefreshRate(rate=-2): curr_rate
' Set sensor refresh rate
'   Valid values are 0, for 0.5Hz, or 1 to 512 in powers of 2 (default: 1)
'   Any other value polls the chip and returns the current setting
'   NOTE: Higher rates will yield noisier images
    readreg(core.CONFIG, 2, 0, @curr_rate)
    case rate
        512, 256, 128, 64, 32, 16, 8, 4, 2, 1, 0:
            rate := lookdownz(rate: 512, 512, 512, 512, 512, 512, 256, 128,{
}           64, 32, 16, 8, 4, 2, 1, 0)
        other:
            curr_rate &= core.REFRATE_BITS
            return lookupz(curr_rate: 512, 512, 512, 512, 512, 512, 256, 128,{
}           64, 32, 16, 8, 4, 2, 1, 0)

    rate := ((curr_rate & core.REFRATE_MASK) | rate) & core.CONFIG_MASK
    writereg(core.CONFIG, rate)

PUB Reset(set=-2): flag
' Set sensor reset flag
'   Valid values: TRUE (-1 or 1)
'   Any other value polls the chip and returns the current setting
'   NOTE: This must be done any time the sensor is initialized, _after_ the configuration register has been updated
'       If FALSE is returned, POR or brown-out has occurred and the process must be repeated
    readreg(core.CONFIG, 2, 0, @flag)
    case abs(set)
        1:
            set := 1 << core.RESET
        other:
            return (((flag >> core.RESET) & 1) == 1)

    set := ((flag & core.RESET_MASK) | set)
    writereg(core.CONFIG, set)

PRI I2CFM(mode=-2): curr_mode
' Enable I2C Fast Mode+
'   Valid values:
'     *TRUE (-1 or 1): Max I2C bus speed 1000kbit/sec
'      FALSE (0): Max I2C bus speed 400kbit/sec
'   NOTE: This is independent of, and has no effect on what speed the driver
'   was started with.
'   Any other value polls the chip and returns the current setting
    readreg(core.CONFIG, 2, 0, @curr_mode)
    case abs(mode)
        0, 1:
            mode := (1-(abs(mode))) << core.I2CFMP
        other:
            return (1-((curr_mode >> core.I2CFMP) & 1)) == 1

    mode := ((curr_mode & core.I2CFMP_MASK) | mode)
    writereg(core.CONFIG, curr_mode)

PRI readReg(reg_nr, nr_reads, rd_step, ptr_buff) | cmd_pkt[2]
' Read nr_reads from device into ptr_buff
    cmd_pkt.byte[0] := SLAVE_WR
    cmd_pkt.byte[1] := core.CMD_READREG
    case reg_nr
        $00..$41:                               ' RAM
            cmd_pkt.byte[2] := reg_nr
            cmd_pkt.byte[3] := rd_step
            cmd_pkt.byte[4] := nr_reads
            nr_reads <<= 1
        $92..$93:                               ' Configuration regs
            cmd_pkt.byte[2] := reg_nr
            cmd_pkt.byte[3] := 0                ' Address step
            cmd_pkt.byte[4] := 1                ' Number of reads
            nr_reads := 2
        other:
            return

    i2c.start()
    i2c.wrblock_lsbf(@cmd_pkt, 5)

    i2c.start()
    i2c.write(SLAVE_RD)
    i2c.rdblock_lsbf(ptr_buff, nr_reads, i2c.NAK)
    i2c.stop()

PRI writeReg(reg_nr, val) | cmd_pkt[2], nr_bytes
' Write val to device
    cmd_pkt.byte[0] := SLAVE_WR
    case reg_nr
        core.CONFIG:
            cmd_pkt.byte[1] := core.CMD_WRITEREG_CFG
            cmd_pkt.byte[2] := val.byte[0] - CFG_CKBYTE
            cmd_pkt.byte[3] := val.byte[0]
            cmd_pkt.byte[4] := val.byte[1] - CFG_CKBYTE
            cmd_pkt.byte[5] := val.byte[1]
            nr_bytes := 6
        core.OSC_TRIM:
            cmd_pkt.byte[1] := core.CMD_WRITEREG_OSCTRIM
            cmd_pkt.byte[2] := val.byte[0] - OSC_CKBYTE
            cmd_pkt.byte[3] := val.byte[0]
            cmd_pkt.byte[4] := val.byte[1] - OSC_CKBYTE
            cmd_pkt.byte[5] := val.byte[1]
            nr_bytes := 6
        core.CMD_STEP_MEASURE:
            cmd_pkt.byte[1] := core.CMD_STEP_MEASURE & $FF
            cmd_pkt.byte[2] := (core.CMD_STEP_MEASURE >> 8) & $FF
            nr_bytes := 3

        other:
            return

    i2c.start()
    i2c.wrblock_lsbf(@cmd_pkt, nr_bytes)
    i2c.stop()

DAT
{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

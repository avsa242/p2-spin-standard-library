{
    --------------------------------------------
    Filename: com.serial.terminal.ansi.spin2
    Description: ANSI-compatible serial terminal,
     using com.serial smartpin-based serial driver
     for low-level async serial.
    Author: Jesse Burt
    Created: Jun 15, 2019
    Updated: Jun 23, 2020
    See end of file for terms of use.
    --------------------------------------------

    NOTE: Based on SmartSerial.spin2, by Eric Smith
}

#include "lib.terminal.spin2"
#include "lib.ansiterminal.spin2"
#include "lib.termwidgets.spin2"

OBJ

    ser : "com.serial"
    int : "string.integer"

PUB Start(baudrate): okay

    okay := ser.Start(baudrate)
    Clear()
    return okay

PUB StartRxTx(rxpin, txpin, mode, baudrate): okay

    return ser.StartRxTx(rxpin, txpin, mode, baudrate)

PUB BinIn(): result
' Read a binary number as a string, converted to a number
'   Returns: Number
    StrInMax(@buffer, MAXSTR_LENGTH)
    return int.StrToBase(@buffer, 2)

PUB Char(ch)
' Send a character
    ser.Char(ch)

PUB CharIn(): result
' Read a character
'   Returns: Byte
    return ser.CharIn()

PUB Chars(ch, nr_chars)
' Send character repeatedly, nr_chars times
    repeat nr_chars
        ser.Char(ch)

PUB DecIn(): result
' Read a decimal number as a string, converted to a number
'   Returns: Number
    StrInMax(@buffer, MAXSTR_LENGTH)
    return int.StrToBase(@buffer, 10)

PUB HexIn(): result
' Read a hexadecimal number as a string, converted to a number
'   Returns: Number
    StrInMax(@buffer, MAXSTR_LENGTH)
    return int.StrToBase(@buffer, 16)

PUB ReadLine(line, maxline): size | c
' Read a string, terminated by a carriage return or line-feed, up to maxline length
    repeat
        case c := CharIn()
            BS:     if size
                        size--
                        ser.Char(c)
            CR, LF: byte[line][size] := 0
                    ser.Char(c)
                    quit
            other:  if size < maxline
                        byte[line][size++] := c
                        ser.Char(c)

PUB RXCheck(): rxbyte
' Check if byte received (never waits)
'   Returns:
'       -1 if no byte, otherwise byte
    return ser.RXCheck()

PUB StrIn(stringptr)
' Read a string
    StrInMax(stringptr, -1)

PUB StrInMax(stringptr, maxcount)
' Read a string, up to maxcount characters
    repeat while (maxcount--)                                   ' While maxcount not reached
        if (byte[stringptr++] := ser.CharIn()) == CR            ' Get chars until CR
            quit
    byte[stringptr+(byte[stringptr-1] == CR)]~                  ' Zero terminate string; overwrite CR or append 0 char


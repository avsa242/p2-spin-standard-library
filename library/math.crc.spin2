{
    --------------------------------------------
    Filename: math.crc.spin2
    Author: Jesse Burt
    Description: A collection of CRC routines
    Copyright (c) 2021
    Started Nov 19, 2017
    Updated Jun 16, 2021
    See end of file for terms of use.
    --------------------------------------------
}

CON

    POLY8_DSMAX     = $8C
    POLY8_MEAS      = $31
    POLY8_SENSIRION = $31
    POLY16_DSMAX    = $A001

PUB MeasCRC8(data, len): crc | currbyte, i, j
' Measurement specialties CRC8
    crc := $00
    repeat i from 0 to len-1
        currbyte := byte[data][(len-1)-i]
        crc := crc ^ currbyte

        repeat j from 0 to 7
            if (crc & $80)
                crc := (crc << 1) ^ POLY8_MEAS
            else
                crc := (crc << 1)
    crc ^= $00
    return crc & $FF

PUB SensirionCRC8(data, len): crc | currbyte, i, j

    crc := $FF
    repeat i from 0 to len-1
        currbyte := byte[data][(len-1)-i]
        crc := crc ^ currbyte

        repeat j from 0 to 7
            if (crc & $80)
                crc := (crc << 1) ^ POLY8_SENSIRION
            else
                crc := (crc << 1)
    crc ^= $00
    return crc & $ff

PUB DallasMaximCRC8(data, len): crc | currbyte, i, j, mix

    crc := $00
    repeat i from 0 to len-1
        currbyte := byte[data][i]
        repeat j from 0 to 7
            mix := (crc ^ currbyte) & $01
            crc >>= 1
            if mix
                crc ^= POLY8_DSMAX
            currbyte >>= 1

    return

PUB DallasMaximCRC16(data, len): crc | currbyte, i, j, mix

    crc := $00
    repeat i from 0 to len-1
        currbyte := byte[data][i]
        repeat j from 0 to 7
            mix := (crc ^ currbyte) & $01
            crc >>= 1
            if mix
                crc ^= POLY16_DSMAX
            currbyte >>= 1

    return

DAT
{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

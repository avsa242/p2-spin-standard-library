{
    --------------------------------------------
    Filename: sensor.temp_rh.si70xx.i2c.spin2
    Author: Jesse Burt
    Description: Driver for Silicon Labs Si70xx-series
        temperature/humidity sensors (P2 version)
    Copyright (c) 2021
    Started Jul 20, 2019
    Updated Jan 3, 2021
    See end of file for terms of use.
    --------------------------------------------
}

CON

    SLAVE_WR          = core.SLAVE_ADDR
    SLAVE_RD          = core.SLAVE_ADDR|1

    DEF_HZ            = 100_000
    I2C_MAX_FREQ      = core.I2C_MAX_FREQ

' Temperature scales
    C = 0
    F = 1

VAR

    byte _temp_scale

OBJ

    i2c : "com.i2c"
    core: "core.con.si70xx.spin"
    crc : "math.crc"

PUB Null()
' This is not a top-level object

PUB Start(SCL_PIN, SDA_PIN, I2C_HZ=DEF_HZ): okay
' Start using custom settings
    if lookdown(SCL_PIN: 0..63) and lookdown(SDA_PIN: 0..63) and{
}   I2C_HZ <= core.I2C_MAX_FREQ
        if okay := i2c.setup(SCL_PIN, SDA_PIN, I2C_HZ)
            waitms(core.TPU)
            if i2c.present(SLAVE_WR)            ' check device bus presence
                reset()
                if lookdown(deviceid(): $0D, $14, $15, $00, $FF)
                    return okay

    return FALSE                                ' something above failed

PUB Stop()

PUB ADCRes(bits): curr_adcres
' Set resolution of readings, in bits
'   Valid values:
'                   RH  Temp
'      *12_14:      12  14 bits
'       8_12:       8   12
'       10_13:      10  13
'       11_11:      11  11
'   Any other value polls the chip and returns the current setting
    curr_adcres := 0
    readreg(core.RD_RH_T_USER1, 1, @curr_adcres)
    case bits
        12_14, 8_12, 10_13, 11_11:
            bits := lookdownz(bits: 12_14, 8_12, 10_13, 11_11)
            bits := lookupz(bits: $00, $01, $80, $81)
        other:
            curr_adcres &= core.ADCRES_BITS
            curr_adcres := lookdownz(curr_adcres: $00, $01, $80, $81)
            return lookupz(curr_adcres: 12_14, 8_12, 10_13, 11_11)

    bits := (curr_adcres & core.ADCRES_MASK) | bits
    writereg(core.WR_RH_T_USER1, 1, @bits)

PUB DeviceID(): id | tmp[2]
' Read the Part number portion of the serial number
'   Returns:
'       $00/$FF: Engineering samples
'       $0D (13): Si7013
'       $14 (20): Si7020
'       $15 (21): Si7021
    serialnum(@tmp)
    return tmp.byte[3]

PUB FirmwareRev(): fwrev
' Read sensor internal firmware revision
'   Returns:
'       $FF: Version 1.0
'       $20: Version 2.0
    readreg(core.RD_FIRMWARE_REV, 1, @fwrev)

PUB HeaterCurrent(htr_curr): curr_htrc
' Set heater current, in milliamperes
'   Valid values: *3, 9, 15, 21, 27, 33, 40, 46, 52, 58, 64, 70, 76, 82, 88, 94
'   Any other value polls the chip and returns the current setting
'   NOTE: Values are approximate, and typical
    case htr_curr
        3, 9, 15, 21, 27, 33, 40, 46, 52, 58, 64, 70, 76, 82, 88, 94:
            htr_curr := lookdownz(htr_curr: 3, 9, 15, 21, 27, 33, 40, 46, 52,{
}           58, 64, 70, 76, 82, 88, 94)
            writereg(core.WR_HEATER, 1, @htr_curr)
        other:
            curr_htrc := 0
            readreg(core.RD_HEATER, 1, @curr_htrc)
            curr_htrc &= core.HEATER_BITS
            return lookupz(curr_htrc: 3, 9, 15, 21, 27, 33, 40, 46, 52, 58,{
}           64, 70, 76, 82, 88, 94)

PUB HeaterEnabled(state): curr_state
' Enable the on-chip heater
'   Valid values: TRUE (-1 or 1), *FALSE (0)
'   Any other value polls the chip and returns the current setting
    curr_state := 0
    readreg(core.RD_RH_T_USER1, 1, @curr_state)
    case abs(state)
        0, 1:
            state := abs(state) << core.HTRE
        other:
            return ((curr_state >> core.HTRE) & 1) == 1

    state := ((curr_state & core.HTRE_MASK) | state) & core.RD_RH_T_USER1_MASK
    writereg(core.WR_RH_T_USER1, 1, @state)

PUB Humidity(): rh
' Read humidity
'   Returns: Relative Humidity, in hundreths of a percent
    rh := 0
    readreg(core.MEAS_RH_NOHOLD, 2, @rh)
    return ((125_00 * rh) / 65536) - 6_00

PUB Reset()
' Perform soft-reset
    writereg(core.RESET, 0, 0)
    waitms(15)

PUB SerialNum(ptr_buff) | sna[2], snb[2]
' Read the 64-bit serial number of the device
    longfill(@sna, 0, 4)
    readreg(core.RD_SERIALNUM_1, 8, @sna)
    readreg(core.RD_SERIALNUM_2, 6, @snb)
    byte[ptr_buff][7] := sna.byte[0]
    byte[ptr_buff][6] := sna.byte[2]
    byte[ptr_buff][5] := sna.byte[4]
    byte[ptr_buff][4] := sna.byte[6]
    byte[ptr_buff][3] := snb.byte[0]            ' Device ID
    byte[ptr_buff][2] := snb.byte[1]
    byte[ptr_buff][1] := snb.byte[3]
    byte[ptr_buff][0] := snb.byte[4]

PUB Temperature(): temp
' Current Temperature, in hundredths of a degree
'   Returns: Integer
'   (e.g., 2105 is equivalent to 21.05 deg C)
    temp := 0
    readreg(core.READ_PREV_TEMP, 2, @temp)
    temp := ((175_72 * temp) / 65536) - 46_85
    case _temp_scale
        F:
            if temp > 0
                temp := temp * 9 / 5 + 32_00
            else
                temp := 32_00 - (abs(temp) * 9 / 5)
        other:
            return

PUB TempScale(temp_scale): curr_scale
' Set scale of temperature data returned by Temperature method
'   Valid values:
'      *C (0): Celsius
'       F (1): Fahrenheit
'   Any other value returns the current setting
    case temp_scale
        F, C:
            _temp_scale := temp_scale
        other:
            return _temp_scale

PRI readReg(reg_nr, nr_bytes, ptr_buff) | cmd_pkt, tmp, crc_in, rt
' Read nr_bytes from the slave device into ptr_buff
    case reg_nr
        core.READ_PREV_TEMP:
            cmd_pkt.byte[0] := SLAVE_WR
            cmd_pkt.byte[1] := reg_nr
            i2c.start()
            repeat tmp from 0 to 1
                i2c.write(cmd_pkt.byte[tmp])
            i2c.wait(SLAVE_RD)
            repeat tmp from nr_bytes-1 to 0     ' read data and NAK last byte
                byte[ptr_buff][tmp] := i2c.read(tmp == 0)
            i2c.stop()
        core.MEAS_RH_NOHOLD:
            cmd_pkt.byte[0] := SLAVE_WR
            cmd_pkt.byte[1] := reg_nr
            i2c.start()
            repeat tmp from 0 to 1
                i2c.write(cmd_pkt.byte[tmp])
            i2c.wait(SLAVE_RD)
            repeat tmp from nr_bytes-1 to 0
                byte[ptr_buff][tmp] := i2c.read(tmp == 0)
' XXX CRC check here
            i2c.stop()
        core.MEAS_TEMP_HOLD:
            cmd_pkt.byte[0] := SLAVE_WR
            cmd_pkt.byte[1] := reg_nr
            i2c.start()
            repeat tmp from 0 to 1
                i2c.write(cmd_pkt.byte[tmp])
            i2c.start()
            i2c.write(SLAVE_RD)
            waitms(11)
            repeat tmp from nr_bytes-1 to 0
                byte[ptr_buff][tmp] := i2c.read(tmp == 0)
            i2c.stop()
        core.MEAS_TEMP_NOHOLD:
            cmd_pkt.byte[0] := SLAVE_WR
            cmd_pkt.byte[1] := reg_nr
            i2c.start()
            repeat tmp from 0 to 1
                i2c.write(cmd_pkt.byte[tmp])
            i2c.wait(SLAVE_RD)
            repeat tmp from nr_bytes-1 to 0
                byte[ptr_buff][tmp] := i2c.read(tmp == 0)
            i2c.stop()
        core.RD_RH_T_USER1, core.RD_HEATER:
            cmd_pkt.byte[0] := SLAVE_WR
            cmd_pkt.byte[1] := reg_nr
            i2c.start()
            repeat tmp from 0 to 1
                i2c.write(cmd_pkt.byte[tmp])
            i2c.wait(SLAVE_RD)
            repeat tmp from nr_bytes-1 to 0
                byte[ptr_buff][tmp] := i2c.read(tmp == 0)
            i2c.stop()
        core.RD_SERIALNUM_1, core.RD_SERIALNUM_2, core.RD_FIRMWARE_REV:
            cmd_pkt.byte[0] := SLAVE_WR
            cmd_pkt.byte[1] := reg_nr.byte[1]
            cmd_pkt.byte[2] := reg_nr.byte[0]
            i2c.start()
            repeat tmp from 0 to 2
                i2c.write(cmd_pkt.byte[tmp])
            i2c.wait(SLAVE_RD)
            waitus(20)                          ' necessary for reliable read
            repeat tmp from 0 to nr_bytes-1
                byte[ptr_buff][tmp] := i2c.read(tmp == nr_bytes-1)
        other:
            return

PRI writeReg(reg_nr, nr_bytes, ptr_buff) | cmd_pkt, tmp
' Write nr_bytes from ptr_buff to the slave device
    case reg_nr
        core.RESET:
            i2c.start()
            i2c.write(SLAVE_WR)
            i2c.write(reg_nr)
            i2c.stop()
        core.WR_RH_T_USER1, core.WR_HEATER:
            cmd_pkt.byte[0] := SLAVE_WR
            cmd_pkt.byte[1] := reg_nr
            cmd_pkt.byte[2] := byte[ptr_buff][0]
            i2c.start()
            repeat tmp from 0 to 2
                i2c.write(cmd_pkt.byte[tmp])
            i2c.stop()
        other:
            return

DAT
{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

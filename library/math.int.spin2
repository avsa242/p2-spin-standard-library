{
    --------------------------------------------
    Filename: math.int.spin
    Author: Jesse Burt
    Description: Basic integer math functions
    Copyright (c) 2022
    Started Apr 11, 2021
    Updated Sep 18, 2022
    See end of file for terms of use.
    --------------------------------------------
}
CON

    SCALE       = 1_000
    FULLCIRCLE  = (360 * SCALE)

VAR

    long _rndseed

PUB null()
' This is not a top-level object

PUB atan2(y, x): r | angle
' 2-argument arctangent
    _, angle := xypol(x, y)
    return angle sca FULLCIRCLE

PUB cos(angle): cosine
' Return the cosine of angle
    org
        shl     angle,      #20
        qrotate ##100000,   angle
        getqx   cosine
    end
    return

PUB log2(num): l2
' Return base-2 logarithm of num
    return encod(num)

PUB pow(base, exp): r
' Return base raised to the power of exp
    r := base
    exp--
    repeat exp
        r *= base

PUB rnd(maxval): ur
' Return random unsigned number up to maxval
    return abs(getrnd()) +// maxval

PUB rnds(maxval): sr
' Return random signed number up to maxval
    return getrnd() +// maxval

PUB rndi(maxval): ur
' Return random unsigned number up to maxval, inclusive
    return abs(getrnd()) +// (maxval+1)

PUB rndsi(maxval): sr
' Return random signed number up to maxval, inclusive
    return getrnd() +// (maxval+1)

PUB sin(angle): sine
' Return the sine of angle
    org
        shl     angle, #20
        qrotate ##100000, angle
        getqy   sine
    end
    return

DAT
{
Copyright 2022 Jesse Burt

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
associated documentation files (the "Software"), to deal in the Software without restriction,
including without limitation the rights to use, copy, modify, merge, publish, distribute,
sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or
substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT
NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT
OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}


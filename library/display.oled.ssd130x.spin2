{
    --------------------------------------------
    Filename: display.oled.ssd130x.spin2
    Description: Driver for Solomon Systech SSD130x OLED displays
    Author: Jesse Burt
    Copyright (c) 2022
    Created: Apr 26, 2018
    Updated: Nov 5, 2022
    See end of file for terms of use.
    --------------------------------------------
}
#define 1BPP
#define MEMMV_NATIVE bytemove
#include "graphics.common.spin2h"

{ if a specific display controller isn't defined, default to SSD1306 }
#ifndef SSD1306
#ifndef SSD1309
#define SSD1306
#endif
#endif
CON

    SLAVE_WR        = core.SLAVE_ADDR
    SLAVE_RD        = core.SLAVE_ADDR|1

    DEF_HZ          = 100_000
    MAX_COLOR       = 1
    BYTESPERPX      = 1

' States for D/C pin
    DATA            = 1
    CMD             = 0

' Display visibility modes
    NORMAL          = 0
    ALL_ON          = 1
    INVERTED        = 2

' Addressing modes
    HORIZ           = 0
    VERT            = 1
    PAGE            = 2

OBJ

    core: "core.con.ssd130x"

#ifdef SSD130X_SPI
    spi : "com.spi.25mhz"                             ' SPI engine (up to ~16MHz)

#else

{ default to I2C }
#define SSD130X_I2C
    i2c : "com.i2c"                             ' I2C engine (~1MHz)

#endif

VAR

    long _CS, _DC, _RES
    byte _addr_bits

PUB null()
' This is not a top-level object

#ifdef SSD130X_I2C
PUB startx(SCL_PIN, SDA_PIN, RES_PIN, I2C_HZ, ADDR_BITS, WIDTH, HEIGHT, ptr_dispbuff): status
' Start the driver with custom I/O settings
'   SCL_PIN: 0..63
'   SDA_PIN: 0..63
'   RES_PIN: 0..31 (optional; use -1 to disable)
'   I2C_HZ: max official is 400_000 (unenforced, YMMV!)
'   SLAVE_LSB: 0, 1
'   WIDTH: 96, 128
'   HEIGHT: 32, 64
    if lookdown(SCL_PIN: 0..63) and lookdown(SDA_PIN: 0..63)
        if (status := i2c.init(SCL_PIN, SDA_PIN, I2C_HZ))
            waitus(core.TPOR)                   ' wait for device startup
            _addr_bits := abs(ADDR_BITS == 1) << 1 ' slave address bit option
            _RES := RES_PIN                     ' -1 to disable
            reset()
            if i2c.present(SLAVE_WR | _addr_bits)  ' test device bus presence
                _disp_width := width
                _disp_height := height
                _disp_xmax := _disp_width-1
                _disp_ymax := _disp_height-1
                ' calc display memory usage from dimensions and 1bpp depth
                _buff_sz := (_disp_width * _disp_height) / 8
                _bytesperln := _disp_width * BYTESPERPX

                address(ptr_dispbuff)           ' set display buffer address
                return
    ' if this point is reached, something above failed
    ' Re-check I/O pin assignments, bus speed, connections, power
    ' Lastly - make sure you have at least one free core/cog
    return FALSE
#elseifdef SSD130X_SPI
PUB startx(CS_PIN, SCK_PIN, SDIN_PIN, DC_PIN, RES_PIN, SCK_HZ, WIDTH, HEIGHT, ptr_dispbuff): status
' Start the driver with custom I/O settings
'   CS_PIN: 0..63
'   SCK_PIN: 0..63
'   SDIN_PIN: 0..63
'   DC_PIN: 0..63
'   RES_PIN: 0..63 (optional; use -1 to disable)
'   SCK_HZ: max official is 10_000_000 (YMMV!)
'   WIDTH: 96, 128
'   HEIGHT: 32, 64
    if lookdown(CS_PIN: 0..63) and lookdown(SCK_PIN: 0..63) and {
}   lookdown(SDIN_PIN: 0..63) and lookdown(DC_PIN: 0..63)
        if (status := spi.init(SCK_PIN, SDIN_PIN, -1, core.SPI_MODE, {
}       SCK_HZ))
            waitus(core.TPOR)                   ' wait for device startup
            _CS := CS_PIN
            _DC := DC_PIN
            _RES := RES_PIN                     ' -1 to disable
            reset()

            pinh(_CS)
            pinh(_DC)
            _disp_width := WIDTH
            _disp_height := HEIGHT
            _disp_xmax := _disp_width-1
            _disp_ymax := _disp_height-1
            ' calc display memory usage from dimensions and 1bpp depth
            _buff_sz := (_disp_width * _disp_height) / 8
            _bytesperln := _disp_width * BYTESPERPX

            address(ptr_dispbuff)               ' set display buffer address
            return
    ' if this point is reached, something above failed
    ' Re-check I/O pin assignments, bus speed, connections, power
    ' Lastly - make sure you have at least one free core/cog
    return FALSE
#endif

PUB stop()
' Stop the driver
    powered(FALSE)
#ifdef SSD130X_I2C
    i2c.deinit()
#elseifdef SSD130X_SPI
    spi.deinit()
#endif

PUB defaults()
' Apply power-on-reset default settings
#ifndef HAS_RESET
    ' this code will be called only if HAS_RESET isn't defined at build-time
    ' define it if the display's reset pin is to be controlled by GPIO
    '   or if it's tied to the MCU's reset pin or similar
    powered(FALSE)
    disp_lines(64)
    disp_start_line(0)
    chg_pump_voltage(7_500)
    addr_mode(PAGE)
    contrast(127)
    visibility(NORMAL)
    draw_area(0, 0, 127, 63)
    powered(TRUE)
#else
    reset()
#endif

PUB preset_128x()
' Preset: 128px wide, determine settings for height at runtime
    disp_lines(_disp_height)
    disp_start_line(0)
    chg_pump_voltage(7_500)
    addr_mode(HORIZ)
    visibility(NORMAL)
    case _disp_height
        32:
            com_pin_cfg(0, 0)
        64:
            com_pin_cfg(1, 0)
        other:
            com_pin_cfg(0, 0)
    powered(TRUE)

PUB preset_128x32()
' Preset: 128px wide, setup for 32px height
    disp_lines(32)
    disp_start_line(0)
    chg_pump_voltage(7_500)
    addr_mode(HORIZ)
    visibility(NORMAL)
    com_pin_cfg(0, 0)
    powered(TRUE)

PUB preset_128x64()
' Preset: 128px wide, setup for 64px height
    disp_lines(64)
    disp_start_line(0)
    chg_pump_voltage(7_500)
    addr_mode(HORIZ)
    visibility(NORMAL)
    com_pin_cfg(1, 0)
    powered(TRUE)

PUB address(addr): curr_addr
' Set framebuffer address
    case addr
        $0004..$7FFF-_buff_sz:
            _ptr_drawbuffer := addr
        other:
            return _ptr_drawbuffer

PUB addr_mode(mode)
' Set Memory Addressing Mode
'   Valid values:
'       0: Horizontal addressing mode
'       1: Vertical
'      *2: Page
'   Any other value is ignored
    case mode
        HORIZ, VERT, PAGE:
            writereg(core.MEM_ADDRMODE, 1, mode)
        other:
            return

#ifdef GFX_DIRECT
PUB bitmap(ptr_bmap, sx, sy, ex, ey) | bm_sz
' Display bitmap
'   ptr_bmap: pointer to bitmap data
'   (sx, sy): upper-left corner of bitmap
'   (ex, ey): lower-right corner of bitmap
    draw_area(sx, sy, ex, ey)
    bm_sz := ((ex-sx) * (ey-sy)) / 8

    wr_buffer(ptr_bmap, bm_sz)
#endif

#ifdef GFX_DIRECT
PUB tx = putchar
PUB char = putchar
PUB putchar(ch) | ch_offs
' Draw a character from the loaded font
    ch_offs := _font_addr + (ch << 3)
    draw_area(_charpx_x, _charpx_y, _charpx_x+_charcell_w, _charpx_y+_charcell_h)

    wr_buffer(ch_offs, _charcell_w)

    _charpx_x += _charcell_w                    ' go to next column
    if (_charpx_x > _charpx_xmax)               ' last col?
        _charpx_x := 0                          ' go to first col of
        _charpx_y += _charcell_h                '   next line
        if (_charpx_y > _charpx_ymax)           ' last col of last row?
            _charpx_x := _charpx_y := 0         ' wrap to beginning of disp
#endif

PUB chg_pump_voltage(v)
' Set charge pump regulator voltage, in millivolts
'   Valid values:
'       0 (off), 6_000, *7_500, 8_500, 9_000
'   Any other value is ignored
'   NOTE: This must be called before display power is enabled with Powered()
#ifdef SSD1306
    ' the SSD1309 doesn't have an internal charge pump,
    ' so only build this code for the SSD1306
    case v
        0_000:
            v := core.CHGP_OFF
        6_000:
            v := core.CHGP_6000
        7_500:
            v := core.CHGP_7500
        8_500:
            v := core.CHGP_8500
        9_000:
            v := core.CHGP_9000
        other:
            return

    writereg(core.CHGPUMP, 1, v)
#endif

PUB clear()
' Clear the display
#ifdef GFX_DIRECT
#ifdef SSD130X_I2C
    i2c.start
    i2c.write(SLAVE_WR | _addr_bits)
    i2c.wr_byte(core.CTRLBYTE_DATA)
    repeat _buff_sz
        i2c.wr_byte(_bgcolor)
    i2c.stop
#elseifdef SSD130X_SPI
    pinw(_DC, DATA)
    pinl(_CS)
    spi.wrbytex(_bgcolor, _buff_sz)
    pinh(_CS)
#endif
#else
    bytefill(_ptr_drawbuffer, _bgcolor, _buff_sz)
#endif

PUB clk_freq(freq)
' Set display internal oscillator frequency, in kHz
'   Valid values: (disply-specific)
'       SSD1306:
'           333, 337, 342, 347, 352, 357, 362, 367, 372, 377, 382, 387, 392,
'           397, 402, 407
'       SSD1309:
'           360, 372, 384, 396, 408, 420, 432, 444, 456, 468, 480, 492, 504,
'           516, 528, 540
'   Any other value is ignored
'   NOTE: Range is interpolated, based solely on the range specified in the
'   datasheet, divided into 16 steps
    case freq
        core.FOSC_MIN..core.FOSC_MAX:
#ifdef SSD1306
            freq := ((freq / 5) - 66) << core.OSCFREQ
#elseifdef SSD1309
            freq := ((freq / 12) - 30) << core.OSCFREQ
#endif
            writereg(core.SETOSCFREQ, 1, freq)
        other:
            return

PUB com_pin_cfg(pin_config, remap) | config
' Set COM Pins Hardware Configuration and Left/Right Remap
'   Valid values:
'       pin_config: 0: Sequential                      1: Alternative (POR)
'       remap:      0: Disable Left/Right remap (POR)  1: Enable remap
'   Any other value sets the default value
    config := %0000_0010    ' XXX use named constant/clarify
    case pin_config
        0:
        other:
            config := config | (1 << 4)

    case remap
        1:
            config := config | (1 << 5)
        other:

    writereg(core.SETCOM_CFG, 1, config)

PUB contrast(level)
' Set Contrast Level
'   Valid values: 0..255 (clamped to range)
    level := (0 #> level <# 255)
    writereg(core.CONTRAST, 1, level)

PUB draw_area(sx, sy, ex, ey)
' Set displayable area
    ifnot (lookup(sx: 0..127) or lookup(sy: 0..63) or lookup(ex: 0..127) or lookup(ey: 0..63))
        return

    sy >>= 3                                    ' convert y-coordinates to
    ey >>= 3                                    '   page numbers
    writereg(core.SET_COLADDR, 2, (ex << 8) | sx)
    writereg(core.SET_PAGEADDR, 2, (ey << 8) | sy)

PUB invert_colors(state) | tmp
' Invert display colors
    if (state)
        visibility(INVERTED)
    else
        visibility(NORMAL)

PUB disp_lines(lines)
' Set total number of display lines
'   Valid values: 16..64
'   Typical values: 32, 64
'   Any other value is ignored
    lines := ((16 #> lines <# 64) - 1)
    writereg(core.SETMUXRATIO, 1, lines)

PUB disp_offset(offset)
' Set display offset/vertical shift
'   Valid values: 0..63 (default: 0)
'   Any other value sets the default value
    offset := (0 #> offset <# 63)
    writereg(core.SETDISPOFFS, 1, offset)

PUB disp_start_line(line)
' Set Display Start Line
'   Valid values: 0..63 (default: 0)
'   Any other value sets the default value
    line := (0 #> line <# 63)
    writereg(core.DISP_STLINE, 0, line)

PUB visibility(mode)
' Set display visibility
    case mode
        NORMAL:
            writereg(core.RAMDISP_ON, 0, 0)
            writereg(core.DISP_NORM, 0, 0)
        ALL_ON:
            writereg(core.RAMDISP_ON, 0, 1)
        INVERTED:
            writereg(core.DISP_NORM, 0, 1)
        other:
            return

PUB mirror_h(state)
' Mirror display, horizontally
'   Valid values: TRUE (non-zero), *FALSE (0)
'   Any other value is ignored
'   NOTE: Takes effect only after next display update
    state := ((state <> 0) & 1)
    writereg(core.SEG_MAP0, 0, ((state <> 0) & 1))

PUB mirror_v(state)
' Mirror display, vertically
'   Valid values: TRUE (non-zero), *FALSE (0)
'   Any other value is ignored
'   NOTE: Takes effect only after next display update
    if (state)
        state := 8
    writereg(core.COMDIR_NORM, 0, state)

PUB plot(x, y, color)
' Plot pixel at (x, y) in color
    if (x < 0 or x > _disp_xmax) or (y < 0 or y > _disp_ymax)
        return                                  ' coords out of bounds, ignore
#ifdef GFX_DIRECT
' direct to display
'   (not implemented)
#else
' buffered display
    case color
        1:
            byte[_ptr_drawbuffer][x + (y>>3) * _disp_width] |= (|< (y&7))
        0:
            byte[_ptr_drawbuffer][x + (y>>3) * _disp_width] &= !(|< (y&7))
        -1:
            byte[_ptr_drawbuffer][x + (y>>3) * _disp_width] ^= (|< (y&7))
        other:
            return
#endif

#ifndef GFX_DIRECT
PUB point(x, y): pix_clr
' Get color of pixel at x, y
    x := 0 #> x <# _disp_xmax
    y := 0 #> y <# _disp_ymax

    return (byte[_ptr_drawbuffer][(x + (y >> 3) * _disp_width)] & (1 << (y & 7)) <> 0) * -1
#endif

PUB powered(state) | tmp
' Enable display power
    state := (((state <> 0) & 1) + core.DISP_OFF)
    writereg(state, 0, 0)

PUB precharge_period(phs1_clks, phs2_clks)
' Set display refresh pre-charge period, in display clocks
'   Valid values: 1..15 (clamped to range)
    phs1_clks := (1 #> phs1_clks <# 15)
    phs2_clks := (1 #> phs2_clks <# 15)
    writereg(core.SETPRECHARGE, 1, (phs2_clks << 4) | phs1_clks)

PUB reset()
' Reset the display controller
    if (lookdown(_RES: 0..63))
        pinh(_RES)
        waitus(3)
        pinl(_RES)
        waitus(3)
        pinh(_RES)

PUB show() | tmp
' Write display buffer to display
    draw_area(0, 0, _disp_xmax, _disp_ymax)

#ifdef SSD130X_I2C
    i2c.start()
    i2c.wr_byte(SLAVE_WR | _addr_bits)
    i2c.wr_byte(core.CTRLBYTE_DATA)
    i2c.wrblock_lsbf(_ptr_drawbuffer, _buff_sz)
    i2c.stop()
#elseifdef SSD130X_SPI
    pinw(_DC, DATA)
    pinl(_CS)
    spi.wrblock_lsbf(_ptr_drawbuffer, _buff_sz)
    pinh(_CS)
#endif

PUB vcomh_voltage(level)
' Set COM output voltage, in millivolts
'   Valid values:
'       SSD1306:
'           0_650: 0.65 * Vcc
'          *0_770: 0.77 * Vcc
'           0_830: 0.83 * Vcc
'       SSD1309:
'           0_640: 0.64 * Vcc
'          *0_780: 0.78 * Vcc
'           0_840: 0.84 * Vcc
'   Any other value is ignored
    case level
#ifdef SSD1306
        0_650:
            level := %000 << core.VCOMH
        0_770:
            level := %010 << core.VCOMH
        0_830:
            level := %011 << core.VCOMH
#elseifdef SSD1309
        0_640:
            level := %0000 << core.VCOMH
        0_780:
            level := %1101 << core.VCOMH
        0_840:
            level := %1111 << core.VCOMH
#endif
        other:
            return

    writereg(core.SETVCOMDESEL, 1, level)

PUB wr_buffer(ptr_buff, buff_sz) | tmp
' Write alternate buffer to display
'   buff_sz: bytes to write
'   ptr_buff: address of buffer to write to display
'   NOTE: Does not set position on display
#ifdef SSD130X_I2C
    i2c.start()
    i2c.wr_byte(SLAVE_WR | _addr_bits)
    i2c.wr_byte(core.CTRLBYTE_DATA)
    i2c.wrblock_lsbf(ptr_buff, buff_sz)
    i2c.stop()
#elseifdef SSD130X_SPI
    pinw(_DC, DATA)
    pinl(_CS)
    spi.wrblock_lsbf(ptr_buff, buff_sz)
    pinh(_CS)
#endif

#ifndef GFX_DIRECT
PRI memfill(xs, ys, val, count)
' Fill region of display buffer memory
'   xs, ys: Start of region
'   val: Color
'   count: Number of consecutive memory locations to write
    bytefill(_ptr_drawbuffer + (xs + (ys * _bytesperln)), val, count)
#endif

PRI writereg(reg_nr, nr_bytes, val) | cmd_pkt[2], tmp, ackbit
' Write nr_bytes from val to device
#ifdef SSD130X_I2C
    cmd_pkt.byte[0] := SLAVE_WR | _addr_bits
    cmd_pkt.byte[1] := core.CTRLBYTE_CMD
    case nr_bytes
        0:
            cmd_pkt.byte[2] := reg_nr | val 'Simple command
            nr_bytes := 3
        1:
            cmd_pkt.byte[2] := reg_nr       'Command w/1-byte argument
            cmd_pkt.byte[3] := val
            nr_bytes := 4
        2:
            cmd_pkt.byte[2] := reg_nr       'Command w/2-byte argument
            cmd_pkt.byte[3] := val & $FF
            cmd_pkt.byte[4] := (val >> 8) & $FF
            nr_bytes := 5
        other:
            return

    i2c.start()
    i2c.wrblock_lsbf(@cmd_pkt, nr_bytes)
    i2c.stop()
#elseifdef SSD130X_SPI
    case nr_bytes
        0:
            cmd_pkt.byte[0] := reg_nr | val 'Simple command
            nr_bytes := 1
        1:
            cmd_pkt.byte[0] := reg_nr       'Command w/1-byte argument
            cmd_pkt.byte[1] := val
            nr_bytes := 2
        2:
            cmd_pkt.byte[0] := reg_nr       'Command w/2-byte argument
            cmd_pkt.byte[1] := val & $FF
            cmd_pkt.byte[2] := (val >> 8) & $FF
            nr_bytes := 3
        other:
            return

    pinw(_DC, CMD)
    pinl(_CS)
    spi.wrblock_lsbf(@cmd_pkt, nr_bytes)
    pinh(_CS)
#endif

DAT
{
Copyright 2022 Jesse Burt

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
associated documentation files (the "Software"), to deal in the Software without restriction,
including without limitation the rights to use, copy, modify, merge, publish, distribute,
sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or
substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT
NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT
OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}


{                                                                                                                
    --------------------------------------------
    Filename: W25Qxxx-Test.spin2
    Author: Jesse Burt
    Description: Test app for the W25Qxxx driver
    Copyright (c) 2019
    Started Nov 09, 2019
    Updated Nov 10, 2019
    See end of file for terms of use.
    --------------------------------------------
}

CON

    XTAL        = cfg#XTAL
    XDIV        = cfg#XDIV
    XMUL        = cfg#XMUL
    XDIVP       = cfg#XDIVP
    XOSC        = cfg#XOSC
    XSEL        = cfg#XSEL
    XPPPP       = cfg#XPPPP
    CLOCKFREQ   = cfg#CLOCKFREQ
    SETFREQ     = cfg#SETFREQ
    ENAFREQ     = cfg#ENAFREQ

    LED         = cfg#LED1
    SER_RX      = cfg#SER_RX
    SER_TX      = cfg#SER_TX
    SER_BAUD    = 2_000_000

    CS          = 61
    SCK         = 60
    MOSI        = 59
    MISO        = 58

OBJ

    ser         : "com.serial.terminal.ansi"
    cfg         : "core.con.boardcfg.p2eval"
    io          : "io"
    time        : "time"
    mem         : "memory.flash.w25qxxx.spi"

VAR

    long _ser_cog, _mem_cog

PUB Main | uid[2], base, seg, offs, rd_tmp, i, tmp

    Setup

    ser.printf("Dev ID: %x\n", mem.DeviceID)
    ser.printf("Mfr ID: %x\n", mem.ManufacturerID)
    ser.printf("JEDEC ID: %x\n", mem.JEDECID)
    mem.UID(@uid)
    ser.printf("UID: %x%x\n", uid[0], uid[1])

    mem.WritesEnabled(FALSE)
    ser.printf("Writes enabled? %d\n", mem.WritesEnabled)

    seg := base := 0
    ser.position(0, 10)

    repeat
        ser.printf("%x: ", base + seg)
        repeat offs from 0 to 15
            rd_tmp := mem.ReadByte(base + seg + offs)
            ser.hex(rd_tmp, 2)
            ser.char(" ")
        ser.newline
        seg += 16
        if seg > 255
            seg := 0
            case ser.CharIn
                "[":
                    base := base - 256
                    if base < 0
                        base := 0
                "]":
                    base := base + 256
                    if base > $FF_FF_00
                        base := $FF_FF_00
                "e":
                    base := $FF_FF_00
                "s":
                    base := 0
                "x":
                    EraseSector(base)
                "w":
                    WriteTest(base)
                "q":
                    ser.printf("Halting\n")
                    quit
                OTHER:
            ser.Position(0, 10)

    Flash(LED, 100)     ' Signal execution finished

PUB EraseSector(start_addr) | s, e

    mem.WritesEnabled(TRUE)
    MessageWritesEna

    ser.printf("Erasing...")
    s := cnt
    mem.Erase(mem#SECTOR, start_addr)
    repeat until not mem.Busy
    e := cnt-s
    ser.printf("done (%d cycles).\n", e)
    ser.Clear(ser#CLR_CUR_TO_END)

PUB WriteTest(start_addr) | i, s, e, tmp

    mem.WritesEnabled(TRUE)
    MessageWritesEna

    tmp := $DE_AD_BE_EF
    ser.printf("Writing test value...")
    s := cnt
    repeat i from start_addr to start_addr + 3
        mem.WritesEnabled(TRUE)
        mem.WriteByte(i, tmp.byte[3-i])
    e := cnt-s
    ser.printf("done (%d cycles).\n", e)
    ser.Clear(ser#CLR_CUR_TO_END)

PUB MessageWritesEna

    ser.printf("Writes enabled? ")
    case mem.WritesEnabled
        TRUE:
            ser.printf("Yes\n")
        FALSE:
            ser.printf("No\n")

PUB Setup
    clkset(ENAFREQ, CLOCKFREQ, XSEL)
    repeat until _ser_cog := ser.StartRXTX (SER_RX, SER_TX, 0, SER_BAUD)
    ser.Clear
    ser.PrintF("Serial terminal started\n")
    if _mem_cog := mem.Startx (CS, SCK, MOSI, MISO, 5_000_000, 0)
        ser.Printf("W25Qxxx driver started\n")
    else
        ser.Printf("W25Qxxx driver failed to start - halting\n")
        Flash(LED, 500)

PUB Flash(led_pin, delay_ms)

    io.Output(led_pin)
    repeat
        io.Toggle(led_pin)
        time.MSleep(delay_ms)

DAT
{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}
